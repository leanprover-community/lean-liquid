import topology.category.Profinite.projective
import for_mathlib.Profinite.disjoint_union
import for_mathlib.concrete_equalizer

noncomputable theory

open category_theory

universes u w v

structure ExtrDisc :=
(val : Profinite.{u})
[cond : projective val]

namespace ExtrDisc

@[ext]
structure hom (X Y : ExtrDisc) := mk :: (val : X.val ‚ü∂ Y.val)

def of (X : Profinite) [projective X] : ExtrDisc := ‚ü®X‚ü©

@[simp]
lemma of_val (X : Profinite) [projective X] : (of X).val = X := rfl

@[simps]
instance : category ExtrDisc :=
{ hom := hom,
  id := Œª X, ‚ü®ùüô _‚ü©,
  comp := Œª X Y Z f g, ‚ü®f.val ‚â´ g.val‚ü© }

@[simps]
def _root_.ExtrDisc_to_Profinite : ExtrDisc ‚•§ Profinite :=
{ obj := val,
  map := Œª X Y f, f.val }

instance : full ExtrDisc_to_Profinite := { preimage := Œª X Y f, ‚ü®f‚ü© }

instance : faithful ExtrDisc_to_Profinite := { }

instance : concrete_category ExtrDisc.{u} :=
{ forget := ExtrDisc_to_Profinite ‚ãô forget _,
  forget_faithful := ‚ü®‚ü© }

instance : has_coe_to_sort ExtrDisc Type* :=
concrete_category.has_coe_to_sort _

instance {X Y : ExtrDisc} : has_coe_to_fun (X ‚ü∂ Y) (Œª f, X ‚Üí Y) :=
‚ü®Œª f, f.val‚ü©

@[simp]
lemma coe_fun_eq {X Y : ExtrDisc} (f : X ‚ü∂ Y) (x : X) :
  f x = f.val x := rfl

instance (X : ExtrDisc) : projective X.val := X.cond

example (X : ExtrDisc) : projective (ExtrDisc_to_Profinite.obj X) :=
by { dsimp, apply_instance }

def lift {X Y : Profinite} {P : ExtrDisc} (f : X ‚ü∂ Y)
  (hf : function.surjective f) (e : P.val ‚ü∂ Y) : P.val ‚ü∂ X :=
begin
  haveI : epi f := by rwa Profinite.epi_iff_surjective f,
  choose g h using projective.factors e f,
  exact g,
end

@[simp, reassoc]
lemma lift_lifts {X Y : Profinite} {P : ExtrDisc} (f : X ‚ü∂ Y)
  (hf : function.surjective f) (e : P.val ‚ü∂ Y) :
  lift f hf e ‚â´ f = e :=
begin
  haveI : epi f := by rwa Profinite.epi_iff_surjective f,
  apply (projective.factors e f).some_spec,
end

instance (X : ExtrDisc) : topological_space X :=
show topological_space X.val, by apply_instance

instance (X : ExtrDisc) : compact_space X :=
show compact_space X.val, by apply_instance

instance (X : ExtrDisc) : t2_space X :=
show t2_space X.val, by apply_instance

instance (X : ExtrDisc) : totally_disconnected_space X :=
show totally_disconnected_space X.val, by apply_instance

.-- move this
-- @[simps]
def _root_.Profinite.sum_iso_coprod (X Y : Profinite.{u}) :
  Profinite.sum X Y ‚âÖ X ‚®ø Y :=
{ hom := Profinite.sum.desc _ _ limits.coprod.inl limits.coprod.inr,
  inv := limits.coprod.desc (Profinite.sum.inl _ _) (Profinite.sum.inr _ _),
  hom_inv_id' := by { apply Profinite.sum.hom_ext;
    simp only [‚Üê category.assoc, category.comp_id, Profinite.sum.inl_desc,
      limits.coprod.inl_desc, Profinite.sum.inr_desc, limits.coprod.inr_desc] },
  inv_hom_id' := by { apply limits.coprod.hom_ext;
    simp only [‚Üê category.assoc, category.comp_id, Profinite.sum.inl_desc,
      limits.coprod.inl_desc, Profinite.sum.inr_desc, limits.coprod.inr_desc] } }

@[simps]
def sum (X Y : ExtrDisc.{u}) : ExtrDisc.{u} :=
{ val := Profinite.sum X.val Y.val,
  cond := begin
    let Z := Profinite.sum X.val Y.val,
    apply projective.of_iso (Profinite.sum_iso_coprod X.val Y.val).symm,
    apply_instance,
  end }

@[simps]
def sum.inl (X Y : ExtrDisc) : X ‚ü∂ sum X Y :=
‚ü®Profinite.sum.inl _ _‚ü©

@[simps]
def sum.inr (X Y : ExtrDisc) : Y ‚ü∂ sum X Y :=
‚ü®Profinite.sum.inr _ _‚ü©

@[simps]
def sum.desc {X Y Z : ExtrDisc} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
  sum X Y ‚ü∂ Z :=
‚ü®Profinite.sum.desc _ _ f.val g.val‚ü©

@[simp]
lemma sum.inl_desc {X Y Z : ExtrDisc} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
  sum.inl X Y ‚â´ sum.desc f g = f :=
by { ext1, dsimp, simp }

@[simp]
lemma sum.inr_desc {X Y Z : ExtrDisc} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
  sum.inr X Y ‚â´ sum.desc f g = g :=
by { ext1, dsimp, simp }

@[ext]
lemma sum.hom_ext {X Y Z : ExtrDisc} (f g : sum X Y ‚ü∂ Z)
  (hl : sum.inl X Y ‚â´ f = sum.inl X Y ‚â´ g)
  (hr : sum.inr X Y ‚â´ f = sum.inr X Y ‚â´ g) : f = g :=
begin
  ext1,
  apply Profinite.sum.hom_ext,
  { apply_fun (Œª e, e.val) at hl, exact hl },
  { apply_fun (Œª e, e.val) at hr, exact hr }
end

-- move this
def _root_.Profinite.empty_is_initial : limits.is_initial Profinite.empty.{u} :=
@limits.is_initial.of_unique.{u} _ _ _ (Œª Y, ‚ü®‚ü®Profinite.empty.elim _‚ü©, Œª f, by { ext, cases x, }‚ü©)

@[simps]
def empty : ExtrDisc :=
{ val := Profinite.empty,
  cond := begin
    let e : Profinite.empty ‚âÖ ‚ä•_ _ :=
    Profinite.empty_is_initial.unique_up_to_iso limits.initial_is_initial,
    apply projective.of_iso e.symm,
    -- apply_instance, <-- missing instance : projective (‚ä•_ _)
    constructor,
    introsI A B f g _,
    refine ‚ü®limits.initial.to A, by simp‚ü©,
  end }

@[simps]
def empty.elim (X : ExtrDisc) : empty ‚ü∂ X :=
‚ü®Profinite.empty.elim _‚ü©

@[ext]
lemma empty.hom_ext {X : ExtrDisc} (f g : empty ‚ü∂ X) : f = g :=
by { ext x, cases x }

def sigma {Œπ : Type u} [fintype Œπ] (X : Œπ ‚Üí ExtrDisc) : ExtrDisc :=
{ val := Profinite.sigma $ Œª i, (X i).val,
  cond := begin
    let e : Profinite.sigma (Œª i, (X i).val) ‚âÖ ‚àê (Œª i, (X i).val) :=
      (Profinite.sigma_cofan_is_colimit _).cocone_point_unique_up_to_iso
      (limits.colimit.is_colimit _),
    apply projective.of_iso e.symm,
    apply_instance,
  end }

def sigma.Œπ {Œπ : Type u} [fintype Œπ] (X : Œπ ‚Üí ExtrDisc) (i) :
  X i ‚ü∂ sigma X := ‚ü®Profinite.sigma.Œπ _ i‚ü©

def sigma.desc {Œπ : Type u} {Y : ExtrDisc} [fintype Œπ] (X : Œπ ‚Üí ExtrDisc)
  (f : Œ† i, X i ‚ü∂ Y) : sigma X ‚ü∂ Y := ‚ü®Profinite.sigma.desc _ $ Œª i, (f i).val‚ü©

lemma sigma.Œπ_desc {Œπ : Type u} {Y : ExtrDisc} [fintype Œπ] (X : Œπ ‚Üí ExtrDisc)
  (f : Œ† i, X i ‚ü∂ Y) (i) : sigma.Œπ X i ‚â´ sigma.desc X f = f i :=
begin
  ext1,
  apply Profinite.sigma.Œπ_desc,
end

lemma sigma.hom_ext {Œπ : Type u} {Y : ExtrDisc} [fintype Œπ] (X : Œπ ‚Üí ExtrDisc)
  (a b : sigma X ‚ü∂ Y) (w : ‚àÄ i, sigma.Œπ X i ‚â´ a = sigma.Œπ X i ‚â´ b) : a = b :=
begin
  ext1,
  apply Profinite.sigma.hom_ext,
  intros i,
  specialize w i,
  apply_fun (Œª e, e.val) at w,
  exact w,
end

lemma sigma.Œπ_jointly_surjective {Œπ : Type u} [fintype Œπ] (X : Œπ ‚Üí ExtrDisc)
  (x : sigma X) : ‚àÉ i (t : X i), sigma.Œπ X i t = x :=
Profinite.sigma.Œπ_jointly_surjective _ _

open opposite

variables {C : Type v} [category.{w} C] (F : ExtrDisc.{u}·µí·µñ ‚•§ C)

def terminal_condition [limits.has_terminal C] : Prop :=
  is_iso (limits.terminal.from (F.obj (op empty)))

def binary_product_condition [limits.has_binary_products C] : Prop := ‚àÄ (X Y : ExtrDisc.{u}),
  is_iso (limits.prod.lift (F.map (sum.inl X Y).op) (F.map (sum.inr X Y).op))

def finite_product_condition [limits.has_finite_products C] (F : ExtrDisc.{u}·µí·µñ ‚•§ C) :
  Prop := ‚àÄ (Œπ : Type u) [fintype Œπ] (X : Œπ ‚Üí ExtrDisc),
begin
  -- Lean is being annoying here...
  resetI,
  let t : Œ† i, F.obj (op (sigma X)) ‚ü∂ F.obj (op (X i)) := Œª i, F.map (sigma.Œπ X i).op,
  exact is_iso (limits.pi.lift t)
end

def finite_product_condition_for_types (F : ExtrDisc.{u}·µí·µñ ‚•§ Type w) : Prop :=
  ‚àÄ (Œπ : Type u) [fintype Œπ] (X : Œπ ‚Üí ExtrDisc),
begin
  resetI,
  dsimp_result {
    let t : Œ† i, F.obj (op (sigma X)) ‚Üí F.obj (op (X i)) := Œª i, F.map (sigma.Œπ X i).op,
    let tt : F.obj (op (sigma X)) ‚Üí Œ† i, F.obj (op (X i)) := Œª x i, t i x,
    exact function.bijective tt }
end

def equalizer_condition [limits.has_equalizers C] (F : ExtrDisc.{u}·µí·µñ ‚•§ C) : Prop :=
  ‚àÄ {R X B : ExtrDisc} (f : X ‚ü∂ B) (hf : function.surjective f)
    (g : R.val ‚ü∂ Profinite.pullback f.val f.val) (hg : function.surjective g),
  let e‚ÇÅ : R ‚ü∂ X := ‚ü®g ‚â´ Profinite.pullback.fst _ _‚ü©,
      e‚ÇÇ : R ‚ü∂ X := ‚ü®g ‚â´ Profinite.pullback.snd _ _‚ü©,
      w : e‚ÇÅ ‚â´ f = e‚ÇÇ ‚â´ f := by { ext1, dsimp [e‚ÇÅ, e‚ÇÇ], simp [Profinite.pullback.condition] },
      h : F.map f.op ‚â´ F.map e‚ÇÅ.op = F.map f.op ‚â´ F.map e‚ÇÇ.op :=
        by { simp only [‚Üê F.map_comp, ‚Üê op_comp, w] } in
  is_iso (limits.equalizer.lift _ h)

def equalizer_condition_for_types (F : ExtrDisc.{u}·µí·µñ ‚•§ Type w) : Prop :=
  ‚àÄ {R X B : ExtrDisc} (f : X ‚ü∂ B) (hf : function.surjective f)
    (g : R.val ‚ü∂ Profinite.pullback f.val f.val) (hg : function.surjective g),
  by dsimp_result { exact
  let e‚ÇÅ : R ‚ü∂ X := ‚ü®g ‚â´ Profinite.pullback.fst _ _‚ü©,
      e‚ÇÇ : R ‚ü∂ X := ‚ü®g ‚â´ Profinite.pullback.snd _ _‚ü©,
      w : e‚ÇÅ ‚â´ f = e‚ÇÇ ‚â´ f := by { ext1, dsimp [e‚ÇÅ, e‚ÇÇ], simp [Profinite.pullback.condition] },
      h : F.map f.op ‚â´ F.map e‚ÇÅ.op = F.map f.op ‚â´ F.map e‚ÇÇ.op :=
        by { simp only [‚Üê F.map_comp, ‚Üê op_comp, w] },
      E := { x : F.obj (op X) // F.map e‚ÇÅ.op x = F.map e‚ÇÇ.op x },
      t : F.obj (op B) ‚Üí E := Œª x, ‚ü®F.map f.op x, begin
        change (F.map f.op ‚â´ F.map e‚ÇÅ.op) x = (F.map f.op ‚â´ F.map e‚ÇÇ.op) x,
        rw h,
      end‚ü© in
    function.bijective t }

lemma equalizer_condition_holds [limits.has_equalizers C] (F : ExtrDisc.{u}·µí·µñ ‚•§ C) :
  equalizer_condition F :=
begin
  intros R X B f hf g hg,
  dsimp,
  let e‚ÇÅ : R ‚ü∂ X := ‚ü®g ‚â´ Profinite.pullback.fst _ _‚ü©,
  let e‚ÇÇ : R ‚ü∂ X := ‚ü®g ‚â´ Profinite.pullback.snd _ _‚ü©,
  let œÉ : B ‚ü∂ X := ‚ü®ExtrDisc.lift _ hf (ùüô _)‚ü©,
  let t : X ‚ü∂ R := ‚ü®ExtrDisc.lift _ hg _‚ü©,
  swap,
  { refine Profinite.pullback.lift _ _ (ùüô _) (f.val ‚â´ œÉ.val) _,
    dsimp, simp },
  have h‚ÇÅ : t ‚â´ e‚ÇÅ = ùüô _, by { ext1, dsimp, simp },
  have h‚ÇÇ : t ‚â´ e‚ÇÇ = f ‚â´ œÉ, by { ext1, dsimp, simp, },
  have hh : œÉ ‚â´ f = ùüô _, by { ext1, dsimp, simp },
  use (limits.equalizer.Œπ _ _ ‚â´ F.map œÉ.op),
  split,
  { simp only [limits.equalizer.lift_Œπ_assoc],
    simp only [‚Üê F.map_comp, ‚Üê op_comp, hh],
    simp },
  { ext,
    simp only [limits.equalizer.lift_Œπ, category.id_comp, category.assoc],
    simp only [‚Üê F.map_comp, ‚Üê op_comp],
    erw [‚Üê h‚ÇÇ, op_comp, F.map_comp],
    dsimp [e‚ÇÇ],
    erw ‚Üê limits.equalizer.condition_assoc,
    change _ ‚â´ F.map e‚ÇÅ.op ‚â´ F.map t.op = _,
    rw [‚Üê F.map_comp, ‚Üê op_comp, h‚ÇÅ],
    simp }
end

lemma equalizer_condition_for_types_holds (F : ExtrDisc.{u}·µí·µñ ‚•§ Type w) :
  equalizer_condition_for_types F :=
begin
  -- Should be fairly easy, just mimic the proof in the general case above.
   intros R X B f hf g hg,
  let e‚ÇÅ : R ‚ü∂ X := ‚ü®g ‚â´ Profinite.pullback.fst _ _‚ü©,
  let e‚ÇÇ : R ‚ü∂ X := ‚ü®g ‚â´ Profinite.pullback.snd _ _‚ü©,
  have w : e‚ÇÅ ‚â´ f = e‚ÇÇ ‚â´ f := begin
    dsimp [e‚ÇÅ,e‚ÇÇ],
    apply ExtrDisc.hom.ext,
    simp [category.assoc, Profinite.pullback.condition],
  end,
  have h : F.map f.op ‚â´ F.map e‚ÇÅ.op = F.map f.op ‚â´ F.map e‚ÇÇ.op :=
  by { simp only [‚Üê F.map_comp, ‚Üê op_comp, w] },
  let E := { x : F.obj (op X) // F.map e‚ÇÅ.op x = F.map e‚ÇÇ.op x },
  let t : F.obj (op B) ‚Üí E := Œª x, ‚ü®F.map f.op x, begin
    change (F.map f.op ‚â´ F.map e‚ÇÅ.op) x = (F.map f.op ‚â´ F.map e‚ÇÇ.op) x,
    rw h,
  end‚ü©,
  change function.bijective t,
  let ee := limits.concrete.equalizer_equiv (F.map e‚ÇÅ.op) (F.map e‚ÇÇ.op),
  suffices : function.bijective (ee.symm ‚àò t),
    by exact (equiv.comp_bijective t (equiv.symm ee)).mp this,
  have : ee.symm ‚àò t = limits.equalizer.lift _ h,
  { suffices : t = ee ‚àò limits.equalizer.lift _ h,
    { rw this, ext, simp, },
    ext,
    apply subtype.ext,
    change _ = (limits.equalizer.lift _ h ‚â´ limits.equalizer.Œπ _ _) _,
    rw limits.equalizer.lift_Œπ,
    refl },
  rw this,
  rw ‚Üê is_iso_iff_bijective,
  apply equalizer_condition_holds,
  assumption'
end

end ExtrDisc

namespace Profinite

lemma exists_projective_presentation (B : Profinite.{u}) :
  ‚àÉ (X : ExtrDisc) (œÄ : X.val ‚ü∂ B), function.surjective œÄ :=
begin
  obtain ‚ü®‚ü®X,h,œÄ,hœÄ‚ü©‚ü© := enough_projectives.presentation B,
  resetI,
  use [‚ü®X‚ü©, œÄ],
  rwa ‚Üê epi_iff_surjective
end

def pres (B : Profinite.{u}) : ExtrDisc :=
  B.exists_projective_presentation.some

def pres_œÄ (B : Profinite.{u}) : B.pres.val ‚ü∂ B :=
  B.exists_projective_presentation.some_spec.some

lemma pres_œÄ_surjective (B : Profinite.{u}) :
  function.surjective B.pres_œÄ :=
B.exists_projective_presentation.some_spec.some_spec

end Profinite

open opposite

variables {C : Type v} [category.{w} C] (F : ExtrDisc.{u}·µí·µñ ‚•§ C)

def is_ExtrSheaf_of_types (P : ExtrDisc.{u}·µí·µñ ‚•§ Type w) : Prop :=
‚àÄ (B : ExtrDisc.{u}) (Œπ : Type u) [fintype Œπ] (Œ± : Œπ ‚Üí ExtrDisc.{u})
  (f : Œ† i, Œ± i ‚ü∂ B) (hf : ‚àÄ b : B, ‚àÉ i (x : Œ± i), f i x = b)
  (x : Œ† i, P.obj (op (Œ± i)))
  (hx : ‚àÄ (i j : Œπ) (Z : ExtrDisc) (g‚ÇÅ : Z ‚ü∂ Œ± i) (g‚ÇÇ : Z ‚ü∂ Œ± j),
    g‚ÇÅ ‚â´ f _ = g‚ÇÇ ‚â´ f _ ‚Üí P.map g‚ÇÅ.op (x _) = P.map g‚ÇÇ.op (x _)),
‚àÉ! t : P.obj (op B), ‚àÄ i, P.map (f i).op t = x _

-- We encode the general condition essentially using Yoneda.
def is_ExtrSheaf (P : ExtrDisc.{u}·µí·µñ ‚•§ C) : Prop :=
‚àÄ (B : ExtrDisc.{u}) (Œπ : Type u) [fintype Œπ] (Œ± : Œπ ‚Üí ExtrDisc.{u})
  (f : Œ† i, Œ± i ‚ü∂ B) (hf : ‚àÄ b : B, ‚àÉ i (x : Œ± i), f i x = b)
  (T : C) (x : Œ† i, T ‚ü∂ P.obj (op (Œ± i)))
  (hx : ‚àÄ (i j : Œπ) (Z : ExtrDisc) (g‚ÇÅ : Z ‚ü∂ Œ± i) (g‚ÇÇ : Z ‚ü∂ Œ± j),
    g‚ÇÅ ‚â´ f _ = g‚ÇÇ ‚â´ f _ ‚Üí x _ ‚â´ P.map g‚ÇÅ.op = x _ ‚â´ P.map g‚ÇÇ.op),
‚àÉ! t : T ‚ü∂ P.obj (op B), ‚àÄ i, t ‚â´ P.map (f i).op = x _

lemma subsingleton_of_empty_of_is_ExtrSheaf_of_types
  (F : ExtrDisc.{u}·µí·µñ ‚•§ Type w) (hF : is_ExtrSheaf_of_types F) (Z : ExtrDisc)
  [hZ : is_empty Z] : subsingleton (F.obj (op Z)) :=
begin
  constructor,
  intros a b,
  specialize hF Z pempty pempty.elim (Œª a, a.elim) hZ.elim (Œª a, a.elim) (Œª a, a.elim),
  obtain ‚ü®t,h1,h2‚ü© := hF,
  have : a = t, { apply h2, intros i, exact i.elim },
  have : b = t, { apply h2, intros i, exact i.elim },
  cc,
end

lemma finite_product_condition_for_types_of_is_ExtrSheaf_of_types
  (F : ExtrDisc.{u}·µí·µñ ‚•§ Type w) (hF : is_ExtrSheaf_of_types F) :
    ExtrDisc.finite_product_condition_for_types F :=
begin
  introsI Œπ _ X,
  have hF' := hF,
  specialize hF (ExtrDisc.sigma X) Œπ X (ExtrDisc.sigma.Œπ _)
    (ExtrDisc.sigma.Œπ_jointly_surjective _),
  split,
  { intros x y hh,
    dsimp at hh,
    have hx := hF (Œª i, F.map (ExtrDisc.sigma.Œπ X i).op x) _,
    swap,
    { intros i j Z g‚ÇÅ g‚ÇÇ hh,
      dsimp,
      change (F.map _ ‚â´ F.map _) _ = (F.map _ ‚â´ F.map _) _,
      simp only [‚Üê F.map_comp, ‚Üê op_comp],
      rw hh },
    have hy := hF (Œª i, F.map (ExtrDisc.sigma.Œπ X i).op y) _,
    swap,
    { intros i j Z g‚ÇÅ g‚ÇÇ hh,
      dsimp,
      change (F.map _ ‚â´ F.map _) _ = (F.map _ ‚â´ F.map _) _,
      simp only [‚Üê F.map_comp, ‚Üê op_comp],
      rw hh },
    obtain ‚ü®tx,htx1,htx2‚ü© := hx,
    obtain ‚ü®ty,hty1,hty2‚ü© := hy,
    have : x = tx,
    { apply htx2,
      intros i,
      refl },
    rw this,
    symmetry,
    apply htx2,
    intros i,
    apply_fun (Œª e, e i) at hh,
    exact hh.symm },
  { intros x,
    have hx := hF x _,
    swap,
    { intros i j Z g‚ÇÅ g‚ÇÇ hh,
      by_cases hZ : nonempty Z,
      { obtain ‚ü®z‚ü© := hZ,
        have : i = j,
        { apply_fun (Œª e, (e z).1) at hh, exact hh },
        subst this,
        have : g‚ÇÅ = g‚ÇÇ,
        { ext t : 2,
          apply_fun ExtrDisc.sigma.Œπ X i,
          swap,
          { apply Profinite.sigma.Œπ_injective },
          apply_fun (Œª e, e t) at hh,
          exact hh },
        rw this },
      { simp at hZ, resetI,
        haveI := subsingleton_of_empty_of_is_ExtrSheaf_of_types F hF' Z,
        apply subsingleton.elim } },
    obtain ‚ü®t,ht,_‚ü© := hx,
    use t,
    ext1,
    apply ht }
end

namespace product_condition_setup
section

parameters {P : ExtrDisc.{u}·µí·µñ ‚•§ Type w} (hP : ExtrDisc.finite_product_condition_for_types P)
parameters {B : ExtrDisc.{u}} {Œπ : Type u} [fintype Œπ] (X : Œπ ‚Üí ExtrDisc.{u}) (f : Œ† i, X i ‚ü∂ B)

def G : Œπ √ó Œπ ‚Üí ExtrDisc := Œª ii, (Profinite.pullback (f ii.1).val (f ii.2).val).pres
def gfst : Œ† ii : Œπ √ó Œπ, G ii ‚ü∂ X ii.1 := Œª ii, ‚ü®Profinite.pres_œÄ _ ‚â´ Profinite.pullback.fst _ _‚ü©
def gsnd : Œ† ii : Œπ √ó Œπ, G ii ‚ü∂ X ii.2 := Œª ii, ‚ü®Profinite.pres_œÄ _ ‚â´ Profinite.pullback.snd _ _‚ü©

lemma hX : function.bijective
  (Œª (x : P.obj (op (ExtrDisc.sigma X))) (i : Œπ), P.map (ExtrDisc.sigma.Œπ X i).op x) := hP Œπ X
lemma hG : function.bijective
  (Œª (x : P.obj (op (ExtrDisc.sigma G))) (i : Œπ √ó Œπ), P.map (ExtrDisc.sigma.Œπ G i).op x) := hP (Œπ √ó Œπ) G

def œÄ : ExtrDisc.sigma X ‚ü∂ B := ExtrDisc.sigma.desc X f

lemma hœÄ  (surj : ‚àÄ b : B, ‚àÉ i (x : X i), f i x = b) : function.surjective œÄ :=
begin
  intros b,
  have := surj,
  obtain ‚ü®i,x,hx‚ü© := surj b,
  use ExtrDisc.sigma.Œπ X i x,
  exact hx
end

def r : (ExtrDisc.sigma G).val ‚ü∂ Profinite.pullback œÄ.val œÄ.val :=
begin
  refine Profinite.pullback.lift _ _ _ _ _,
  { refine Profinite.sigma.desc _ _,
    intros ii,
    refine _ ‚â´ Profinite.sigma.Œπ _ ii.1,
    refine (gfst _ _ _).val },
  { refine Profinite.sigma.desc _ _,
    intros ii,
    refine _ ‚â´ Profinite.sigma.Œπ _ ii.2,
    dsimp,
    refine (gsnd _ _ _).val },
  { apply Profinite.sigma.hom_ext,
    rintros ‚ü®i,j‚ü©,
    dsimp [œÄ, ExtrDisc.sigma.desc, gfst, gsnd],
    simp [Profinite.pullback.condition] },
end

lemma hr : function.surjective r :=
begin
  rintros ‚ü®‚ü®‚ü®i,a‚ü©,‚ü®j,b‚ü©‚ü©,h‚ü©,
  dsimp [œÄ, ExtrDisc.sigma.desc, Profinite.sigma.desc] at a b h,
  let ab : Profinite.pullback (f i).val (f j).val := ‚ü®‚ü®a,b‚ü©,h‚ü©,
  obtain ‚ü®c,hc‚ü© := Profinite.pres_œÄ_surjective _ ab,
  use ExtrDisc.sigma.Œπ (G X f) (i,j) c,
  apply subtype.ext,
  apply prod.ext,
  { apply sigma.ext, { refl },
    apply heq_of_eq,
    change (((Profinite.pullback (f i).val (f j).val).pres_œÄ) c).val.fst = _,
    rw hc, refl },
  { apply sigma.ext, { refl },
    apply heq_of_eq,
    change (((Profinite.pullback (f i).val (f j).val).pres_œÄ) c).val.snd = _,
    rw hc, refl }
end

@[nolint def_lemma] -- this lemma has an extremely annoying type to write down
def hE (surj : ‚àÄ b : B, ‚àÉ i (x : X i), f i x = b) :=
  ExtrDisc.equalizer_condition_for_types_holds P œÄ (hœÄ surj) r hr

def QX : P.obj (op (ExtrDisc.sigma X)) ‚âÉ Œ† i, P.obj (op (X i)) :=
  equiv.of_bijective _ hX
def QG : P.obj (op (ExtrDisc.sigma G)) ‚âÉ Œ† ii, P.obj (op (G ii)) :=
  equiv.of_bijective _ hG

def rfst : ExtrDisc.sigma G ‚ü∂ ExtrDisc.sigma X :=
  ‚ü®r ‚â´ Profinite.pullback.fst _ _‚ü©
def rsnd : ExtrDisc.sigma G ‚ü∂ ExtrDisc.sigma X :=
  ‚ü®r ‚â´ Profinite.pullback.snd _ _‚ü©

lemma Œπ_rfst (ii : Œπ √ó Œπ) : ExtrDisc.sigma.Œπ G ii ‚â´ rfst =
  gfst ii ‚â´ ExtrDisc.sigma.Œπ _ _ :=
begin
  ext1,
  dsimp [rfst, gfst, ExtrDisc.sigma.Œπ, ExtrDisc.sigma.desc, r],
  simp [Profinite.pullback.condition, Profinite.pullback.condition_assoc],
end

lemma Œπ_rsnd (ii : Œπ √ó Œπ) : ExtrDisc.sigma.Œπ G ii ‚â´ rsnd =
  gsnd ii ‚â´ ExtrDisc.sigma.Œπ _ _ :=
begin
  ext1,
  dsimp [rsnd, gsnd, ExtrDisc.sigma.Œπ, ExtrDisc.sigma.desc, r],
  simp [Profinite.pullback.condition, Profinite.pullback.condition_assoc],
end

lemma QX_symm_Œπ_aux (q : Œ† i, P.obj (op (X i))) :
  q = Œª i, P.map (ExtrDisc.sigma.Œπ X i).op (QX.symm q) :=
begin
  apply_fun (QX hP X).symm,
  change _ = (QX hP X).symm ((QX hP X) _),
  rw equiv.symm_apply_apply,
end

lemma QX_symm_Œπ (q : Œ† i, P.obj (op (X i))) (i : Œπ) :
  P.map (ExtrDisc.sigma.Œπ X i).op (QX.symm q) = q i :=
begin
  revert i,
  rw ‚Üê function.funext_iff,
  change _ = q,
  symmetry,
  apply QX_symm_Œπ_aux hP X q,
end

lemma QX_QG_compat_fst (q : Œ† i, P.obj (op (X i))) (i : Œπ √ó Œπ) :
  QG (P.map rfst.op (QX.symm q)) i = P.map (gfst i).op (q i.fst) :=
begin
  dsimp [QG],
  change (P.map _ ‚â´ P.map _) _ = _,
  simp only [‚Üê P.map_comp, ‚Üê op_comp, Œπ_rfst],
  simp only [P.map_comp, op_comp],
  dsimp,
  rw QX_symm_Œπ,
end

lemma QX_QG_compat_snd (q : Œ† i, P.obj (op (X i))) (i : Œπ √ó Œπ) :
  QG (P.map rsnd.op (QX.symm q)) i = P.map (gsnd i).op (q i.snd) :=
begin
  dsimp [QG],
  change (P.map _ ‚â´ P.map _) _ = _,
  simp only [‚Üê P.map_comp, ‚Üê op_comp, Œπ_rsnd],
  simp only [P.map_comp, op_comp],
  dsimp,
  rw QX_symm_Œπ,
end

end
end product_condition_setup

open product_condition_setup

theorem is_ExtrSheaf_of_types_of_finite_product_condition_for_types
  (F : ExtrDisc.{u}·µí·µñ ‚•§ Type w) (hF : ExtrDisc.finite_product_condition_for_types F) :
  is_ExtrSheaf_of_types F :=
begin
  introsI B Œπ _ X f surj x hx,
  have hrfst : ‚àÄ (q : Œ† i, F.obj (op (X i))),
    (QG hF X f) (F.map (rfst X f).op ((QX hF X).symm q)) =
    (Œª ii, F.map (gfst X f ii).op (q ii.1)),
  { intros q, funext ii,
    change (F.map _ ‚â´ F.map _) _ = _,
    simp only [‚Üê F.map_comp, ‚Üê op_comp, Œπ_rfst],
    simp only [F.map_comp, op_comp],
    dsimp,
    rw QX_symm_Œπ hF X q },
  have hrgsnd : ‚àÄ (q : Œ† i, F.obj (op (X i))),
    (QG hF X f) (F.map (rsnd X f).op ((QX hF X).symm q)) =
    (Œª ii, F.map (gsnd X f ii).op (q ii.2)),
  { intros q, funext ii,
    change (F.map _ ‚â´ F.map _) _ = _,
    simp only [‚Üê F.map_comp, ‚Üê op_comp, Œπ_rsnd],
    simp only [F.map_comp, op_comp],
    dsimp,
    rw QX_symm_Œπ hF X q },
  let EE : F.obj (op B) ‚âÉ
    { t : F.obj (op (ExtrDisc.sigma X)) // F.map (rfst X f).op t = F.map (rsnd X f).op t } :=
      equiv.of_bijective _ (hE X f surj),
  let x' : F.obj (op (ExtrDisc.sigma X)) := (QX hF X).symm x,
  -- Should follow from hx,
  have hx' : F.map (rfst X f).op x' = F.map (rsnd X f).op x',
  { apply_fun (QG hF X f),
    funext ii,
    rw QX_QG_compat_fst,
    rw QX_QG_compat_snd,
    apply hx,
    ext1,
    dsimp [gfst, gsnd],
    simp [Profinite.pullback.condition] },
  let b : F.obj (op B) := EE.symm ‚ü®x',hx'‚ü©,
  use b,
  have hb : ‚àÄ i, F.map (f i).op b = x i,
  { intros i,
    have : f i = ExtrDisc.sigma.Œπ X i ‚â´ (œÄ X f),
    { dsimp [œÄ], rw ExtrDisc.sigma.Œπ_desc },
    rw [this, op_comp, F.map_comp],
    dsimp,
    have : F.map (œÄ X f).op b = x',
    { change ‚Üë(EE b) = x',
      dsimp only [b],
      rw equiv.apply_symm_apply,
      refl },
    rw this,
    dsimp [x'],
    change ((QX hF X) ((QX hF X).symm x)) _ = _,
    rw equiv.apply_symm_apply },
  refine ‚ü®hb, _‚ü©,
  { intros b' hb',
    apply_fun EE,
    ext1,
    apply_fun (QX hF X),
    dsimp [EE, QX, œÄ],
    funext i,
    change (F.map _ ‚â´ F.map _) _ = (F.map _ ‚â´ F.map _) _,
    simp only [‚Üê F.map_comp, ‚Üê op_comp, ExtrDisc.sigma.Œπ_desc, hb, hb'] }
end

theorem is_ExtrSheaf_of_types_iff_product_condition_for_types (F : ExtrDisc.{u}·µí·µñ ‚•§ Type w) :
  is_ExtrSheaf_of_types F ‚Üî ExtrDisc.finite_product_condition_for_types F :=
begin
  split,
  { intro h, exact finite_product_condition_for_types_of_is_ExtrSheaf_of_types _ h },
  { intro h, exact is_ExtrSheaf_of_types_of_finite_product_condition_for_types _ h }
end

lemma is_ExtrSheaf_iff_forall_yoneda (F : ExtrDisc.{u}·µí·µñ ‚•§ C) :
  is_ExtrSheaf F ‚Üî (‚àÄ (T : C), is_ExtrSheaf_of_types (F ‚ãô coyoneda.obj (op T))) :=
begin
  split,
  { introsI h T B Œπ _ X f surj x hx,
    exact h B Œπ X f surj T x hx },
  { introsI h B Œπ _ X f surj T x hx,
    exact h T B Œπ X f surj x hx }
end

theorem finite_product_condition_iff_forall_yoneda [limits.has_finite_products C]
  (F : ExtrDisc.{u}·µí·µñ ‚•§ C) :
  ExtrDisc.finite_product_condition F ‚Üî
  (‚àÄ (T : C), ExtrDisc.finite_product_condition_for_types (F ‚ãô coyoneda.obj (op T))) :=
begin
  split,
  { introsI h T Œπ _ X,
    let t : F.obj (op (ExtrDisc.sigma X)) ‚ü∂ ‚àè Œª (i : Œπ), F.obj (op (X i)) :=
      limits.pi.lift (Œª (i : Œπ), F.map (ExtrDisc.sigma.Œπ X i).op),
    specialize h Œπ X,
    dsimp at h ‚ä¢,
    change is_iso t at h,
    resetI,
    split,
    { intros a b hab,
      dsimp at hab,
      suffices : a ‚â´ t = b ‚â´ t,
      { apply_fun (Œª e, e ‚â´ inv t) at this, simpa using this },
      ext1,
      rw function.funext_iff at hab,
      simp [hab] },
    { intros a,
      use limits.pi.lift a ‚â´ inv t,
      dsimp,
      funext i,
      have : inv t ‚â´ F.map (ExtrDisc.sigma.Œπ X i).op = limits.pi.œÄ _ i,
      { simp [is_iso.inv_comp_eq] },
      simp [this] } },
  { introsI h Œπ _ X,
    dsimp,
    let h‚ÇÅ := h (‚àè Œª (i : Œπ), F.obj (op (X i))) Œπ X,
    let h‚ÇÇ := h (F.obj (op (ExtrDisc.sigma X))) Œπ X,
    dsimp at h‚ÇÅ h‚ÇÇ,
    replace h‚ÇÅ := h‚ÇÅ.2,
    replace h‚ÇÇ := h‚ÇÇ.1,
    obtain ‚ü®s,hs‚ü© := h‚ÇÅ (Œª i, limits.pi.œÄ _ i),
    use s,
    rw function.funext_iff at hs,
    dsimp at *,
    split,
    { apply h‚ÇÇ,
      ext1 i,
      dsimp,
      simp [hs i] },
    { ext1 i,
      simp [hs i] } }
end

theorem is_ExtrSheaf_iff_product_condition
  [limits.has_finite_products C] (F : ExtrDisc.{u}·µí·µñ ‚•§ C) :
  is_ExtrSheaf F ‚Üî ExtrDisc.finite_product_condition F :=
begin
  rw is_ExtrSheaf_iff_forall_yoneda,
  rw finite_product_condition_iff_forall_yoneda,
  apply forall_congr (Œª T, _),
  apply is_ExtrSheaf_of_types_iff_product_condition_for_types
end

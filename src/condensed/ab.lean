import category_theory.abelian.projective
import pseudo_normed_group.category
import topology.continuous_function.algebra

import algebra.group.ulift

import for_mathlib.abelian_sheaves.main
import for_mathlib.AddCommGroup.exact
import for_mathlib.types

import condensed.adjunctions
import condensed.top_comparison
import condensed.filtered_colimits

/-!
# Properties of the category of condensed abelian groups

-/

open category_theory category_theory.limits

universes v u

-- Move this!
-- @[simps obj map {fully_applied := ff}] -- we probably don't want these as global simp lemmas
def Ab.ulift : Ab.{u} ‚•§ Ab.{max v u} :=
{ obj := Œª M, AddCommGroup.of $ ulift.{v} M,
  map := Œª M N f,
  { to_fun := Œª x, ‚ü®f x.down‚ü©,
    map_zero' := by { ext1, apply f.map_zero },
    map_add' := Œª x y, by { ext1, apply f.map_add } },
  map_id' := by { intros, ext, refl },
  map_comp' := by { intros, ext, refl } }

@[simp] lemma Ab.exact_ulift_map {A B C : Ab} (f : A ‚ü∂ B) (g : B ‚ü∂ C) :
  exact (Ab.ulift.map f) (Ab.ulift.map g) ‚Üî exact f g :=
begin
  let F := Ab.ulift.map f,
  let G := Ab.ulift.map g,
  change exact F G ‚Üî _,
  rw AddCommGroup.exact_iff,
  rw AddCommGroup.exact_iff,
  split,
  { intro h,
    apply le_antisymm,
    { rintros _ ‚ü®x,rfl‚ü©,
      have : ulift.up (f x) ‚àà F.range := ‚ü®‚ü®x‚ü©, rfl‚ü©,
      rw h at this,
      change _ = _ at this,
      apply_fun (Œª e, e.down) at this,
      exact this },
    { rintros x hx, change _ = _ at hx,
      have : ulift.up x ‚àà G.ker, by { apply_fun ulift.up at hx, exact hx },
      rw ‚Üê h at this,
      obtain ‚ü®y,hy‚ü© := this,
      apply_fun (Œª e, e.down) at hy,
      rw ‚Üê hy,
      use [y.down, rfl] } },
  { intro h,
    apply le_antisymm,
    { rintros _ ‚ü®x,rfl‚ü©,
      ext,
      change _ ‚àà g.ker,
      rw ‚Üê h,
      use [x.down, rfl] },
    { intros x hx,
      change _ = _ at hx,
      apply_fun (Œª e, e.down) at hx,
      change _ ‚àà g.ker at hx,
      rw ‚Üê h at hx,
      obtain ‚ü®y,hy‚ü© := hx,
      use y,
      ext,
      exact hy } },
end

namespace Condensed

--instance : preadditive (Condensed Ab.{u+1}) := by admit

noncomputable theory

-- Sanity check
example {J : Type (u+1)} [small_category J] [is_filtered J] :
  limits.preserves_colimits_of_shape J (forget Ab.{u+1}) := by apply_instance

-- this is now available in `condensed/projective_resolutions.lean`...
--instance : enough_projectives (Condensed Ab.{u+1}) := by admit

instance : is_right_adjoint (Sheaf_to_presheaf _ _ : Condensed Ab.{u+1} ‚•§ _) :=
{ left := presheaf_to_Sheaf _ _,
  adj := (sheafification_adjunction _ _) }

@[simps obj map {fully_applied := ff}]
def forget_to_CondensedType : Condensed Ab.{u+1} ‚•§ CondensedSet :=
{ obj := Œª F, ‚ü®F.val ‚ãô forget _, begin
    cases F with F hF,
    rwa (presheaf.is_sheaf_iff_is_sheaf_forget _ _ (forget Ab)) at hF,
    apply_instance
  end ‚ü©,
  map := Œª A B f, ‚ü®whisker_right f.val _‚ü© }

instance : is_right_adjoint forget_to_CondensedType :=
{ left := CondensedSet_to_Condensed_Ab,
  adj := Condensed_Ab_CondensedSet_adjunction }

section

variables (A : Type u) [add_comm_group A] [topological_space A] [topological_add_group A]

def of_top_ab.presheaf : Profinite.{u}·µí·µñ ‚•§ Ab.{u} :=
{ obj := Œª S, ‚ü®C(S.unop, A)‚ü©,
  map := Œª S‚ÇÅ S‚ÇÇ f, add_monoid_hom.mk' (Œª g, g.comp f.unop) $ Œª g‚ÇÅ g‚ÇÇ, rfl,
  map_id' := by { intros, ext, refl },
  map_comp' := by { intros, ext, refl } }

/-- The condensed abelian group associated with a topological abelian group -/
def of_top_ab : Condensed.{u} Ab.{u+1} :=
{ val := of_top_ab.presheaf A ‚ãô Ab.ulift.{u+1},
  cond := begin
    rw category_theory.presheaf.is_sheaf_iff_is_sheaf_forget _ _ (forget Ab),
    swap, apply_instance,
    let B := Top.of A,
    change presheaf.is_sheaf _ B.to_Condensed.val,
    exact B.to_Condensed.cond,
  end }


variables {A} {B : Type u} [add_comm_group B] [topological_space B] [topological_add_group B]

def of_top_ab_map (f : A ‚Üí+ B) (hf : continuous f) : of_top_ab A ‚ü∂ of_top_ab B :=
{ val := whisker_right
  { app := Œª S, begin
      refine add_monoid_hom.mk' (Œª g, ‚ü®f ‚àò (show C(‚Ü•(opposite.unop S), A), from g), hf.comp _‚ü©) _,
      { exact g.continuous },
      { intros, ext, exact f.map_add _ _, }
    end,
    naturality' := Œª S T g, rfl, }
  Ab.ulift.{u+1} }


end

end Condensed

namespace CompHausFiltPseuNormGrp

open_locale nnreal
open pseudo_normed_group comphaus_filtered_pseudo_normed_group

def presheaf (A : CompHausFiltPseuNormGrp.{u}) (S : Profinite.{u}) : Type u :=
{ f : S ‚Üí A // ‚àÉ (c : ‚Ñù‚â•0) (f‚ÇÄ : S ‚Üí filtration A c), continuous f‚ÇÄ ‚àß f = coe ‚àò f‚ÇÄ }

namespace presheaf

variables (A : CompHausFiltPseuNormGrp.{u}) (S : Profinite.{u})

@[ext]
lemma ext {A : CompHausFiltPseuNormGrp} {S : Profinite} (f g : presheaf A S) : f.1 = g.1 ‚Üí f = g :=
subtype.ext

instance : has_zero (presheaf A S) := ‚ü®‚ü®0, 0, 0, continuous_zero, rfl‚ü©‚ü©

instance : has_neg (presheaf A S) :=
‚ü®Œª f, ‚ü®-f.1,
  begin
    obtain ‚ü®_, c, f, hf, rfl‚ü© := f,
    refine ‚ü®c, Œª s, - f s, _, rfl‚ü©,
    exact (continuous_neg' c).comp hf
  end‚ü©‚ü©

instance : has_add (presheaf A S) :=
‚ü®Œª f g, ‚ü®f.1 + g.1,
  begin
    obtain ‚ü®_, cf, f, hf, rfl‚ü© := f,
    obtain ‚ü®_, cg, g, hg, rfl‚ü© := g,
    refine ‚ü®cf + cg, Œª s, ‚ü®f s + g s, add_mem_filtration (f s).2 (g s).2‚ü©, _, rfl‚ü©,
    have aux := (hf.prod_mk hg),
    exact (continuous_add' cf cg).comp aux,
  end‚ü©‚ü©

instance : has_sub (presheaf A S) :=
‚ü®Œª f g, ‚ü®f.1 - g.1,
  begin
    obtain ‚ü®_, cf, f, hf, rfl‚ü© := f,
    obtain ‚ü®_, cg, g, hg, rfl‚ü© := g,
    refine ‚ü®cf + cg, Œª s, ‚ü®f s - g s, sub_mem_filtration (f s).2 (g s).2‚ü©, _, rfl‚ü©,
    have aux := (hf.prod_mk ((continuous_neg' cg).comp hg)),
    simp only [sub_eq_add_neg],
    exact (continuous_add' cf cg).comp aux,
  end‚ü©‚ü©

variables {A S}

protected def nsmul (n : ‚Ñï) (f : presheaf A S) : presheaf A S :=
‚ü®n ‚Ä¢ f.1,
begin
  obtain ‚ü®_, c, f, hf, rfl‚ü© := f,
  refine ‚ü®n * c, Œª s, ‚ü®n ‚Ä¢ f s, nat_smul_mem_filtration _ _ _ (f s).2‚ü©, _, rfl‚ü©,
  exact continuous_nsmul _ _ _ hf,
end‚ü©

protected def zsmul (n : ‚Ñ§) (f : presheaf A S) : presheaf A S :=
‚ü®n ‚Ä¢ f.1,
begin
  obtain ‚ü®_, c, f, hf, rfl‚ü© := f,
  refine ‚ü®n.nat_abs * c, Œª s, ‚ü®n ‚Ä¢ f s, int_smul_mem_filtration _ _ _ (f s).2‚ü©, _, rfl‚ü©,
  exact continuous_zsmul _ _ _ hf,
end‚ü©

variables (A S)

instance : add_comm_group (presheaf A S) :=
{ zero := 0,
  add := (+),
  nsmul := presheaf.nsmul,
  zsmul := presheaf.zsmul,
  add_assoc := by { intros, ext, exact add_assoc _ _ _ },
  zero_add := by { intros, ext, exact zero_add _ },
  add_zero := by { intros, ext, exact add_zero _ },
  add_comm := by { intros, ext, exact add_comm _ _ },
  add_left_neg := by { intros, ext, exact add_left_neg _ },
  sub_eq_add_neg := by { intros, ext, exact sub_eq_add_neg _ _ },
  nsmul_zero' := by { intros, ext, exact zero_nsmul _ },
  nsmul_succ' := by { intros, ext, exact succ_nsmul _ _ },
  zsmul_zero' := by { intros, ext, exact zero_zsmul _ },
  zsmul_succ' := by { intros, ext, exact add_comm_group.zsmul_succ' _ _ },
  zsmul_neg' := by { intros, ext, exact add_comm_group.zsmul_neg' _ _ },
  .. presheaf.has_sub A S, .. presheaf.has_neg A S }

@[simps apply {fully_applied := ff}]
def comap (A : CompHausFiltPseuNormGrp) {S T : Profinite} (œÜ : S ‚ü∂ T) :
  presheaf A T ‚Üí+ presheaf A S :=
{ to_fun := Œª f, ‚ü®f.1 ‚àò œÜ,
  begin
    obtain ‚ü®_, c, f, hf, rfl‚ü© := f,
    refine ‚ü®c, f ‚àò œÜ, hf.comp œÜ.continuous, rfl‚ü©,
  end‚ü©,
  map_zero' := rfl,
  map_add' := by { intros, refl } }

@[simps apply {fully_applied := ff}]
def map {A B : CompHausFiltPseuNormGrp} (œÜ : A ‚ü∂ B) (S : Profinite) :
  presheaf A S ‚Üí+ presheaf B S :=
{ to_fun := Œª f, ‚ü®œÜ ‚àò f.1,
  begin
    obtain ‚ü®_, c, f, hf, rfl‚ü© := f,
    obtain ‚ü®d,hd‚ü© := œÜ.bound,
    let e : filtration A c ‚Üí filtration B (d * c) := Œª t, ‚ü®œÜ t, hd t.2‚ü©,
    have he : continuous e,
    { apply œÜ.continuous, intros, refl },
    refine ‚ü®d * c, e ‚àò f, he.comp hf, rfl‚ü©,
  end‚ü©,
  map_zero' := by { ext, exact œÜ.map_zero },
  map_add' := by { intros, ext, exact œÜ.map_add _ _ } }

end presheaf

open opposite

@[simps obj map {fully_applied := ff}]
def Presheaf (A : CompHausFiltPseuNormGrp.{u}) : Profinite.{u}·µí·µñ ‚•§ Ab :=
{ obj := Œª S, ‚ü®presheaf A (unop S)‚ü©,
  map := Œª S T œÜ, presheaf.comap A œÜ.unop,
  map_id' := by { intros, ext, refl },
  map_comp' := by { intros, ext, refl } }

@[simps app {fully_applied := ff}]
def Presheaf.map {A B : CompHausFiltPseuNormGrp} (œÜ : A ‚ü∂ B) :
  Presheaf A ‚ü∂ Presheaf B :=
{ app := Œª S, presheaf.map œÜ (unop S),
  naturality' := by { intros, refl } }

@[simp]
lemma Presheaf.map_id (A : CompHausFiltPseuNormGrp) :
  Presheaf.map (ùüô A) = ùüô _ := by { ext, refl }

@[simp]
lemma Presheaf.map_comp {A B C : CompHausFiltPseuNormGrp} (f : A ‚ü∂ B) (g : B ‚ü∂ C) :
  Presheaf.map (f ‚â´ g) = Presheaf.map f ‚â´ Presheaf.map g := by { ext, refl }

--set_option pp.universes true

lemma Presheaf_comp_ulift_is_sheaf_aux_equalizer
  (A : CompHausFiltPseuNormGrp.{u}) :
  (A.Presheaf ‚ãô Ab.ulift.{u+1 u} ‚ãô forget.{u+2 u+1 u+1} Ab.{u+1}).equalizer_condition :=
begin
  intros X B œÄ hh,
  split,
  { rintros ‚ü®x‚ü© ‚ü®y‚ü© h,
    ext t,
    obtain ‚ü®t,rfl‚ü© := hh t,
    apply_fun (Œª e, e.val.down.val t) at h,
    exact h },
  { rintros ‚ü®‚ü®‚ü®t,c,t',ht',ht‚ü©‚ü©,h‚ü©,
    let E : Top := Top.of (filtration A c),
    let t'' : Profinite.to_Top.obj X ‚ü∂ E := ‚ü®t',ht'‚ü©,
    have hw : Profinite.to_Top.{u}.map (Profinite.pullback.fst.{u} œÄ œÄ) ‚â´ t'' =
      Profinite.to_Top.{u}.map (Profinite.pullback.snd.{u} œÄ œÄ) ‚â´ t'',
    { dsimp at h,
      ext i,
      dsimp [Profinite.pullback.fst, Profinite.pullback.snd],
      apply_fun (Œª e, e.down.val i) at h,
      change (coe ‚àò t') i.val.fst = (coe ‚àò t') i.val.snd,
      rw ‚Üê ht,
      exact h },
    let w := Profinite.descend_to_Top œÄ t'' hh hw,
    refine ‚ü®‚ü®‚ü®_,c,w,w.2,rfl‚ü©‚ü©,_‚ü©,
    ext : 3,
    dsimp,
    rw ht,
    ext i,
    dsimp [CompHausFiltPseuNormGrp.Presheaf, Ab.ulift,
      functor.map_to_equalizer],
    have := Profinite.œÄ_descend_to_Top œÄ t'' hh hw,
    apply_fun (Œª e, (e i).val) at this, exact this }
end

lemma Presheaf_comp_ulift_is_sheaf (A : CompHausFiltPseuNormGrp.{u}):
  presheaf.is_sheaf proetale_topology (Presheaf A ‚ãô Ab.ulift.{u+1}) :=
begin
  rw category_theory.presheaf.is_sheaf_iff_is_sheaf_forget _ _ (forget Ab),
  swap, apply_instance,
  rw is_sheaf_iff_is_sheaf_of_type,
  erw (functor.is_proetale_sheaf_of_types_tfae
    (A.Presheaf ‚ãô Ab.ulift.{u+1} ‚ãô forget _)).out 0 5,
  refine ‚ü®_,_,_‚ü©,
  { dsimp [functor.empty_condition],
    split,
    { intros a b h, ext ‚ü®‚ü© },
    { intros x, dsimp,
      refine ‚ü®‚ü®‚ü®Œª x, x.elim, 0, Œª x, x.elim, by continuity, _‚ü©‚ü©, _‚ü©,
      { ext ‚ü®‚ü© },
      { cases x, refl } } },
  { intros X Y,
    split,
    { rintros ‚ü®x‚ü© ‚ü®y‚ü© h, dsimp at h,
      ext : 2,
      dsimp,
      ext (t|t),
      { apply_fun (Œª e, e.fst.down.val t) at h, exact h },
      { apply_fun (Œª e, e.snd.down.val t) at h, exact h } },
    { rintros ‚ü®‚ü®f,c,f',hf',hf‚ü©,‚ü®g,d,g',hg',hg‚ü©‚ü©,
      let p : X.sum Y ‚Üí A := Œª t, sum.rec_on t f g,
      let e : ‚Ñù‚â•0 := c ‚äî d,
      haveI : fact (c ‚â§ e) := ‚ü®le_sup_left‚ü©,
      haveI : fact (d ‚â§ e) := ‚ü®le_sup_right‚ü©,
      let p' : X.sum Y ‚Üí filtration A e :=
        Œª t, sum.rec_on t (cast_le ‚àò f') (cast_le ‚àò g'),
      have hp' : continuous p',
      { apply continuous_sup_dom,
        { apply continuous_coinduced_dom,
          have : p' ‚àò sum.inl = cast_le ‚àò f', by ext; refl,
          rw this,
          apply continuous.comp _ hf',
          apply continuous_cast_le },
        { apply continuous_coinduced_dom,
          have : p' ‚àò sum.inr = cast_le ‚àò g', by ext; refl,
          rw this,
          apply continuous.comp _ hg',
          apply continuous_cast_le } },
      have hh : p = coe ‚àò p',
      { ext (a|a),
        { apply_fun (Œª u, u a) at hf, exact hf },
        { apply_fun (Œª u, u a) at hg, exact hg } },
      refine ‚ü®‚ü®‚ü®p,e,p',hp',hh‚ü©‚ü©,_‚ü©,
      ext; refl } },
  { apply Presheaf_comp_ulift_is_sheaf_aux_equalizer }
end

@[simps obj map {fully_applied := ff}]
def to_Condensed : CompHausFiltPseuNormGrp.{u} ‚•§ Condensed.{u} Ab.{u+1} :=
{ obj := Œª A,
  { val := Presheaf A ‚ãô Ab.ulift.{u+1},
    cond := Presheaf_comp_ulift_is_sheaf _ },
  map := Œª A B f, ‚ü®whisker_right (Presheaf.map f) _‚ü©,
  map_id' := Œª X, by { ext : 2, dsimp, simp },
  map_comp' := Œª X Y Z f g, by { ext : 2, dsimp, simp } }

section

-- #check Top.to_Condensed

variables (A : CompHausFiltPseuNormGrp.{u})

@[simps]
def level : ‚Ñù‚â•0 ‚•§ CompHaus.{u} :=
{ obj := Œª r, CompHaus.of $ filtration A r,
  map := Œª r s h,
  { to_fun := cast_le' h.le,
    continuous_to_fun := by letI : fact (r ‚â§ s) := ‚ü®h.le‚ü©; exact continuous_cast_le _ _ },
  map_id' := Œª r, by { ext, refl },
  map_comp' := Œª r s t h1 h2, by { ext, refl } }

@[simps]
def level_Condensed_diagram : ‚Ñù‚â•0 ‚•§ CondensedSet.{u} :=
A.level ‚ãô CompHaus_to_Top.{u} ‚ãô Top_to_Condensed.{u}

@[simps]
def level_Condensed_diagram' : (as_small.{u+1} ‚Ñù‚â•0) ‚•§ CondensedSet.{u} :=
as_small.down ‚ãô A.level_Condensed_diagram

def level_Condensed_diagram_cocone :
  cocone A.level_Condensed_diagram' :=
{ X := Condensed_Ab_to_CondensedSet.obj (to_Condensed.obj A),
  Œπ :=
  { app := Œª r, Sheaf.hom.mk $
    { app := Œª S f, ulift.up $ ‚ü®_, ulift.down r, f.down.1, f.down.2, rfl‚ü©,
      naturality' := Œª S T f, by { ext, refl } },
    naturality' := Œª r s h, by { ext, refl } } } .

def colimit_iso_Condensed_obj_aux_fun (X) :
let E := A.level_Condensed_diagram' ‚ãô Sheaf_to_presheaf _ _ ‚ãô (evaluation _ _).obj (op X) in
  (types.filtered_colimit_cocone E).X ‚Üí A.presheaf X :=
let E := A.level_Condensed_diagram' ‚ãô Sheaf_to_presheaf _ _ ‚ãô (evaluation _ _).obj (op X) in
Œª t, @quotient.lift_on' (Œ£ (j : as_small.{u+1} ‚Ñù‚â•0), E.obj j) (A.presheaf X)
  (types.filtered_colimit_setoid E) t
  (Œª f, ‚ü®_,ulift.down f.1, f.2.down.1, f.2.down.2, rfl‚ü©) begin
    rintros ‚ü®i,x‚ü© ‚ü®j,y‚ü© ‚ü®e,u,v,h‚ü©,
    ext q : 2,
    dsimp [level_Condensed_diagram, level_Condensed_diagram'] at *,
    apply_fun (Œª e, (e.down q).1) at h, exact h
  end

lemma colimit_iso_Condensed_obj_aux_fun_bijective (X) :
  function.bijective (colimit_iso_Condensed_obj_aux_fun A X) :=
begin
  split,
  { rintros ‚ü®‚ü®‚ü®i‚ü©,f‚ü©‚ü© ‚ü®‚ü®‚ü®j‚ü©,g‚ü©‚ü© h, dsimp [colimit_iso_Condensed_obj_aux_fun] at h ‚ä¢,
    simp only [subtype.mk_eq_mk] at h,
    apply quotient.sound',
    use [‚ü®i ‚äî j‚ü©, ‚ü®le_sup_left‚ü©, ‚ü®le_sup_right‚ü©],
    ext q,
    dsimp [level_Condensed_diagram'], apply_fun (Œª e, e q) at h, exact h },
  { rintros ‚ü®f,c,g,hg,hf‚ü©,
    use quotient.mk' ‚ü®‚ü®c‚ü©,‚ü®‚ü®g,hg‚ü©‚ü©‚ü©, ext tt, dsimp, rw hf, refl }
end

-- We would have to use `some` to define the inverse of this equiv, so we may as well just use
-- `equiv.of_bijective`
@[simps]
def colimit_iso_Condensed_obj_aux (X) :
let E := A.level_Condensed_diagram' ‚ãô Sheaf_to_presheaf _ _ ‚ãô (evaluation _ _).obj (op X) in
  (types.filtered_colimit_cocone E).X ‚âÉ A.presheaf X :=
equiv.of_bijective (A.colimit_iso_Condensed_obj_aux_fun X)
(A.colimit_iso_Condensed_obj_aux_fun_bijective X)

/-
let E := A.level_Condensed_diagram' ‚ãô Sheaf_to_presheaf _ _ ‚ãô (evaluation _ _).obj (op X) in
  (types.filtered_colimit_cocone E).X ‚âÉ A.presheaf X :=
let E := A.level_Condensed_diagram' ‚ãô Sheaf_to_presheaf _ _ ‚ãô (evaluation _ _).obj (op X) in
equiv.of_bijective
(Œª t, @quotient.lift_on' (Œ£ (j : as_small.{u+1} ‚Ñù‚â•0), E.obj j) (A.presheaf X)
  (types.filtered_colimit_setoid E) t
  (Œª f, ‚ü®_,ulift.down f.1, f.2.down.1, f.2.down.2, rfl‚ü©) begin
    rintros ‚ü®i,x‚ü© ‚ü®j,y‚ü© ‚ü®e,u,v,h‚ü©,
    ext q : 2,
    dsimp [level_Condensed_diagram, level_Condensed_diagram'] at *,
    apply_fun (Œª e, (e.down q).1) at h, exact h
  end)
begin
  split,
  { rintros ‚ü®‚ü®‚ü®i‚ü©,f‚ü©‚ü© ‚ü®‚ü®‚ü®j‚ü©,g‚ü©‚ü© h, dsimp at h ‚ä¢, apply quotient.sound',
    simp only [subtype.mk_eq_mk] at h, use [‚ü®i ‚äî j‚ü©, ‚ü®le_sup_left‚ü©, ‚ü®le_sup_right‚ü©],
    ext q,
    dsimp [level_Condensed_diagram'], apply_fun (Œª e, e q) at h, exact h },
  { rintros ‚ü®f,c,g,hg,hf‚ü©,
    use quotient.mk' ‚ü®‚ü®c‚ü©,‚ü®‚ü®g,hg‚ü©‚ü©‚ü©, ext tt, dsimp, rw hf }
end
-/

def colimit_iso_Condensed_obj_aux_nat_iso :
  (filtered_cocone.{u} A.level_Condensed_diagram').X.val ‚âÖ
  (Condensed_Ab_to_CondensedSet.{u}.obj (to_Condensed.{u}.obj A)).val :=
  nat_iso.of_components (Œª X,
    (is_colimit_of_preserves ((evaluation _ _).obj X)
      (colimit.is_colimit (A.level_Condensed_diagram' ‚ãô
        Sheaf_to_presheaf _ _))).cocone_point_unique_up_to_iso (colimit.is_colimit _) ‚â™‚â´
    (colimit.is_colimit _).cocone_point_unique_up_to_iso
    (types.filtered_colimit_cocone_is_colimit _) ‚â™‚â´
    equiv.to_iso ((A.colimit_iso_Condensed_obj_aux X.unop).trans equiv.ulift.symm)
  )
begin
  intros X Y f, dsimp [is_colimit.cocone_point_unique_up_to_iso],
  apply
    (is_colimit_of_preserves ((evaluation Profinite·µí·µñ (Type (u+1))).obj X)
    (colimit.is_colimit (A.level_Condensed_diagram' ‚ãô
    Sheaf_to_presheaf proetale_topology (Type (u+1))))).hom_ext,
  intros j, simp only [category.assoc],
  slice_lhs 0 1
  { dsimp, rw ‚Üê nat_trans.naturality },
  slice_lhs 2 3
  { erw ((is_colimit_of_preserves ((evaluation Profinite·µí·µñ (Type (u+1))).obj Y)
      (colimit.is_colimit (A.level_Condensed_diagram' ‚ãô
      Sheaf_to_presheaf proetale_topology (Type (u+1)))))).fac },
  slice_lhs 2 3
  { erw colimit.Œπ_desc },
  slice_rhs 1 2
  { erw (is_colimit_of_preserves ((evaluation Profinite·µí·µñ (Type (u+1))).obj X)
    (colimit.is_colimit (A.level_Condensed_diagram' ‚ãô
    Sheaf_to_presheaf proetale_topology (Type (u+1))))).fac },
  slice_rhs 1 2
  { erw colimit.Œπ_desc },
  ext, refl
end

def colimit_iso_Condensed_obj :
  colimit A.level_Condensed_diagram' ‚âÖ Condensed_Ab_to_CondensedSet.obj (to_Condensed.obj A) :=
(colimit.is_colimit _).cocone_point_unique_up_to_iso (filtered_cocone_is_colimit _) ‚â™‚â´
  Sheaf.iso.mk _ (Condensed_Ab_to_CondensedSet.{u}.obj (to_Condensed.{u}.obj A))
    A.colimit_iso_Condensed_obj_aux_nat_iso

def colimit_to_Condensed_obj :
  colimit A.level_Condensed_diagram' ‚ü∂ Condensed_Ab_to_CondensedSet.obj (to_Condensed.obj A) :=
colimit.desc _ A.level_Condensed_diagram_cocone

instance is_iso_colimit_to_Condensed_obj : is_iso A.colimit_to_Condensed_obj :=
begin
  suffices : A.colimit_to_Condensed_obj =
    A.colimit_iso_Condensed_obj.hom, by { rw this, apply_instance },
  dsimp [colimit_iso_Condensed_obj, colimit_to_Condensed_obj],
  apply colimit.hom_ext, intros i,
  dsimp [is_colimit.cocone_point_unique_up_to_iso],
  rw [colimit.Œπ_desc, colimit.Œπ_desc_assoc],
  dsimp [Sheaf.iso.mk],
  ext T : 3, dsimp,
  rw ‚Üê nat_trans.comp_app,
  dsimp [colimit_iso_Condensed_obj_aux_nat_iso, nat_iso.of_components],
  slice_rhs 1 2
  { erw (is_colimit_of_preserves ((evaluation Profinite·µí·µñ (Type (u+1))).obj T)
      (colimit.is_colimit (A.level_Condensed_diagram' ‚ãô
      Sheaf_to_presheaf proetale_topology (Type (u+1))))).fac },
  slice_rhs 1 2 { erw colimit.Œπ_desc },
  ext, refl,
end

end

end CompHausFiltPseuNormGrp

@[simps obj map {fully_applied := ff}]
def CompHausFiltPseuNormGrp‚ÇÅ.to_Condensed :
  CompHausFiltPseuNormGrp‚ÇÅ.{u} ‚•§ Condensed.{u} Ab.{u+1} :=
CompHausFiltPseuNormGrp‚ÇÅ.enlarging_functor ‚ãô CompHausFiltPseuNormGrp.to_Condensed

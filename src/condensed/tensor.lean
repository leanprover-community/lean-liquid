import condensed.adjunctions
import condensed.extr.equivalence
import linear_algebra.tensor_product

import for_mathlib.endomorphisms.functor
import for_mathlib.AddCommGroup_instances
import for_mathlib.AddCommGroup.explicit_products

noncomputable theory

universes u
open_locale tensor_product

open category_theory

namespace AddCommGroup

def linear_equiv_to_iso {A B : AddCommGroup.{u}}
  (e : A ‚âÉ‚Çó[‚Ñ§] B) :
  A ‚âÖ B :=
{ hom := e.to_linear_map.to_add_monoid_hom,
  inv := e.symm.to_linear_map.to_add_monoid_hom,
  hom_inv_id' := begin
    ext t,
    simp,
  end,
  inv_hom_id' := begin
    ext t,
    simp,
  end }

def tensor (A B : AddCommGroup.{u}) : AddCommGroup.{u} :=
AddCommGroup.of (A ‚äó[‚Ñ§] B)

def tensor_uncurry {A B C : AddCommGroup.{u}}
  (e : A ‚ü∂ AddCommGroup.of (B ‚ü∂ C)) : tensor A B ‚ü∂ C :=
linear_map.to_add_monoid_hom $ tensor_product.lift $
let e' := e.to_int_linear_map,
  e'' : (B ‚ü∂ C) ‚Üí‚Çó[‚Ñ§] (B ‚Üí‚Çó[‚Ñ§] C) :=
  add_monoid_hom.to_int_linear_map
  { to_fun := Œª f, f.to_int_linear_map,
    map_zero' := by { ext, refl },
    map_add' := Œª f g, by { ext, refl } } in
e''.comp e'

def tensor_curry {A B C : AddCommGroup.{u}}
  (e : tensor A B ‚ü∂ C) : A ‚ü∂ AddCommGroup.of (B ‚ü∂ C) :=
{ to_fun := Œª a,
  { to_fun := Œª b, e (a ‚äó‚Çú b),
    map_zero' := by { rw [tensor_product.tmul_zero, e.map_zero], },
    map_add' := begin
      intros b c,
      rw [tensor_product.tmul_add, e.map_add],
    end },
  map_zero' := begin
    ext t,
    dsimp,
    rw [tensor_product.zero_tmul, e.map_zero],
  end,
  map_add' := begin
    intros x y, ext t,
    dsimp,
    rw [tensor_product.add_tmul, e.map_add],
  end }

.

@[simps]
def tensor_curry_equiv (A B C : AddCommGroup.{u}) :
  (tensor A B ‚ü∂ C) ‚âÉ+ (A ‚ü∂ (AddCommGroup.of (B ‚ü∂ C))) :=
{ to_fun := tensor_curry,
  inv_fun := tensor_uncurry,
  left_inv := begin
    intros f,
    ext t,
    apply tensor_product.induction_on t,
    { simp, },
    { intros x y, dsimp [tensor_uncurry, tensor_curry],
      simp, },
    { intros x y h1 h2,
      simp only [map_add, h1, h2] }
  end,
  right_inv := Œª f, by { ext, dsimp [tensor_uncurry, tensor_curry], simp, },
  map_add' := Œª x y, by { ext, refl } }

def map_tensor {A A' B B' : AddCommGroup.{u}}
  (f : A ‚ü∂ A') (g : B ‚ü∂ B') : tensor A B ‚ü∂ tensor A' B' :=
(tensor_product.map f.to_int_linear_map g.to_int_linear_map).to_add_monoid_hom

lemma id_helper (A : AddCommGroup.{u}) :
  (ùüô A : A ‚ü∂ A).to_int_linear_map = linear_map.id := rfl

lemma comp_helper {A B C : AddCommGroup.{u}}
  (f : A ‚ü∂ B) (g : B ‚ü∂ C) :
  (f ‚â´ g).to_int_linear_map = g.to_int_linear_map.comp f.to_int_linear_map := rfl

@[simp]
lemma zero_helper {A B : AddCommGroup.{u}} :
  (0 : A ‚ü∂ B).to_int_linear_map = 0 := rfl

@[simp]
lemma map_tensor_id {A B : AddCommGroup.{u}} :
  map_tensor (ùüô A) (ùüô B) = ùüô _ :=
begin
  ext t, dsimp [map_tensor], simp [id_helper],
end

@[simp]
lemma map_tensor_comp_left {A A' A'' B : AddCommGroup.{u}} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') :
  map_tensor (f ‚â´ g) (ùüô B) = map_tensor f (ùüô _) ‚â´ map_tensor g (ùüô _) :=
begin
  ext t,
  rw ‚Üê category.id_comp (ùüô B),
  dsimp [map_tensor], simp only [comp_helper, id_helper, tensor_product.map_comp],
  simp,
end

@[simp]
lemma map_tensor_comp_right {A B B' B'' : AddCommGroup.{u}} (f : B ‚ü∂ B') (g : B' ‚ü∂ B'') :
  map_tensor (ùüô A) (f ‚â´ g) = map_tensor (ùüô _) f ‚â´ map_tensor (ùüô _) g :=
begin
  ext t,
  rw ‚Üê category.id_comp (ùüô A),
  dsimp [map_tensor], simp only [comp_helper, id_helper, tensor_product.map_comp],
  simp,
end

@[simp]
lemma map_tensor_comp_comp {A A' A'' B B' B'' : AddCommGroup.{u}}
  (f : A ‚ü∂ A') (f' : A' ‚ü∂ A'') (g : B ‚ü∂ B') (g' : B' ‚ü∂ B'') :
  map_tensor (f ‚â´ f') (g ‚â´ g') = map_tensor f g ‚â´ map_tensor f' g' :=
begin
  ext t,
  dsimp [map_tensor], simp only [comp_helper, id_helper, tensor_product.map_comp],
  simp,
end

lemma map_tensor_eq_comp {A A' B B' : AddCommGroup.{u}} (f : A ‚ü∂ A') (g : B ‚ü∂ B') :
  map_tensor f g = map_tensor f (ùüô _) ‚â´ map_tensor (ùüô _) g :=
begin
  nth_rewrite 0 ‚Üê category.id_comp g,
  nth_rewrite 0 ‚Üê category.comp_id f,
  rw map_tensor_comp_comp,
end

lemma map_tensor_eq_comp' {A A' B B' : AddCommGroup.{u}} (f : A ‚ü∂ A') (g : B ‚ü∂ B') :
  map_tensor f g = map_tensor (ùüô _) g ‚â´ map_tensor f (ùüô _) :=
begin
  nth_rewrite 0 ‚Üê category.id_comp f,
  nth_rewrite 0 ‚Üê category.comp_id g,
  rw map_tensor_comp_comp,
end

@[simp]
lemma map_tensor_zero_left {A A' B B' : AddCommGroup.{u}} (f : B ‚ü∂ B') :
  map_tensor (0 : A ‚ü∂ A') f = 0 :=
begin
  apply (tensor_curry_equiv _ _ _).injective,
  ext a b,
  dsimp [tensor_curry, map_tensor],
  simp,
end

@[simp]
lemma map_tensor_zero_right {A A' B B' : AddCommGroup.{u}} (f : A ‚ü∂ A') :
  map_tensor f (0 : B ‚ü∂ B') = 0 :=
begin
  apply (tensor_curry_equiv _ _ _).injective,
  ext a b,
  dsimp [tensor_curry, map_tensor],
  simp,
end

lemma tensor_uncurry_curry {A B C D : AddCommGroup.{u}} (f : A ‚ü∂ B) (g : B.tensor C ‚ü∂ D) :
  tensor_uncurry (f ‚â´ tensor_curry g) = map_tensor f (ùüô _) ‚â´ g :=
begin
  apply (tensor_curry_equiv _ _ _).injective,
  erw (tensor_curry_equiv _ _ _).apply_symm_apply,
  ext a c,
  dsimp [tensor_curry, tensor_curry_equiv, map_tensor],
  simp,
end

lemma tensor_curry_uncurry {A B C D : AddCommGroup.{u}}
  (e : A ‚ü∂ AddCommGroup.of (B ‚ü∂ C)) (g : C ‚ü∂ D):
  tensor_curry (tensor_uncurry e ‚â´ g) =
  e ‚â´ (preadditive_yoneda.flip.obj (opposite.op B)).map g := sorry

@[simps]
def tensor_functor : AddCommGroup.{u} ‚•§ AddCommGroup.{u} ‚•§ AddCommGroup.{u} :=
{ obj := Œª A,
  { obj := Œª B, tensor A B,
    map := Œª B B' f, map_tensor (ùüô _) f,
    map_id' := Œª A, map_tensor_id,
    map_comp' := Œª A B C f g, map_tensor_comp_right _ _ },
  map := Œª A A' f,
  { app := Œª B, map_tensor f (ùüô _),
    naturality' := Œª B C g, begin
      dsimp,
      rw [‚Üê map_tensor_eq_comp, ‚Üê map_tensor_eq_comp'],
    end },
  map_id' := begin
    intros A,
    ext B : 2,
    dsimp, exact map_tensor_id,
  end,
  map_comp' := begin
    intros A B C f g,
    ext B : 2,
    dsimp, exact map_tensor_comp_left _ _,
  end }

def tensor_explicit_pi_comparison {Œ± : Type u} [fintype Œ±] (X : Œ± ‚Üí AddCommGroup.{u+1})
  (B : AddCommGroup.{u+1}) :
  tensor (AddCommGroup.of (direct_sum Œ± (Œª i, X i))) B ‚ü∂
  AddCommGroup.of (direct_sum Œ± (Œª i, tensor (X i) B)) :=
direct_sum_lift.{u u+1} _ $ Œª a, map_tensor (direct_sum_œÄ.{u u+1} _ _) (ùüô _)

def tensor_pi_comparison {Œ± : Type u} (X : Œ± ‚Üí AddCommGroup.{u+1})
  (B : AddCommGroup.{u+1}) :
  tensor (‚àè X) B ‚ü∂ ‚àè (Œª a, tensor (X a) B) :=
limits.pi.lift $ Œª b, map_tensor (limits.pi.œÄ _ _) (ùüô _)

open_locale classical

def tensor_explicit_pi_iso {Œ± : Type u}
  (X : Œ± ‚Üí AddCommGroup.{u+1})
  (B : AddCommGroup.{u+1}) :
  (of (direct_sum Œ± (Œª (i : Œ±), ‚Ü•(X i)))).tensor B ‚âÖ
  of (direct_sum Œ± (Œª (i : Œ±), ‚Ü•((X i).tensor B))) :=
{ hom := tensor_uncurry $ direct_sum_desc.{u u+1} X $ Œª i, tensor_curry $
    direct_sum_Œπ.{u u+1} _ i,
  inv := direct_sum_desc.{u u+1} _ $ Œª i,
    map_tensor (direct_sum_Œπ.{u u+1} X i) (ùüô _),
  hom_inv_id' := begin
    apply (tensor_curry_equiv _ _ _).injective,
    ext a b,
    dsimp [tensor_curry, tensor_uncurry, direct_sum_desc],
    simp only [comp_apply, linear_map.to_add_monoid_hom_coe, tensor_product.lift.tmul,
      linear_map.coe_comp, add_monoid_hom.coe_to_int_linear_map, add_monoid_hom.coe_mk,
      direct_sum.to_add_monoid_of, id_apply],
    dsimp [direct_sum_Œπ],
    simp only [direct_sum.to_add_monoid_of],
    dsimp [map_tensor],
    simp only [id_apply],
  end,
  inv_hom_id' := begin
    apply direct_sum_hom_ext'.{u u+1},
    intros i,
    simp only [direct_sum_Œπ_desc_assoc, category.comp_id],
    apply (tensor_curry_equiv _ _ _).injective,
    ext a b k,
    dsimp [tensor_curry, direct_sum_Œπ, direct_sum.of, map_tensor,
      tensor_uncurry, tensor_curry, direct_sum_desc],
    simp only [comp_apply, linear_map.to_add_monoid_hom_coe, tensor_product.map_tmul,
      add_monoid_hom.coe_to_int_linear_map, dfinsupp.single_add_hom_apply, id_apply,
      tensor_product.lift.tmul, linear_map.coe_comp, add_monoid_hom.coe_mk,
      dfinsupp.single_apply],
    dsimp [direct_sum.to_add_monoid],
    simp only [dfinsupp.sum_add_hom_single, add_monoid_hom.coe_mk, dfinsupp.single_apply]
  end }

lemma tensor_explicit_pi_iso_hom_eq {Œ± : Type u} [fintype Œ±]
  (X : Œ± ‚Üí AddCommGroup.{u+1})
  (B : AddCommGroup.{u+1}) :
  (tensor_explicit_pi_iso X B).hom = tensor_explicit_pi_comparison X B :=
begin
  symmetry,
  apply direct_sum_hom_ext.{u u+1}, swap, apply_instance,
  intros j,
  apply (tensor_curry_equiv _ _ _).injective,
  apply direct_sum_hom_ext'.{u u+1}, intros i,
  apply (tensor_curry_equiv _ _ _).symm.injective,
  dsimp,
  simp_rw tensor_uncurry_curry,
  erw [direct_sum_lift_œÄ, ‚Üê map_tensor_comp_comp, category.id_comp],
  dsimp only [tensor_explicit_pi_iso],
  erw [‚Üê category.assoc], let t := _, change _ = t ‚â´ _,
  have ht : t = direct_sum_Œπ.{u u+1} _ i,
  { dsimp [t],
    have := direct_sum_Œπ_desc.{u u+1} (Œª i, tensor (X i) B)
      (Œª i, map_tensor (direct_sum_Œπ.{u u+1} _ i) (ùüô _)) i,
    dsimp at this, rw ‚Üê this, clear this,
    rw category.assoc,
    erw [(tensor_explicit_pi_iso X B).inv_hom_id, category.comp_id] },
  rw ht, clear ht, clear t,
  by_cases i = j,
  { subst h,
    simp [direct_sum_Œπ_œÄ.{u u+1}] },
  { simp [direct_sum_Œπ_œÄ_of_ne.{u u+1} _ _ _ h], }
end

instance is_iso_tensor_explicit_pi_comparison {Œ± : Type u} [fintype Œ±]
  (X : Œ± ‚Üí AddCommGroup.{u+1})
  (B : AddCommGroup.{u+1}) : is_iso (tensor_explicit_pi_comparison X B) :=
begin
  rw ‚Üê tensor_explicit_pi_iso_hom_eq,
  apply_instance
end

lemma tensor_explicit_pi_comparison_comparison {Œ± : Type u}
  [fintype Œ±]
  (X : Œ± ‚Üí AddCommGroup.{u+1})
  (B : AddCommGroup.{u+1}) :
  tensor_pi_comparison X B =
  map_tensor (direct_sum_lift.{u u+1} _ $ limits.pi.œÄ _) (ùüô _) ‚â´
  tensor_explicit_pi_comparison X B ‚â´
  limits.pi.lift (direct_sum_œÄ.{u u+1} (Œª i, tensor (X i) B)) :=
begin
  ext1,
  dsimp [tensor_pi_comparison],
  simp only [limits.limit.lift_œÄ, limits.fan.mk_œÄ_app, category.assoc],
  dsimp [tensor_explicit_pi_comparison],
  rw [direct_sum_lift_œÄ, ‚Üê map_tensor_comp_left, direct_sum_lift_œÄ],
end

instance is_iso_tensor_pi_comparison {Œ± : Type u} [fintype Œ±]
  (X : Œ± ‚Üí AddCommGroup.{u+1})
  (B : AddCommGroup.{u+1}) : is_iso (tensor_pi_comparison X B) :=
begin
  rw tensor_explicit_pi_comparison_comparison,
  apply_with is_iso.comp_is_iso { instances := ff },
  { change is_iso ((tensor_functor.flip.obj B).map _),
    apply_with functor.map_is_iso { instances := ff },
    change is_iso ((limits.limit.is_limit _).cone_point_unique_up_to_iso
      (is_limit_direct_sum_fan.{u u+1} X)).hom,
    apply_instance },
  apply_with is_iso.comp_is_iso { instances := ff }, apply_instance,
  change is_iso ((is_limit_direct_sum_fan.{u u+1} _).cone_point_unique_up_to_iso
    (limits.limit.is_limit _)).hom,
  apply_instance,
  apply_instance
end

def tensor_flip (A B : AddCommGroup.{u}) : A.tensor B ‚âÖ B.tensor A :=
linear_equiv_to_iso (tensor_product.comm _ _ _)

end AddCommGroup

namespace ExtrSheafProd

/-- S ‚Ü¶ M(S) ‚äó A -/
def tensor_presheaf (M : ExtrDisc.{u}·µí·µñ ‚•§ Ab.{u+1}) (A : Ab.{u+1}) :
  ExtrDisc.{u}·µí·µñ ‚•§ Ab.{u+1} :=
M ‚ãô AddCommGroup.tensor_functor.flip.obj A

def tensor (M : ExtrSheafProd.{u} Ab.{u+1}) (A : Ab.{u+1}) :
  ExtrSheafProd.{u} Ab.{u+1} :=
{ val := tensor_presheaf M.val A,
  cond := begin
    introsI Œ± _ X, dsimp [tensor_presheaf, AddCommGroup.tensor_functor],
    let e := _, change is_iso e,
    have hq := M.cond _ X, dsimp at hq, let q := _, change is_iso q at hq,
    have he : e = AddCommGroup.map_tensor q (ùüô _) ‚â´
      AddCommGroup.tensor_pi_comparison _ _,
    { ext1 j,
      dsimp [AddCommGroup.tensor_pi_comparison],
      simp only [‚ÜêAddCommGroup.map_tensor_comp_left, limits.limit.lift_œÄ,
        limits.fan.mk_œÄ_app, category.assoc]},
    rw he, resetI, apply_with is_iso.comp_is_iso { instances := ff },
    swap, apply_instance,
    use AddCommGroup.map_tensor (inv q) (ùüô _),
    split,
    { rw [‚Üê AddCommGroup.map_tensor_comp_left, is_iso.hom_inv_id, AddCommGroup.map_tensor_id], },
    { rw [‚Üê AddCommGroup.map_tensor_comp_left, is_iso.inv_hom_id, AddCommGroup.map_tensor_id], },
  end } -- tensor products commutes with direct sums.

-- Slow, so probably break into pieces
def tensor_functor : ExtrSheafProd.{u} Ab.{u+1} ‚•§ Ab.{u+1} ‚•§ ExtrSheafProd.{u} Ab.{u+1} :=
{ obj := Œª M,
  { obj := Œª A, tensor M A,
    map := Œª A B f,
      ‚ü®{ app := Œª S, AddCommGroup.map_tensor (ùüô _) f,
         naturality' := Œª M N g, begin
          dsimp [tensor, tensor_presheaf, AddCommGroup.tensor_functor],
          simp [‚Üê AddCommGroup.map_tensor_eq_comp, ‚Üê AddCommGroup.map_tensor_eq_comp'],
         end,
         }‚ü©,
    map_id' := begin
      intros A,
      ext S : 3,
      dsimp, simpa,
    end,
    map_comp' := begin
      intros A B C f g,
      ext S : 3,
      dsimp, simp,
    end },
  map := Œª M N f,
  { app := Œª A,
    ‚ü®{ app := Œª S, AddCommGroup.map_tensor (f.val.app _) (ùüô _),
       naturality' := begin
        intros S T e,
        dsimp [tensor, tensor_presheaf, AddCommGroup.tensor_functor],
        simp only [‚Üê AddCommGroup.map_tensor_comp_left, nat_trans.naturality],
       end }‚ü©,
    naturality' := begin
      intros A B g,
      ext S : 3,
      dsimp,
      simp [‚Üê AddCommGroup.map_tensor_eq_comp, ‚Üê AddCommGroup.map_tensor_eq_comp'],
    end },
  map_id' := begin
    intros X,
    ext : 5,
    dsimp,
    simpa,
  end,
  map_comp' := begin
    intros X Y Z f g,
    ext : 5,
    dsimp, simp,
  end }

.

@[simps]
instance hom_has_add {M N : ExtrSheafProd.{u} Ab.{u+1}} : has_add (M ‚ü∂ N) :=
‚ü®Œª f g, ‚ü®f.val + g.val‚ü©‚ü©

@[simps]
instance hom_has_zero {M N : ExtrSheafProd.{u} Ab.{u+1}} : has_zero (M ‚ü∂ N) :=
‚ü®‚ü®0‚ü©‚ü©

@[simps]
instance hom_has_neg {M N : ExtrSheafProd.{u} Ab.{u+1}} : has_neg (M ‚ü∂ N) :=
‚ü®Œª f, ‚ü®-f.val‚ü©‚ü©

@[simps]
instance hom_has_sub {M N : ExtrSheafProd.{u} Ab.{u+1}} : has_sub (M ‚ü∂ N) :=
‚ü®Œª f g, ‚ü®f.val - g.val‚ü©‚ü©

instance preadditive : preadditive (ExtrSheafProd.{u} Ab.{u+1}) :=
{ hom_group := Œª P Q,
  { add_assoc := Œª f g h, by { ext1, dsimp, rw add_assoc },
    zero_add := Œª f, by { ext1, dsimp, rw zero_add },
    add_zero := Œª f, by { ext1, dsimp, rw add_zero },
    nsmul := Œª n f, ‚ü®n ‚Ä¢ f.val‚ü©,
    nsmul_zero' := Œª f, by { ext1, dsimp, simp, },
    nsmul_succ' := Œª n f, by { ext1, dsimp, exact succ_nsmul f.val n },
    sub_eq_add_neg := Œª f g, by { ext1, dsimp, exact sub_eq_add_neg f.val g.val },
    zsmul := Œª n f, ‚ü®n ‚Ä¢ f.val‚ü©,
    zsmul_zero' := Œª f, by { ext1, dsimp, simp },
    zsmul_succ' := Œª n f, by { ext1, dsimp, rw [add_zsmul, one_zsmul, add_comm], },
    zsmul_neg' := Œª n f, by { ext1, dsimp, simpa, },
    add_left_neg := Œª f, by { ext1, dsimp, simp },
    add_comm := Œª f g, by { ext1, dsimp, rw add_comm },
    ..(infer_instance : has_add _),
    ..(infer_instance : has_neg _),
    ..(infer_instance : has_zero _),
    ..(infer_instance : has_sub _) },
  add_comp' := Œª P Q R f f' g, by { ext1, dsimp, simp },
  comp_add' := Œª P Q R f g g', by { ext1, dsimp, simp } }

def evaluation (S : ExtrDisc.{u}) :
  ExtrSheafProd.{u} Ab.{u+1} ‚•§ Ab.{u+1} :=
ExtrSheafProd_to_presheaf _ ‚ãô (evaluation _ _).obj (opposite.op S)

instance evaluation_additive (S) : functor.additive (evaluation S) :=
‚ü®Œª M N f g, rfl‚ü©

def half_internal_hom (A : AddCommGroup.{u+1}) (M : ExtrSheafProd.{u} Ab.{u+1}) :
  ExtrSheafProd.{u} Ab.{u+1} :=
{ val :=
  { obj := Œª S, AddCommGroup.of (A ‚ü∂ M.val.obj S),
    map := Œª X Y f, (preadditive_yoneda.flip.obj (opposite.op A)).map $ M.val.map f,
    map_id' := begin
      intros S,
      dsimp, simpa,
    end,
    map_comp' := begin
      intros R S T f g,
      dsimp,
      simp,
    end },
  cond := begin
    introsI Œ± _ X, dsimp,
    let t := _, change is_iso t,
    have := M.cond Œ± X, dsimp at this, let e := _, change is_iso e at this, resetI,
    let q : AddCommGroup.of (A ‚ü∂ M.val.obj (opposite.op (ExtrDisc.sigma X))) ‚âÖ
      AddCommGroup.of (A ‚ü∂ (‚àè (Œª i, M.val.obj (opposite.op (X i))))) :=
      (preadditive_yoneda.flip.obj (opposite.op A)).map_iso (as_iso e),
    let s : AddCommGroup.of (A ‚ü∂ (‚àè (Œª i, M.val.obj (opposite.op (X i))))) ‚ü∂
      ‚àè (Œª i, AddCommGroup.of (A ‚ü∂ M.val.obj (opposite.op (X i)))) :=
      limits.pi.lift (Œª i, (preadditive_yoneda.flip.obj (opposite.op A)).map
        (limits.pi.œÄ _ i)),
    have ht : t = q.hom ‚â´ s,
    { dsimp [t, q, s, e], ext1,
      simp only [limits.limit.lift_œÄ, limits.fan.mk_œÄ_app, category.assoc],
      rw [‚Üê nat_trans.comp_app, ‚Üê functor.map_comp, limits.limit.lift_œÄ],
      refl },
    rw ht, clear ht,
    suffices : is_iso s,
    { resetI, apply is_iso.comp_is_iso },
    -- Now we need to show that `Hom(A,(Œ† i, X i)) = Œ† i, Hom(A,X i)`.
    apply AddCommGroup.is_iso_hom_product_comparison.{u u+1},
  end }

def tensor_uncurry {A : AddCommGroup.{u+1}} {M N : ExtrSheafProd.{u} Ab.{u+1}}
  (e : M ‚ü∂ half_internal_hom A N) :
  tensor M A ‚ü∂ N := ExtrSheafProd.hom.mk $
{ app := Œª S, AddCommGroup.tensor_uncurry $ e.val.app _,
  naturality' := begin
    intros X Y f,
    erw ‚Üê AddCommGroup.tensor_uncurry_curry,
    apply (AddCommGroup.tensor_curry_equiv _ _ _).injective,
    erw (AddCommGroup.tensor_curry_equiv _ _ _).apply_symm_apply,
    dsimp [AddCommGroup.tensor_curry_equiv],
    erw [AddCommGroup.tensor_curry_uncurry, ‚Üê nat_trans.naturality,
      ‚Üê AddCommGroup.tensor_curry_equiv_apply,
      ‚Üê AddCommGroup.tensor_curry_equiv_symm_apply,
      (AddCommGroup.tensor_curry_equiv _ _ _).apply_symm_apply],
  end }

def tensor_curry {A : AddCommGroup.{u+1}} {M N : ExtrSheafProd.{u} Ab.{u+1}}
  (e : M.tensor A ‚ü∂ N) : M ‚ü∂ half_internal_hom A N := ExtrSheafProd.hom.mk $
{ app := Œª S, AddCommGroup.tensor_curry $ e.val.app _,
  naturality' := begin
    intros X Y f,
    dsimp [half_internal_hom],
    erw [‚Üê AddCommGroup.tensor_curry_uncurry],
    apply (AddCommGroup.tensor_curry_equiv _ _ _).symm.injective,
    simp_rw ‚Üê AddCommGroup.tensor_curry_equiv_apply,
    rw (AddCommGroup.tensor_curry_equiv _ _ _).symm_apply_apply,
    rw ‚Üê AddCommGroup.tensor_curry_equiv_symm_apply,
    rw (AddCommGroup.tensor_curry_equiv _ _ _).symm_apply_apply,
    dsimp,
    rw [AddCommGroup.tensor_uncurry_curry, ‚Üê nat_trans.naturality],
    refl,
  end }

end ExtrSheafProd

namespace condensed

/-- This is the functor that sends `A : Ab` to `M ‚äó A`,
where `M` is a condensed abelian group, functorial in both `M` and `A`. -/
def tensor_functor : Condensed.{u} Ab.{u+1} ‚•§ Ab.{u+1} ‚•§ Condensed.{u} Ab.{u+1} :=
(Condensed_ExtrSheafProd_equiv Ab.{u+1}).functor ‚ãô
((whiskering_right _ _ _).obj $ ((whiskering_right _ _ _).obj
  (Condensed_ExtrSheafProd_equiv Ab.{u+1}).inverse)).obj
  ExtrSheafProd.tensor_functor

/-- This is the tensor product of a condensed abelian group `M` and `A : Ab`. -/
def tensor (M : Condensed.{u} Ab.{u+1}) (A : Ab.{u+1}) : Condensed.{u} Ab.{u+1} :=
(tensor_functor.obj M).obj A

/-- Restrincting to `ExtrDisc` works as expeceted. -/
def tensor_functor_conj_iso :
  (Condensed_ExtrSheafProd_equiv Ab.{u+1}).inverse ‚ãô
  ((whiskering_right _ _ _).obj $ ((whiskering_right _ _ _).obj
    (Condensed_ExtrSheafProd_equiv Ab.{u+1}).functor)).obj tensor_functor ‚âÖ
  ExtrSheafProd.tensor_functor :=
nat_iso.of_components
(Œª X, begin
  refine functor.associator _ _ _ ‚â™‚â´ _,
  refine iso_whisker_left _ (Condensed_ExtrSheafProd_equiv Ab.{u+1}).counit_iso ‚â™‚â´ _,
  refine functor.right_unitor _ ‚â™‚â´ _,
  refine functor.map_iso _ _,
  exact ((Condensed_ExtrSheafProd_equiv Ab.{u+1}).counit_iso.app X),
end)
begin
  intros X Y f, ext : 2,
  dsimp [tensor_functor],
  simp only [equivalence.fun_inv_map, equivalence.equivalence_mk'_counit,
    equivalence.equivalence_mk'_counit_inv, functor.map_comp, nat_trans.comp_app,
    category.assoc, iso.inv_hom_id_app_assoc, category.id_comp,
    nat_iso.cancel_nat_iso_hom_left],
  rw [‚Üê nat_trans.comp_app, ‚Üê functor.map_comp, ‚Üê nat_trans.comp_app],
  have : (Condensed_ExtrSheafProd_equiv Ab).counit_iso.inv.app Y ‚â´
    (Condensed_ExtrSheafProd_equiv Ab).counit_iso.hom.app Y = ùüô _,
  { rw [‚Üê nat_trans.comp_app, iso.inv_hom_id], refl },
  rw this,
  simp only [nat_trans.comp_app],
  dsimp,
  simp only [category_theory.functor.map_id, nat_trans.id_app, category.comp_id],
end

def tensor_functor_conj_iso' :
  tensor_functor ‚ãô (whiskering_right _ _ _).obj
  (Condensed_ExtrSheafProd_equiv _).functor ‚âÖ
  (Condensed_ExtrSheafProd_equiv _).functor ‚ãô ExtrSheafProd.tensor_functor :=
nat_iso.of_components
(Œª X, begin
  dsimp [tensor_functor],
  refine functor.associator _ _ _ ‚â™‚â´ _,
  refine _ ‚â™‚â´ functor.right_unitor _,
  refine ((whiskering_left _ _ _).obj _).map_iso _,
  refine (Condensed_ExtrSheafProd_equiv _).counit_iso,
end)
begin
  intros X Y f, ext : 2,
  dsimp [tensor_functor],
  simp, dsimp, simp,
end

/-- The tensor product behaves in the naive way when evaluated
on extremally disconnected sets. -/
def tensor_eval_iso
  (M : Condensed.{u} Ab.{u+1}) (A : Ab.{u+1}) (S : ExtrDisc.{u}) :
  (tensor M A).val.obj (opposite.op S.val) ‚âÖ
  AddCommGroup.of (M.val.obj (opposite.op S.val) ‚äó[‚Ñ§] A) :=
let e := (tensor_functor_conj_iso'.app M).app A,
  e' := (ExtrSheafProd_to_presheaf _).map_iso e in
e'.app (opposite.op S)

/-- A variant of the tensor product functor for the endormophism category. -/
def endo_tensor :
  (endomorphisms $ Condensed.{u} Ab.{u+1}) ‚•§ Ab.{u+1} ‚•§
  (endomorphisms $ Condensed.{u} Ab.{u+1}) :=
functor.flip $
{ obj := Œª A, (tensor_functor.flip.obj A).map_endomorphisms,
  map := Œª A B f, nat_trans.map_endomorphisms $ tensor_functor.flip.map f }

end condensed

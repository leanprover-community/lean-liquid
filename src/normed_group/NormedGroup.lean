import algebra.punit_instances
import category_theory.concrete_category.bundled_hom
import category_theory.limits.shapes.zero
import category_theory.limits.shapes.kernels
import category_theory.limits.creates

import analysis.normed_space.normed_group_hom

import for_mathlib.normed_group_quotient

/-!
# The category of seminormed abelian groups and continuous group homomorphisms

This file in particular contains a robust API for cokernels of morphisms
of normed groups.

## TODO

This file would sit well in mathlib.

-/

noncomputable theory

universes u v

-- move this
section for_mathlib

-- These instances are in #7616

instance punit.uniform_space : uniform_space punit := ‚ä•

noncomputable
instance punit.metric_space : metric_space punit :=
{ dist := Œª _ _, 0,
  dist_self := Œª _, rfl,
  dist_comm := Œª _ _, rfl,
  eq_of_dist_eq_zero := Œª _ _ _, subsingleton.elim _ _,
  dist_triangle := Œª _ _ _, show (0:‚Ñù) ‚â§ 0 + 0, by rw add_zero,
  .. punit.uniform_space }

noncomputable
instance punit.normed_group : normed_group punit :=
{ norm := function.const _ 0,
  dist_eq := Œª _ _, rfl,
  .. punit.add_comm_group, .. punit.metric_space }

-- move this, better name?
lemma norm_le_add_norm_add {V : Type*} [semi_normed_group V] (x y : V) :
  ‚à•x‚à• ‚â§ ‚à•x + y‚à• + ‚à•y‚à• :=
calc ‚à•x‚à• = ‚à•x + y - y‚à• : by rw add_sub_cancel
... ‚â§ ‚à•x + y‚à• + ‚à•y‚à• : norm_sub_le _ _

end for_mathlib

open category_theory

/-- The category of normed abelian groups and bounded group homomorphisms. -/
def NormedGroup : Type (u+1) := bundled semi_normed_group

namespace NormedGroup

instance bundled_hom : bundled_hom @normed_group_hom :=
‚ü®@normed_group_hom.to_fun, @normed_group_hom.id, @normed_group_hom.comp, @normed_group_hom.coe_inj‚ü©

attribute [derive [has_coe_to_sort, large_category, concrete_category]] NormedGroup

/-- Construct a bundled `NormedGroup` from the underlying type and typeclass. -/
def of (M : Type u) [semi_normed_group M] : NormedGroup := bundled.of M

noncomputable
instance : has_zero NormedGroup := ‚ü®of punit‚ü©

noncomputable
instance : inhabited NormedGroup := ‚ü®0‚ü©

instance (M : NormedGroup) : semi_normed_group M := M.str

@[simp] lemma coe_of (V : Type u) [normed_group V] : (NormedGroup.of V : Type u) = V := rfl

@[simp] lemma coe_id (V : NormedGroup) : ‚áë(ùüô V) = id := rfl

instance : limits.has_zero_morphisms.{u (u+1)} NormedGroup :=
{ comp_zero' := by { intros, apply normed_group_hom.zero_comp },
  zero_comp' := by { intros, apply normed_group_hom.comp_zero } }

lemma iso_isometry_of_norm_noninc {V W : NormedGroup} (i : V ‚âÖ W)
  (h1 : i.hom.norm_noninc) (h2 : i.inv.norm_noninc) :
  isometry i.hom :=
begin
  apply normed_group_hom.isometry_of_norm,
  intro v,
  apply le_antisymm (h1 v),
  calc ‚à•v‚à• = ‚à•i.inv (i.hom v)‚à• : by rw [coe_hom_inv_id]
  ... ‚â§ ‚à•i.hom v‚à• : h2 _,
end

section equalizers_and_kernels

open category_theory.limits

/-- The equalizer cone for a parallel pair of morphisms of normed groups. -/
def parallel_pair_cone {V W : NormedGroup.{u}} (f g : V ‚ü∂ W) :
  cone (parallel_pair f g) :=
@fork.of_Œπ _ _ _ _ _ _ (of (f - g).ker) (normed_group_hom.incl (f - g).ker) $
begin
  ext v,
  have : v.1 ‚àà (f - g).ker := v.2,
  simpa only [normed_group_hom.incl_apply, pi.zero_apply, coe_comp, normed_group_hom.coe_zero,
    subtype.val_eq_coe, normed_group_hom.mem_ker,
    normed_group_hom.coe_sub, pi.sub_apply, sub_eq_zero] using this
end

instance has_limit_parallel_pair {V W : NormedGroup.{u}} (f g : V ‚ü∂ W) :
  has_limit (parallel_pair f g) :=
{ exists_limit := nonempty.intro
  { cone := parallel_pair_cone f g,
    is_limit := fork.is_limit.mk _
      (Œª c, normed_group_hom.ker.lift (fork.Œπ c) _ $
      show normed_group_hom.comp_hom (f - g) c.Œπ = 0,
      by { rw [add_monoid_hom.map_sub, add_monoid_hom.sub_apply, sub_eq_zero], exact c.condition })
      (Œª c, normed_group_hom.ker.incl_comp_lift _ _ _)
      (Œª c g h, by { ext x, dsimp, rw ‚Üê h, refl }) } }

instance : limits.has_equalizers.{u (u+1)} NormedGroup :=
@has_equalizers_of_has_limit_parallel_pair NormedGroup _ $ Œª V W f g,
  NormedGroup.has_limit_parallel_pair f g

instance : limits.has_kernels.{u (u+1)} NormedGroup :=
by apply_instance

end equalizers_and_kernels

section cokernels

variables {A B C : NormedGroup.{u}}

/-- The cokernel of a morphism of normed groups. -/
@[simp]
noncomputable
def coker (f : A ‚ü∂ B) : NormedGroup := NormedGroup.of $
  quotient_add_group.quotient f.range

/-- The projection onto the cokernel. -/
@[simp]
noncomputable
def coker.œÄ {f : A ‚ü∂ B} : B ‚ü∂ coker f :=
f.range.normed_mk

lemma coker.œÄ_surjective {f : A ‚ü∂ B} :
  function.surjective (coker.œÄ : B ‚Üí coker f) :=
surjective_quot_mk _

lemma coker.œÄ_is_quotient {f : A ‚ü∂ B} :
  add_subgroup.is_quotient (coker.œÄ : B ‚ü∂ coker f) :=
add_subgroup.is_quotient_quotient _

lemma coker.œÄ_norm_noninc {f : A ‚ü∂ B} :
  (coker.œÄ : B ‚ü∂ coker f).norm_noninc :=
NormedGroup.coker.œÄ_is_quotient.norm_le

instance coker.œÄ_epi {f : A ‚ü∂ B} : epi (coker.œÄ : B ‚ü∂ coker f) :=
begin
  constructor,
  intros Z g h H,
  ext x,
  rcases coker.œÄ_surjective x with ‚ü®x,rfl‚ü©,
  change (coker.œÄ ‚â´ g) _ = _,
  rw [H],
  refl,
end

open normed_group_hom

/-- Lift (aka descend) a morphism to the cokernel. -/
noncomputable
def coker.lift {f : A ‚ü∂ B} {g : B ‚ü∂ C} (cond : f ‚â´ g = 0) : coker f ‚ü∂ C :=
add_subgroup.lift _ g (begin
  rintros _ ‚ü®b,rfl‚ü©,
  change (f ‚â´ g) b = 0,
  simp [cond]
end)

@[simp]
lemma coker.lift_comp_œÄ {f : A ‚ü∂ B} {g : B ‚ü∂ C} {cond : f ‚â´ g = 0} :
  coker.œÄ ‚â´ coker.lift cond = g :=
begin
  ext,
  rw ‚Üê f.range.lift_mk g,
  refl,
  rintro _ ‚ü®b,rfl‚ü©,
  change (f ‚â´ g) b = 0,
  simp [cond],
end

@[simp]
lemma coker.lift_comp_œÄ_apply {f : A ‚ü∂ B} {g : B ‚ü∂ C} {cond : f ‚â´ g = 0} (x : B) :
  coker.lift cond (coker.œÄ x) = g x :=
show (coker.œÄ ‚â´ coker.lift cond) x = g x, by rw coker.lift_comp_œÄ

lemma coker.lift_unique {f : A ‚ü∂ B} {g : B ‚ü∂ C} {cond : f ‚â´ g = 0} {h : coker f ‚ü∂ C} :
  coker.œÄ ‚â´ h = g ‚Üí h = coker.lift cond := add_subgroup.lift_unique _ _ _ _

lemma coker.comp_pi_eq_zero {f : A ‚ü∂ B} : f ‚â´ (coker.œÄ : B ‚ü∂ coker f) = 0 :=
begin
  ext a,
  rw [coe_zero, pi.zero_apply, coe_comp, coker.œÄ, ‚Üê mem_ker, f.range.ker_normed_mk],
  exact set.mem_range_self a
end

@[simp]
lemma coker.pi_apply_dom_eq_zero {f : A ‚ü∂ B} (x : A) : (coker.œÄ : B ‚ü∂ coker f) (f x) = 0 :=
show (f ‚â´ (coker.œÄ : B ‚ü∂ coker f)) x = 0, by { rw [coker.comp_pi_eq_zero], refl }

variable {D : NormedGroup.{u}}

lemma coker.lift_comp_eq_lift {f : A ‚ü∂ B} {g : B ‚ü∂ C} {h : C ‚ü∂ D} {cond : f ‚â´ g = 0} :
  coker.lift cond ‚â´ h = coker.lift (show f ‚â´ (g ‚â´ h) = 0,
    by rw [‚Üê category_theory.category.assoc, cond, limits.zero_comp]) :=
coker.lift_unique $ by rw [‚Üê category_theory.category.assoc, coker.lift_comp_œÄ]

lemma coker.lift_zero {f : A ‚ü∂ B} :
  coker.lift (show f ‚â´ (0 : B ‚ü∂ C) = 0, from category_theory.limits.comp_zero) = 0 :=
eq.symm $ coker.lift_unique category_theory.limits.comp_zero

section
open_locale nnreal

-- maybe prove this for `normed_group_hom` first, without the category lib
lemma coker.lift_bound_by {f : A ‚ü∂ B} {g : B ‚ü∂ C} {cond : f ‚â´ g = 0} {c : ‚Ñù‚â•0}
  (hg : g.bound_by c) :
  (coker.lift cond).bound_by c :=
begin
  intros x,
  by_cases hc : c = 0,
  { simp only [hc, nnreal.coe_zero, zero_mul] at hg ‚ä¢,
    obtain ‚ü®x, rfl‚ü© := coker.œÄ_surjective x,
    show ‚à•g x‚à• ‚â§ 0,
    calc ‚à•g x‚à• ‚â§ 0 * ‚à•x‚à• : hg x
    ... = 0 : zero_mul _ },
  { replace hc : 0 < c := pos_iff_ne_zero.mpr hc,
    apply le_of_forall_pos_le_add,
    intros Œµ hŒµ,
    have aux : 0 < (Œµ / c) := div_pos hŒµ hc,
    obtain ‚ü®x, rfl, Hx‚ü© : ‚àÉ x', coker.œÄ x' = x ‚àß ‚à•x'‚à• < ‚à•x‚à• + (Œµ / c) :=
      coker.œÄ_is_quotient.norm_lift aux _,
    rw coker.lift_comp_œÄ_apply,
    calc ‚à•g x‚à• ‚â§ c * ‚à•x‚à• : hg x
    ... ‚â§ c * (‚à•coker.œÄ x‚à• + Œµ / c) : (mul_le_mul_left _).mpr Hx.le
    ... = c * _ + Œµ : _,
    { exact_mod_cast hc },
    { rw [mul_add, mul_div_cancel'], exact_mod_cast hc.ne' } },
end

-- maybe prove this for `normed_group_hom` first, without the category lib
lemma coker.lift_norm_noninc {f : A ‚ü∂ B} {g : B ‚ü∂ C} {cond : f ‚â´ g = 0}
  (hg : g.norm_noninc) :
  (coker.lift cond).norm_noninc :=
Œª x, by simpa only [one_mul, nnreal.coe_one] using coker.lift_bound_by hg.bound_by_one x

end

-- maybe prove this for `normed_group_hom` first, without the category lib
lemma neg_norm_noninc (f : A ‚ü∂ B) (hf : f.norm_noninc) : (-f).norm_noninc :=
Œª x, (norm_neg (f x)).le.trans (hf x)

/-- The downwards map between the cokernels making the diagram commute.

    A ----> B ---> coker
    |       |
    |       |
   \/      \/
    C ----> D ---> coker
 -/
noncomputable def coker.map {fab : A ‚ü∂ B} {fbd : B ‚ü∂ D} {fac : A ‚ü∂ C} {fcd : C ‚ü∂ D}
  (h : fab ‚â´ fbd = fac ‚â´ fcd) : coker fab ‚ü∂ coker fcd :=
coker.lift (show fab ‚â´ fbd ‚â´ coker.œÄ = 0, by rw [‚Üê category_theory.category.assoc, h,
  category_theory.category.assoc, coker.comp_pi_eq_zero, limits.comp_zero])

/-
If this commutes
    A ----> B ---> B'
    |       |      |
    |       |      |
   \/      \/      \/
    C ----> D ---> D'

and d^2=0 on both rows then this commutes:

coker (A ‚Üí B) ----> E
   |                |
   | coker.map      |
   |                |
   \/               \/
coker (C ‚Üí D) ----> F
-/

lemma coker.map_lift_comm {B' D' : NormedGroup}
  {fab : A ‚ü∂ B} {fbd : B ‚ü∂ D} {fac : A ‚ü∂ C} {fcd : C ‚ü∂ D}
  {h : fab ‚â´ fbd = fac ‚â´ fcd} {fbb' : B ‚ü∂ B'} {fdd' : D ‚ü∂ D'}
  {condb : fab ‚â´ fbb' = 0} {condd : fcd ‚â´ fdd' = 0} {g : B' ‚ü∂ D'}
  (h' : fbb' ‚â´ g = fbd ‚â´ fdd'):
  coker.lift condb ‚â´ g = coker.map h ‚â´ coker.lift condd :=
begin
  delta coker.map,
  simp only [‚Üê cancel_epi (coker.œÄ : _ ‚ü∂ coker fab), ‚Üê category.assoc, coker.lift_comp_œÄ, h'],
  rw [category.assoc, coker.lift_comp_œÄ]
end

lemma coker.lift_comp_eq_zero {f : A ‚ü∂ B} {g : B ‚ü∂ C} {h : C ‚ü∂ D} (cond : f ‚â´ g = 0)
  (cond2 : g ‚â´ h = 0) : coker.lift cond ‚â´ h = 0 :=
begin
  rw [‚Üê cancel_epi (coker.œÄ : _ ‚ü∂ coker f), ‚Üê category.assoc, coker.lift_comp_œÄ],
  simp [cond2],
end

end cokernels

end NormedGroup
#lint- only unused_arguments def_lemma doc_blame

import topology.category.Profinite.as_limit
import for_mathlib.discrete_quotient
import for_mathlib.Fintype

noncomputable theory

namespace Profinite

open category_theory
open category_theory.limits

universes v u

variables {C : Type u} [category.{v} C] (F : Fintype.{v} ‚•§ C)

/-- Change a cone with respect to a morphism from `Profinite`. -/
@[simps]
def change_cone {X Y : Profinite} (f : X ‚ü∂ Y) (D : cone (X.fintype_diagram ‚ãô F)) :
  cone (Y.fintype_diagram ‚ãô F) :=
{ X := D.X,
  œÄ :=
  { app := Œª S, D.œÄ.app (S.comap f.continuous) ‚â´ F.map (discrete_quotient.map $ le_refl _),
    naturality' := begin
      rintros I J h,
      dsimp,
      simp only [category.id_comp, category.assoc],
      rw ‚Üê D.w (hom_of_le $ discrete_quotient.comap_mono _ $ le_of_hom h),
      simp only [category.assoc, ‚Üê F.map_comp, functor.comp_map],
      congr' 2,
      ext ‚ü®t‚ü©, refl,
    end } } .

-- Assume that C has enough limits.
variable [‚àÄ X : Profinite, has_limits_of_shape (discrete_quotient X) C]

-- PROJECT: Prove that this is isomorphic to the right Kan extension along `Fintype.to_Profinite`.
/-- Extend a functor `Fintype ‚•§ C` to `Profinite`. -/
@[simps]
def extend : Profinite ‚•§ C :=
{ obj := Œª X, limit (X.fintype_diagram ‚ãô F),
  map := Œª X Y f, limit.lift _ (change_cone _ f _),
  map_id' := begin
    intros X,
    ext S,
    dsimp,
    simp only [limit.lift_œÄ, coe_id, change_cone_œÄ_app, limit.cone_œÄ, category.id_comp],
    erw discrete_quotient.map_id,
    change _ ‚â´ F.map (ùüô _) = _,
    rw [F.map_id, category.comp_id],
    congr,
    exact S.comap_id,
  end,
  map_comp' := begin
    intros X Y Z f g,
    ext S,
    dsimp,
    simp only [limit.lift_œÄ, change_cone_œÄ_app,
      limit.cone_œÄ, limit.lift_œÄ_assoc, coe_comp, category.assoc, ‚Üê F.map_comp],
    congr,
    exact discrete_quotient.map_comp _ _,
  end } .

/-- discrete quotients of a finite type has an initial object given by `‚ä•`. -/
@[simps]
def bot_initial (X : Fintype) :
  is_initial (‚ä• : discrete_quotient (Fintype.to_Profinite.obj X)) :=
{ desc := Œª S, hom_of_le bot_le }

/-- The extension of `F : Fintype ‚•§ C` extends `F`. -/
@[simps]
def extend_extends : Fintype.to_Profinite ‚ãô extend F ‚âÖ F :=
nat_iso.of_components (Œª X, begin
  dsimp only [extend, functor.comp_obj],
  let Y := Fintype.to_Profinite.obj X,
  let D := limit.is_limit (Y.fintype_diagram ‚ãô F),
  let E := limit_of_diagram_initial (bot_initial X) (Y.fintype_diagram ‚ãô F),
  letI : topological_space X := ‚ä•,
  let e : Fintype.of (‚ä• : discrete_quotient X) ‚âÖ X :=
    Fintype.iso_of_equiv (equiv.of_bijective _ (discrete_quotient.proj_bot_bijective _)).symm,
  let g := D.cone_point_unique_up_to_iso E,
  exact g ‚â™‚â´ F.map_iso e,
end) begin
  intros X Y f,
  letI : topological_space X := ‚ä•,
  letI : topological_space Y := ‚ä•,
  have hf : continuous f := continuous_bot,
  let A := Fintype.to_Profinite.obj X,
  let B := Fintype.to_Profinite.obj Y,
  dsimp [is_limit.cone_point_unique_up_to_iso, limit_of_diagram_initial],
  simp only [change_cone_œÄ_app, limit.cone_œÄ, limit.lift_œÄ_assoc, category.assoc],
  let e : (‚ä• : discrete_quotient X) ‚ü∂ (‚ä• : discrete_quotient Y).comap hf :=
    hom_of_le bot_le,
  erw ‚Üê limit.w (A.fintype_diagram ‚ãô F) e,
  simp only [category.assoc, ‚Üê F.map_comp, functor.comp_map],
  congr' 2,
  simp_rw [‚Üê iso.inv_comp_eq, ‚Üê category.assoc],
  symmetry,
  rw ‚Üê iso.comp_inv_eq,
  refl,
end .

end Profinite

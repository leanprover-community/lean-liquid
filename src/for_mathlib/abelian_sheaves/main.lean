import category_theory.sites.limits

import for_mathlib.abelian_sheaves.functor_category
import for_mathlib.sheaf
import for_mathlib.abelian_sheaves.left_exact

namespace category_theory
namespace Sheaf


noncomputable theory

universes w v u
variables {C : Type (max v u)} [category.{v} C] {J : grothendieck_topology C}
variables {A : Type w} [category.{max v u} A]

section has_zero_morphisms

variables [limits.has_zero_morphisms A]

instance : limits.has_zero_morphisms (Sheaf J A) :=
{ has_zero := Î» X Y, âŸ¨âŸ¨0âŸ©âŸ©,
  comp_zero' := Î» X Y f Z, Sheaf.hom.ext _ _ $ limits.comp_zero,
  zero_comp' := Î» X Y Z f, Sheaf.hom.ext _ _ $ limits.zero_comp }

end has_zero_morphisms

section parallel_pair

def parallel_pair_iso {F G : Sheaf J A} (Î· Î³ : F âŸ¶ G) :
  limits.parallel_pair Î· Î³ â‹™ Sheaf_to_presheaf J A â‰… limits.parallel_pair Î·.val Î³.val :=
nat_iso.of_components
(Î» x,
match x with
| limits.walking_parallel_pair.zero := eq_to_iso rfl
| limits.walking_parallel_pair.one := eq_to_iso rfl
end) begin
  rintro (x|x) (y|y) (f|f|f),
  any_goals { refl },
  any_goals { ext, dsimp [parallel_pair_iso._match_1], simp },
end

end parallel_pair

section kernels

variables [limits.has_zero_morphisms A]
-- TODO: Add some instances that derive the following from `[has_kernels A]`.
variables [limits.has_limits_of_shape.{_ (max v u)} limits.walking_parallel_pair A]

def kernel_sheaf {F G : Sheaf J A} (Î· : F âŸ¶ G) : Sheaf J A :=
{ val := limits.kernel.{(max v u)} ((Sheaf_to_presheaf J A).map Î·),
  cond := begin
    haveI : limits.has_limit (limits.parallel_pair Î· 0 â‹™ Sheaf_to_presheaf J A) := begin
      apply limits.has_limit_of_iso (parallel_pair_iso _ _).symm,
      apply_instance,
    end,
    let e : limits.limit (limits.parallel_pair Î· 0 â‹™ Sheaf_to_presheaf J A) â‰…
      limits.kernel Î·.val := limits.has_limit.iso_of_nat_iso (parallel_pair_iso _ _),
    apply presheaf.is_sheaf_of_iso J e.symm,
    apply is_sheaf_of_is_limit,
    apply limits.limit.is_limit,
  end }

def kernel_Î¹ {F G : Sheaf J A} (Î· : F âŸ¶ G) : kernel_sheaf Î· âŸ¶ F :=
âŸ¨limits.kernel.Î¹ _âŸ©

def kernel_fork {F G : Sheaf J A} (Î· : F âŸ¶ G) : limits.fork Î· 0 :=
limits.fork.of_Î¹ (kernel_Î¹ Î·) $
by { simp only [limits.comp_zero], ext1, apply limits.kernel.condition }

def is_limit_kernel_fork {F G : Sheaf J A} (Î· : F âŸ¶ G) : limits.is_limit (kernel_fork Î·) :=
limits.is_limit_aux _ (Î» S, âŸ¨limits.kernel.lift _ S.Î¹.val $ congr_arg Sheaf.hom.val S.conditionâŸ©)
(by { intros S, ext1, apply limits.kernel.lift_Î¹, })
begin
  intros S m hm,
  ext : 2,
  simp only [limits.kernel.lift_Î¹],
  exact congr_arg Sheaf.hom.val hm,
end

-- Sanity check
example : limits.has_kernels (Sheaf J A) := by apply_instance

def kernel_iso_kernel_sheaf {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  limits.kernel Î· â‰… kernel_sheaf Î· :=
(limits.limit.is_limit _).cone_point_unique_up_to_iso (is_limit_kernel_fork _)

@[simp, reassoc]
lemma kernel_iso_kernel_sheaf_hom_Î¹ {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  (kernel_iso_kernel_sheaf Î·).hom â‰« kernel_Î¹ Î· = limits.kernel.Î¹ _ :=
((limits.limit.is_limit _).unique_up_to_iso (is_limit_kernel_fork Î·)).hom.w
  limits.walking_parallel_pair.zero

@[simp, reassoc]
lemma kernel_iso_kernel_sheaf_hom_Î¹_val {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  (kernel_iso_kernel_sheaf Î·).hom.val â‰« (kernel_Î¹ Î·).val = (limits.kernel.Î¹ Î·).val :=
begin
  change ((kernel_iso_kernel_sheaf Î·).hom â‰« (kernel_Î¹ Î·)).val = (limits.kernel.Î¹ Î·).val,
  simp,
end

@[simp, reassoc]
lemma kernel_iso_kernel_sheaf_inv_Î¹ {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  (kernel_iso_kernel_sheaf Î·).inv â‰« limits.kernel.Î¹ _ = kernel_Î¹ Î· :=
by simp only [â† kernel_iso_kernel_sheaf_hom_Î¹, iso.inv_hom_id_assoc]

@[simp, reassoc]
lemma kernel_iso_kernel_sheaf_inv_Î¹_val {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  (kernel_iso_kernel_sheaf Î·).inv.val â‰« (limits.kernel.Î¹ Î·).val = (kernel_Î¹ Î·).val :=
begin
  change ((kernel_iso_kernel_sheaf Î·).inv â‰« (limits.kernel.Î¹ Î·)).val = (kernel_Î¹ Î·).val,
  simp,
end

end kernels

section cokernels

variables [limits.has_zero_morphisms A]
-- TODO: Add some instances that derive the following from `[has_cokernels A]`.
variables [limits.has_colimits_of_shape.{_ (max v u)} limits.walking_parallel_pair A]

-- We will need to sheafify....
variables [concrete_category.{max v u} A]
variables [âˆ€ (P : Cáµ’áµ– â¥¤ A) (X : C) (S : J.cover X), limits.has_multiequalizer (S.index P)]
variables [limits.preserves_limits (forget A)]
variables [âˆ€ (X : C), limits.has_colimits_of_shape (J.cover X)áµ’áµ– A]
variables [âˆ€ (X : C), limits.preserves_colimits_of_shape (J.cover X)áµ’áµ– (forget A)]
variables [reflects_isomorphisms (forget A)]

def cokernel_sheaf {F G : Sheaf J A} (Î· : F âŸ¶ G) : Sheaf J A :=
{ val := J.sheafify (limits.cokernel ((Sheaf_to_presheaf J A).map Î·)), -- ;-)
  cond := grothendieck_topology.plus.is_sheaf_plus_plus _ _ }

def cokernel_Ï€ {F G : Sheaf J A} (Î· : F âŸ¶ G) : G âŸ¶ cokernel_sheaf Î· :=
âŸ¨show (Sheaf_to_presheaf J A).obj G âŸ¶ J.sheafify (limits.cokernel ((Sheaf_to_presheaf J A).map Î·)),
from limits.cokernel.Ï€ ((Sheaf_to_presheaf J A).map Î·) â‰«
  J.to_sheafify (limits.cokernel ((Sheaf_to_presheaf J A).map Î·))âŸ©

def cokernel_cofork {F G : Sheaf J A} (Î· : F âŸ¶ G) : limits.cofork Î· 0 :=
limits.cofork.of_Ï€ (cokernel_Ï€ Î·) begin
  ext1,
  rw [limits.zero_comp],
  dsimp only [cokernel_Ï€],
  show Î·.val â‰« _ = 0,
  erw [â† category.assoc, limits.cokernel.condition, limits.zero_comp],
end

def is_colimit_cokernel_cofork {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  limits.is_colimit (cokernel_cofork Î·) :=
limits.is_colimit_aux _ (Î» S,
  âŸ¨J.sheafify_lift
    (limits.cokernel.desc ((Sheaf_to_presheaf J A).map Î·) S.Ï€.val $
      congr_arg Sheaf.hom.val S.condition)
    (S.X.2)âŸ©)
begin
  intros S, ext1,
  change (_ â‰« _) â‰« _ = _,
  rw [category.assoc, J.to_sheafify_sheafify_lift, limits.cokernel.Ï€_desc],
end begin
  intros S m hm, ext1,
  apply J.sheafify_lift_unique,
  rw Sheaf.hom.ext_iff at hm,
  change (_ â‰« _) â‰« _ = _ at hm,
  rw category.assoc at hm,
  ext1,
  rw [hm, limits.cokernel.Ï€_desc],
end

-- Sanity check
example : limits.has_cokernels (Sheaf J A) := by apply_instance

def cokernel_iso_cokernel_sheaf {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  limits.cokernel Î· â‰… cokernel_sheaf Î· :=
(limits.colimit.is_colimit _).cocone_point_unique_up_to_iso (is_colimit_cokernel_cofork _)

@[simp]
lemma cokernel_iso_cokernel_sheaf_hom_Ï€ {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  limits.cokernel.Ï€ Î· â‰« (cokernel_iso_cokernel_sheaf Î·).hom = cokernel_Ï€ _ :=
((limits.colimit.is_colimit _).unique_up_to_iso (is_colimit_cokernel_cofork Î·)).hom.w
  limits.walking_parallel_pair.one

@[simp]
lemma cokernel_iso_cokernel_sheaf_inv_Ï€ {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  cokernel_Ï€ Î· â‰« (cokernel_iso_cokernel_sheaf Î·).inv = limits.cokernel.Ï€ Î· :=
by simp only [â† cokernel_iso_cokernel_sheaf_hom_Ï€,
  category.assoc, iso.hom_inv_id, category.comp_id]

end cokernels

section kernels_and_cokernels

variables [limits.has_zero_morphisms A]
-- TODO: use has kernels and cokernels, when possible... see above
variables [limits.has_colimits_of_shape.{_ (max v u)} limits.walking_parallel_pair A]
variables [limits.has_limits_of_shape.{_ (max v u)} limits.walking_parallel_pair A]

-- We will need to sheafify....
variables [concrete_category.{max v u} A]
variables [âˆ€ (P : Cáµ’áµ– â¥¤ A) (X : C) (S : J.cover X), limits.has_multiequalizer (S.index P)]
variables [limits.preserves_limits (forget A)]
variables [âˆ€ (X : C), limits.has_colimits_of_shape (J.cover X)áµ’áµ– A]
variables [âˆ€ (X : C), limits.preserves_colimits_of_shape (J.cover X)áµ’áµ– (forget A)]
variables [reflects_isomorphisms (forget A)]

def coim_to_im'_aux {F G : Sheaf J A} (Î· : F âŸ¶ G) :
coim ((Sheaf_to_presheaf J A).map Î·) âŸ¶ (Sheaf_to_presheaf J A).obj (kernel_sheaf (cokernel_Ï€ Î·)) :=
(coim_to_im _ â‰« limits.kernel.lift _ (limits.kernel.Î¹ _) begin
  dsimp [cokernel_Ï€],
  rw [â† category.assoc, limits.kernel.condition],
  simp,
end)

def coim_to_im' {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  cokernel_sheaf (kernel_Î¹ Î·) âŸ¶ kernel_sheaf (cokernel_Ï€ Î·) :=
âŸ¨J.sheafify_lift (coim_to_im'_aux Î·) (kernel_sheaf _).2âŸ©

def kernel_sheaf_cokernel_Ï€_iso {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  kernel_sheaf (limits.cokernel.Ï€ Î·) â‰… kernel_sheaf (cokernel_Ï€ Î·) :=
{ hom := âŸ¨limits.kernel.map _ _ (ğŸ™ _)
    ((Sheaf_to_presheaf J A).map (cokernel_iso_cokernel_sheaf Î·).hom) begin
      rw â† functor.map_comp,
      dsimp [cokernel_iso_cokernel_sheaf, limits.is_colimit.cocone_point_unique_up_to_iso,
        cokernel_cofork],
      show Sheaf.hom.val (limits.cokernel.Ï€ Î· â‰« _) = _,
      simp only [limits.coequalizer.Ï€_desc, category.id_comp],
    endâŸ©,
  inv := âŸ¨limits.kernel.map _ _ (ğŸ™ _)
    ((Sheaf_to_presheaf J A).map (cokernel_iso_cokernel_sheaf Î·).inv) begin
      rw â† functor.map_comp,
      dsimp [cokernel_iso_cokernel_sheaf, limits.is_colimit.cocone_point_unique_up_to_iso,
        cokernel_Ï€, is_colimit_cokernel_cofork, limits.is_colimit_aux],
      erw [category.id_comp, category.assoc, J.to_sheafify_sheafify_lift,
        limits.cokernel.Ï€_desc],
    endâŸ©,
  hom_inv_id' := begin
    ext : 2,
    delta limits.kernel.map,
    dsimp,
    erw [category.assoc, limits.kernel.lift_Î¹, â† category.assoc, limits.kernel.lift_Î¹,
      category.comp_id, category.comp_id, category.id_comp],
  end,
  inv_hom_id' := begin
    ext : 2,
    delta limits.kernel.map,
    dsimp,
    erw [category.assoc, limits.kernel.lift_Î¹, â† category.assoc, limits.kernel.lift_Î¹,
      category.comp_id, category.comp_id, category.id_comp],
  end }

def cokernel_sheaf_kernel_Î¹_iso {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  cokernel_sheaf (limits.kernel.Î¹ Î·) â‰… cokernel_sheaf (kernel_Î¹ Î·) :=
{ hom := âŸ¨J.sheafify_lift
    (limits.cokernel.map _ _ ((Sheaf_to_presheaf J A).map (kernel_iso_kernel_sheaf Î·).hom) (ğŸ™ _)
      (by rw [category.comp_id, â† functor.map_comp, kernel_iso_kernel_sheaf_hom_Î¹])
      â‰« J.to_sheafify _) (cokernel_sheaf _).2âŸ©,
  inv := âŸ¨J.sheafify_lift
    (limits.cokernel.map _ _ ((Sheaf_to_presheaf J A).map (kernel_iso_kernel_sheaf Î·).inv) (ğŸ™ _)
      (by rw [category.comp_id, â† functor.map_comp, kernel_iso_kernel_sheaf_inv_Î¹])
      â‰« J.to_sheafify _)
    (cokernel_sheaf _).2âŸ©,
  hom_inv_id' := begin
    ext1,
    apply J.sheafify_hom_ext _ _ (cokernel_sheaf _).2,
    erw [â† category.assoc, J.to_sheafify_sheafify_lift, category.assoc,
      J.to_sheafify_sheafify_lift, â† category.assoc],
    conv_rhs { erw category.comp_id },
    convert category.id_comp _,
    ext1,
    delta limits.cokernel.map,
    erw [â† category.assoc, limits.coequalizer.Ï€_desc, category.id_comp,
      limits.coequalizer.Ï€_desc, category.id_comp, category.comp_id],
  end,
  inv_hom_id' := begin
    ext1,
    apply J.sheafify_hom_ext _ _ (cokernel_sheaf _).2,
    erw [â† category.assoc, J.to_sheafify_sheafify_lift, category.assoc,
      J.to_sheafify_sheafify_lift, â† category.assoc],
    conv_rhs { erw category.comp_id },
    convert category.id_comp _,
    ext1,
    delta limits.cokernel.map,
    erw [â† category.assoc, limits.coequalizer.Ï€_desc, category.id_comp,
      limits.coequalizer.Ï€_desc, category.id_comp, category.comp_id],
  end }

lemma eq_coim_to_im' {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  (cokernel_sheaf_kernel_Î¹_iso Î·).inv â‰«
  (cokernel_iso_cokernel_sheaf _).inv â‰«
  coim_to_im Î·  â‰«
  (kernel_iso_kernel_sheaf _).hom â‰«
  (kernel_sheaf_cokernel_Ï€_iso Î·).hom
  = coim_to_im' Î· :=
begin
  dsimp [coim_to_im, cokernel_sheaf_kernel_Î¹_iso,
    coim_to_im', coim_to_im'_aux, kernel_sheaf_cokernel_Ï€_iso,
    limits.is_colimit.cocone_point_unique_up_to_iso,
    limits.is_limit.cone_point_unique_up_to_iso],
  delta limits.kernel.map limits.cokernel.map,
  ext1,
  apply J.sheafify_lift_unique,
  ext : 2,
  conv_rhs {
    erw [â† category.assoc, limits.cokernel.Ï€_desc,
      category.assoc, limits.kernel.lift_Î¹, limits.kernel.lift_Î¹] },
  dsimp,
  simp only [category.assoc, category.comp_id, category.id_comp,
    J.to_sheafify_sheafify_lift_assoc, kernel_iso_kernel_sheaf_hom_Î¹_val,
      limits.kernel.lift_Î¹, limits.cokernel.Ï€_desc_assoc],
  dsimp [cokernel_iso_cokernel_sheaf, limits.is_colimit.cocone_point_unique_up_to_iso,
    is_colimit_cokernel_cofork, limits.is_colimit_aux],
  rw J.to_sheafify_sheafify_lift_assoc,
  simp only [limits.cokernel.Ï€_desc_assoc, â† category.assoc, â† Sheaf.hom.comp_val,
    limits.cokernel.Ï€_desc],
  simp only [category_theory.category_comp_val, category.assoc],
  erw kernel_iso_kernel_sheaf_hom_Î¹_val,
  rw [â† Sheaf.hom.comp_val, limits.kernel.lift_Î¹],
end

lemma coim_to_im_eq {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  coim_to_im Î· =
  (cokernel_iso_cokernel_sheaf _).hom â‰«
  (cokernel_sheaf_kernel_Î¹_iso Î·).hom â‰«
  coim_to_im' Î· â‰«
  (kernel_sheaf_cokernel_Ï€_iso Î·).inv â‰«
  (kernel_iso_kernel_sheaf _).inv
  :=
begin
  rw â† eq_coim_to_im',
  simp only [category.assoc, iso.hom_inv_id, iso.inv_hom_id,
    iso.hom_inv_id_assoc, iso.inv_hom_id_assoc, category.comp_id],
end

end kernels_and_cokernels

section preadditive

variable [preadditive A]

instance : preadditive (Sheaf J A) :=
{ hom_group := Î» P Q,
  { add := Î» f g, âŸ¨f.val + g.valâŸ©,
    add_assoc := by { intros, ext1, apply add_assoc },
    zero := âŸ¨0âŸ©,
    zero_add := by { intros, ext1, apply zero_add },
    add_zero := by { intros, ext1, apply add_zero },
    nsmul := Î» n f, âŸ¨n â€¢ f.valâŸ©,
    nsmul_zero' := by { intros, ext1, simpa },
    nsmul_succ' := by { intros, ext1, simpa },
    neg := Î» f, âŸ¨-f.valâŸ©,
    sub := Î» f g, âŸ¨f.val - g.valâŸ©,
    sub_eq_add_neg := by { intros, ext1, apply sub_eq_add_neg },
    zsmul := Î» n f, âŸ¨n â€¢ f.valâŸ©,
    zsmul_zero' := by { intros, ext1, simpa },
    zsmul_succ' := by { intros, ext1, simpa },
    zsmul_neg' := by { intros, ext1, simpa },
    add_left_neg := by { intros, ext1, apply add_left_neg },
    add_comm := by { intros, ext1, apply add_comm } },
  add_comp' := Î» P Q R f g h, by { ext1, apply preadditive.add_comp },
  comp_add' := Î» P Q R f g h, by { ext1, apply preadditive.comp_add } }

end preadditive

section additive

variable [additive_category A]

instance : additive_category (Sheaf J A) :=
{ has_biproducts_of_shape := begin
    introsI J _ _,
    constructor,
    intros F,
    apply limits.has_biproduct.of_has_product
  end,
  ..(by apply_instance : preadditive (Sheaf J A)) }

end additive

section abelian

variables [abelian A]
-- We need sheafification
variables [concrete_category.{max v u} A]
variables [âˆ€ (P : Cáµ’áµ– â¥¤ A) (X : C) (S : J.cover X), limits.has_multiequalizer (S.index P)]
variables [limits.preserves_limits (forget A)]
variables [âˆ€ (X : C), limits.has_colimits_of_shape (J.cover X)áµ’áµ– A]
variables [âˆ€ (X : C), limits.preserves_colimits_of_shape (J.cover X)áµ’áµ– (forget A)]
variables [reflects_isomorphisms (forget A)]

open grothendieck_topology

def parallel_pair_sheafification {F G : Sheaf J A} (Î· : F âŸ¶ G) : limits.parallel_pair
  (limits.cokernel.Ï€ Î·.val) 0 â‹™ J.sheafification A â‰…
  limits.parallel_pair (cokernel_Ï€ Î·).val 0 :=
nat_iso.of_components
(Î» x,
match x with
| limits.walking_parallel_pair.zero := by { dsimp, refine (J.iso_sheafify G.2).symm }
| limits.walking_parallel_pair.one := by { dsimp, exact eq_to_iso rfl }
end)
begin
  -- This proof is SLOW :-(
  rintros (a|a) (b|b) (f|f|f),
  { dsimp [parallel_pair_sheafification._match_1],
    simp only [iso.eq_inv_comp, functor.map_id],
    dsimp [grothendieck_topology.iso_sheafify],
    rw [â† category.assoc, is_iso.comp_inv_eq, category.id_comp],
    change J.to_sheafify _ â‰« (sheafification J A).map _ = _,
    erw [functor.map_id, category.comp_id] },
  { dsimp [parallel_pair_sheafification._match_1],
    rw [category.comp_id, iso.eq_inv_comp],
    dsimp [grothendieck_topology.iso_sheafify, cokernel_Ï€],
    change (to_sheafification J A).app _ â‰« (sheafification J A).map _ = _,
    rw â† (to_sheafification J A).naturality,
    refl },
  { dsimp [parallel_pair_sheafification._match_1],
    simp only [limits.comp_zero, category.comp_id],
    change (sheafification J A).map _ = _,
    apply J.sheafify_hom_ext,
    { exact plus.is_sheaf_plus_plus J (limits.cokernel Î·.val) },
    erw â† (to_sheafification J A).naturality,
    simp },
  { dsimp [parallel_pair_sheafification._match_1],
    simp only [limits.comp_zero, category.id_comp, category.comp_id],
    change (sheafification J A).map _ = _,
    simp only [functor.map_id],
    erw (sheafification J A).map_id, refl },
end .

def kernel_cokernel_Ï€_iso {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  J.sheafify (limits.kernel (limits.cokernel.Ï€ ((Sheaf_to_presheaf J A).map Î·))) â‰…
  limits.kernel ((Sheaf_to_presheaf J A).map (cokernel_Ï€ Î·)) :=
begin
  let e := (limits.is_limit_of_preserves (sheafification J A) (limits.limit.is_limit
      (limits.parallel_pair (limits.cokernel.Ï€
      ((Sheaf_to_presheaf J A).map Î·)) 0))).cone_point_unique_up_to_iso (limits.limit.is_limit _),
  refine e â‰ªâ‰« _,
  change limits.limit _ â‰… _,
  refine limits.has_limit.iso_of_nat_iso _,
  apply parallel_pair_sheafification,
end

/-
{ hom := J.sheafify_lift (limits.kernel.map _ _ (ğŸ™ _) (J.to_sheafify _) sorry) sorry,
  inv := begin
    let e : J.sheafify ((Sheaf_to_presheaf J A).obj G) âŸ¶
      J.sheafify (limits.cokernel ((Sheaf_to_presheaf J A).map Î·)) :=
        (sheafification J A).map (limits.cokernel.Ï€ _),
    let ee : limits.kernel ((Sheaf_to_presheaf J A).map (cokernel_Ï€ Î·)) âŸ¶ limits.kernel e,
    { refine limits.kernel.map _ _ (J.to_sheafify _) (ğŸ™ _) _,
      rw category.comp_id,
      dsimp only [e],
      rw â† grothendieck_topology.to_sheafification_app,
      rw â† (to_sheafification J A).naturality,
      refl },
    refine ee â‰« _,
    dsimp only [e],
    change limits.kernel ((Sheaf_to_presheaf J A).map ((presheaf_to_Sheaf J A).map _)) âŸ¶ _,
    refine (Sheaf_to_presheaf J A).map (kernel_iso_kernel_sheaf _).inv â‰« _,
    change _ âŸ¶ (Sheaf_to_presheaf J A).obj ((presheaf_to_Sheaf J A).obj _),
    refine (Sheaf_to_presheaf J A).map _,
    haveI : is_left_adjoint (presheaf_to_Sheaf J A) := sorry,
    -- Now we need to use the fact that finite limits commute with sheafification,
    -- i.e. that sheafification is left exact.
    sorry
  end,
  hom_inv_id' := sorry,
  inv_hom_id' := sorry }
-/

lemma coim_to_im'_eq {F G : Sheaf J A} (Î· : F âŸ¶ G) :
  (Sheaf_to_presheaf J A).map (coim_to_im' Î·) =
  (sheafification J A).map (coim_to_im _) â‰« (kernel_cokernel_Ï€_iso Î·).hom :=
begin
  dsimp only [kernel_cokernel_Ï€_iso, limits.is_limit.cone_point_unique_up_to_iso,
    functor.map_iso, iso.trans_hom],
  simp only [category.assoc],
  dsimp only [id, limits.is_limit.unique_up_to_iso, limits.cones.forget,
    limits.is_limit.lift_cone_morphism, functor.map_cone],
  apply limits.equalizer.hom_ext,
  simp only [category.assoc],
  --delta limits.equalizer.Î¹,
  erw limits.has_limit.iso_of_nat_iso_hom_Ï€,
  dsimp only [parallel_pair_sheafification, nat_iso.of_components, id, iso_sheafify,
    as_iso, iso.symm],
  simp only [â† category.assoc],
  rw is_iso.eq_comp_inv,
  simp only [category.assoc],
  erw limits.limit.lift_Ï€,
  dsimp [limits.cones.functoriality, sheafify_map],
  simp_rw [â† plus_functor_map, â† functor.comp_map, â† functor.map_comp],
  dsimp [coim_to_im', coim_to_im, coim_to_im'_aux],
  apply J.sheafify_hom_ext,
  { exact plus.is_sheaf_plus_plus J G.val, },
  simp_rw â† category.assoc,
  erw J.to_sheafify_sheafify_lift,
  change _ = (to_sheafification J A).app _ â‰« (sheafification J A).map _,
  erw â† (to_sheafification J A).naturality,
  simp only [category.assoc, limits.kernel.lift_Î¹],
  dsimp,
  erw category.assoc,
end

instance is_iso_coim_to_im {F G : Sheaf J A} (Î· : F âŸ¶ G) : is_iso (coim_to_im Î·) :=
begin
  rw coim_to_im_eq,
  suffices : is_iso (coim_to_im' Î·),
  { resetI, apply is_iso.comp_is_iso },
  suffices : is_iso ((Sheaf_to_presheaf J A).map (coim_to_im' Î·)),
  { resetI, apply is_iso_of_fully_faithful (Sheaf_to_presheaf J A) },
  rw coim_to_im'_eq,
  apply is_iso.comp_is_iso,
end

instance abelian : abelian (Sheaf J A) :=
abelian_of_coim_to_im (Î» F G Î·, infer_instance)

end abelian

end Sheaf
end category_theory

import category_theory.limits.shapes.kernels
import category_theory.limits.functor_category
import category_theory.preadditive.functor_category
import category_theory.additive.basic
import category_theory.abelian.basic

namespace category_theory

universes w v u
variables {C : Type (max v u)} [category.{v} C]
variables {D : Type w} [category.{max v u} D]

open category_theory.limits

example [has_zero_morphisms D] : has_zero_morphisms (C â¥¤ D) := infer_instance
example [preadditive D] : preadditive (C â¥¤ D) := infer_instance

noncomputable theory

section kernels

variables [has_zero_morphisms D] [has_kernels D]

@[simps]
def nat_trans.kernel_functor {F G : C â¥¤ D} (Î· : F âŸ¶ G) : C â¥¤ D :=
{ obj := Î» X, kernel (Î·.app X),
  map := Î» X Y f, kernel.map _ _ (F.map f) (G.map f) (Î·.naturality f).symm,
  map_id' := Î» X, by { ext, simp },
  map_comp' := Î» X Y Z f g, by { ext, simp } }

@[simps]
def nat_trans.kernel_Î¹ {F G : C â¥¤ D} (Î· : F âŸ¶ G) :
  Î·.kernel_functor âŸ¶ F :=
{ app := Î» X, kernel.Î¹ _,
  naturality' := Î» X Y f, by simp }

@[simps]
def nat_trans.kernel_fork {F G : C â¥¤ D} (Î· : F âŸ¶ G) :
  kernel_fork Î· :=
limits.kernel_fork.of_Î¹ Î·.kernel_Î¹ $ by { ext, simp }

@[simps]
def nat_trans.is_limit_kernel_fork {F G : C â¥¤ D} (Î· : F âŸ¶ G) :
  is_limit Î·.kernel_fork :=
is_limit_aux _ (Î» S,
  { app := Î» X, kernel.lift _ (S.Î¹.app X) $ by simp [â† nat_trans.comp_app],
    naturality' := Î» X Y f, by { ext, dsimp, simp } })
begin
  intros S,
  ext,
  dsimp,
  simp,
end begin
  intros S m hm,
  ext X,
  apply_fun (Î» e, e.app X) at hm,
  dsimp at âŠ¢ hm,
  simp [hm]
end

instance functor_category_has_kernels :
  has_kernels (C â¥¤ D) := âŸ¨Î» F G Î·, âŸ¨âŸ¨âŸ¨_, Î·.is_limit_kernel_forkâŸ©âŸ©âŸ©âŸ©

def nat_trans.kernel_obj_iso {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  (kernel Î·).obj X â‰… kernel (Î·.app X) :=
((limit.is_limit _).cone_point_unique_up_to_iso Î·.is_limit_kernel_fork).app X

@[simp, reassoc]
lemma nat_trans.kernel_obj_iso_hom_Î¹ {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  (nat_trans.kernel_obj_iso Î· X).hom â‰« kernel.Î¹ (Î·.app X) = (kernel.Î¹ Î·).app X :=
begin
  have h := ((limit.is_limit _).unique_up_to_iso Î·.is_limit_kernel_fork).hom.w
    walking_parallel_pair.zero,
  apply_fun (Î» e, e.app X) at h,
  exact h
end

@[simp, reassoc]
lemma nat_trans.kernel_obj_iso_inv_Î¹ {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  (nat_trans.kernel_obj_iso Î· X).inv â‰« (kernel.Î¹ Î·).app X = kernel.Î¹ _ :=
by simp [iso.inv_comp_eq]

@[simps]
def nat_trans.cokernel_kernel_Î¹_iso [has_cokernels D] {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  cokernel ((kernel.Î¹ Î·).app X) â‰… cokernel (kernel.Î¹ (Î·.app X)) :=
{ hom := cokernel.map _ _ (nat_trans.kernel_obj_iso _ _).hom (ğŸ™ _) (by simp),
  inv := cokernel.map _ _ (nat_trans.kernel_obj_iso _ _).inv (ğŸ™ _) (by simp),
  hom_inv_id' := by { ext, dsimp, simp },
  inv_hom_id' := by { ext, dsimp, simp } }

end kernels

section cokernels

variables [has_zero_morphisms D] [has_cokernels D]

@[simps]
def nat_trans.cokernel_functor {F G : C â¥¤ D} (Î· : F âŸ¶ G) : C â¥¤ D :=
{ obj := Î» X, cokernel (Î·.app X),
  map := Î» X Y f, cokernel.map _ _ (F.map f) (G.map f) (Î·.naturality f).symm,
  map_id' := Î» X, by { ext, simp },
  map_comp' := Î» X Y Z f g, by { ext, simp } }

@[simps]
def nat_trans.cokernel_Ï€ {F G : C â¥¤ D} (Î· : F âŸ¶ G) :
  G âŸ¶ Î·.cokernel_functor :=
{ app := Î» X, cokernel.Ï€ _,
  naturality' := Î» X Y f, by simp }

@[simps]
def nat_trans.cokernel_cofork {F G : C â¥¤ D} (Î· : F âŸ¶ G) :
  cokernel_cofork Î· :=
limits.cokernel_cofork.of_Ï€ Î·.cokernel_Ï€ $ by { ext, simp }

@[simps]
def nat_trans.is_colimit_cokernel_cofork {F G : C â¥¤ D} (Î· : F âŸ¶ G) :
  is_colimit Î·.cokernel_cofork :=
is_colimit_aux _ (Î» S,
  { app := Î» X, cokernel.desc _ (S.Ï€.app X) $ by simp [â† nat_trans.comp_app],
    naturality' := Î» X Y f, by { ext, dsimp, simp } })
begin
  intros S,
  ext,
  dsimp,
  simp,
end begin
  intros S m hm,
  ext X,
  apply_fun (Î» e, e.app X) at hm,
  dsimp at âŠ¢ hm,
  simp [hm]
end

instance functor_category_has_cokernels :
  has_cokernels (C â¥¤ D) := âŸ¨Î» F G Î·, âŸ¨âŸ¨âŸ¨_, Î·.is_colimit_cokernel_coforkâŸ©âŸ©âŸ©âŸ©

def nat_trans.cokernel_obj_iso {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  (cokernel Î·).obj X â‰… cokernel (Î·.app X) :=
((colimit.is_colimit _).cocone_point_unique_up_to_iso Î·.is_colimit_cokernel_cofork).app X

@[simp, reassoc]
lemma nat_trans.cokernel_obj_iso_Ï€_hom {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  (cokernel.Ï€ Î·).app X â‰« (nat_trans.cokernel_obj_iso Î· X).hom = cokernel.Ï€ _ :=
begin
  have h := ((colimit.is_colimit _).unique_up_to_iso Î·.is_colimit_cokernel_cofork).hom.w
    walking_parallel_pair.one,
  apply_fun (Î» e, e.app X) at h,
  exact h,
end

@[simp, reassoc]
lemma nat_trans.cokernel_obj_iso_Ï€_inv {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  cokernel.Ï€ (Î·.app X) â‰« (nat_trans.cokernel_obj_iso Î· X).inv = (cokernel.Ï€ Î·).app X :=
by simp [iso.comp_inv_eq]

@[simps]
def nat_trans.kernel_cokernel_Ï€_iso [has_kernels D] {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  kernel ((cokernel.Ï€ Î·).app X) â‰… kernel (cokernel.Ï€ (Î·.app X)) :=
{ hom := kernel.map _ _ (ğŸ™ _) (nat_trans.cokernel_obj_iso Î· X).hom (by simp),
  inv := kernel.map _ _ (ğŸ™ _) (nat_trans.cokernel_obj_iso Î· X).inv (by simp),
  hom_inv_id' := by { ext, dsimp, simp },
  inv_hom_id' := by { ext, dsimp, simp } }

end cokernels

section cokernels_and_kernels

variables [has_zero_morphisms D] [has_cokernels D] [has_kernels D]

lemma nat_trans.coimage_image_comparison_app {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
 (nat_trans.cokernel_kernel_Î¹_iso _ _).inv â‰«
 (nat_trans.cokernel_obj_iso _ _).inv â‰« (abelian.coimage_image_comparison Î·).app X â‰«
 (nat_trans.kernel_obj_iso _ _).hom â‰«
 (nat_trans.kernel_cokernel_Ï€_iso _ _).hom = abelian.coimage_image_comparison (Î·.app X) :=
begin
  dsimp [abelian.coimage_image_comparison],
  ext,
  dsimp [nat_trans.cokernel_obj_iso, is_colimit.cocone_point_unique_up_to_iso],
  dsimp [nat_trans.kernel_obj_iso, is_limit.cone_point_unique_up_to_iso],
  simp,
end

end cokernels_and_kernels

section additivity

variables [additive_category D]

instance : additive_category (C â¥¤ D) :=
{ has_biproducts_of_shape := begin
    introsI J _ _,
    constructor,
    intros F,
    apply limits.has_biproduct.of_has_product
  end,
  -- without the infer instance, this becomes REALLY slow...
  ..(infer_instance : preadditive (C â¥¤ D)) }

end additivity

section abelian

variable [abelian D]

instance additive_category_of_abelian : additive_category D :=
{ ..(infer_instance : preadditive D) } -- without the infer instance, this becomes REALLY slow...

instance functor_category_is_iso_coim_to_im_app {F G : C â¥¤ D} (Î· : F âŸ¶ G) (X : C) :
  is_iso ((abelian.coimage_image_comparison Î·).app X) :=
begin
  have : (abelian.coimage_image_comparison Î·).app X =
    (nat_trans.cokernel_obj_iso _ _).hom â‰«
    (nat_trans.cokernel_kernel_Î¹_iso _ _).hom â‰«
    abelian.coimage_image_comparison _ â‰«
    (nat_trans.kernel_cokernel_Ï€_iso _ _).inv â‰«
    (nat_trans.kernel_obj_iso _ _).inv,
  { rw â† nat_trans.coimage_image_comparison_app,
    simp only [category.assoc, iso.inv_hom_id, iso.inv_hom_id_assoc,
      iso.hom_inv_id, iso.hom_inv_id_assoc, category.comp_id] },
  rw this,
  apply is_iso.comp_is_iso,
end

instance functor_category_is_iso_coim_to_im {F G : C â¥¤ D} (Î· : F âŸ¶ G) :
  is_iso (abelian.coimage_image_comparison Î·) := nat_iso.is_iso_of_is_iso_app _

instance : abelian (C â¥¤ D) :=
abelian.of_coimage_image_comparison_is_iso

end abelian

end category_theory

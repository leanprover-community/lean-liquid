import algebra.category.Module.adjunctions
import group_theory.free_abelian_group_finsupp
import algebra.category.Group.adjunctions
import algebra.category.Group.filtered_colimits
import algebra.category.Group.biproducts
import algebra.category.Group.abelian
import category_theory.limits.preserves.shapes.products
import category_theory.limits.preserves.filtered
import category_theory.limits.shapes.terminal
import linear_algebra.free_module.pid
import for_mathlib.AddCommGroup.epi
import algebra.group.ulift

open category_theory

universes u

namespace AddCommGroup

noncomputable theory

@[simps]
def free' : Type u â¥¤ AddCommGroup.{u} :=
{ obj := Î» X, AddCommGroup.of $ X â†’â‚€ â„¤,
  map := Î» X Y f, finsupp.map_domain.add_monoid_hom f,
  map_id' := begin
    intros X, ext, dsimp, simp,
  end,
  map_comp' := begin
    intros X Y Z f g, ext, dsimp, simp,
  end } .

@[simps]
def of_iso {A B : Type u} [add_comm_group A] [add_comm_group B]
  (e : A â‰ƒ+ B) : of A â‰… of B :=
{ hom := e.to_add_monoid_hom,
  inv := e.symm.to_add_monoid_hom,
  hom_inv_id' := begin
    ext, dsimp, simp,
  end,
  inv_hom_id' := begin
    ext, dsimp, simp,
  end } .

@[simps]
def free_iso_free' : free.{u} â‰… free'.{u} :=
category_theory.nat_iso.of_components
(Î» X, of_iso (free_abelian_group.equiv_finsupp X))
begin
  intros X Y f, ext, dsimp, simp,
end

def adj' : free'.{u} âŠ£ forget AddCommGroup.{u} :=
AddCommGroup.adj.of_nat_iso_left $ free_iso_free'.{u}

end AddCommGroup

def types.pt {Î± : Type u} (a : Î±) : âŠ¤_ _ âŸ¶ Î± :=
Î» x, a

def types.punit_iso : (punit : Type u) â‰… âŠ¤_ _ :=
{ hom := limits.terminal.from _,
  inv := types.pt punit.star }

namespace AddCommGroup

def tunit : AddCommGroup.{u} :=
  AddCommGroup.free'.obj (âŠ¤_ _)

def tunit.lift {A : AddCommGroup.{u}} (e : âŠ¤_ _ âŸ¶ (forget _).obj A) :
  tunit âŸ¶ A :=
(AddCommGroup.adj'.hom_equiv _ _).symm e

-- Do we really not have this?!
def _root_.finsupp.punit_add_equiv (M : Type*) [add_monoid M] :
  ((punit : Type u) â†’â‚€ M) â‰ƒ+ M :=
{ to_fun := Î» f, f punit.star,
  inv_fun := Î» m, finsupp.single punit.star m,
  left_inv := Î» x, by { ext, dsimp, simp },
  right_inv := Î» x, by { dsimp, simp },
  map_add' := Î» f g, by simp }

-- Do we really not have this?!
def add_equiv_of_iso (A B : AddCommGroup.{u}) (e : A â‰… B) :
  A â‰ƒ+ B :=
{ to_fun := e.hom,
  inv_fun := e.inv,
  left_inv := Î» x, by simp,
  right_inv := Î» x, by simp,
  map_add' := Î» x y, e.hom.map_add _ _ }

def tunit_add_equiv : tunit.{u} â‰ƒ+ â„¤ :=
add_equiv.trans (add_equiv_of_iso _ _ $ AddCommGroup.free'.map_iso
  types.punit_iso.symm) (finsupp.punit_add_equiv â„¤)

def tunit.gen : tunit.{u} :=
AddCommGroup.adj'.unit.app _ $
  (limits.terminal.from (punit : Type u) : punit â†’ âŠ¤_ (Type u)) punit.star

open_locale classical

-- Of course this is true without the fintype assumption...
def iso_of_basis {Î¹ : Type u} [fintype Î¹] {A : AddCommGroup.{u}} (ğ“‘ : basis Î¹ â„¤ A) :
  (âˆ (Î» i : Î¹, tunit.{u})) â‰… A :=
begin
  -- This is very messy...
  let e : (âˆ (Î» i : Î¹, tunit.{u})) â‰… (â¨ (Î» i, tunit.{u})) :=
    (limits.colimit.is_colimit _).cocone_point_unique_up_to_iso
      (limits.biproduct.is_bilimit _).is_colimit,
  refine e â‰ªâ‰« _,
  refine biproduct_iso_pi _ â‰ªâ‰« _,
  refine AddCommGroup.of_iso _ â‰ªâ‰« (AddCommGroup.of_iso ğ“‘.repr.to_add_equiv).symm â‰ªâ‰«
    âŸ¨add_monoid_hom.id _, add_monoid_hom.id _, by { ext, refl }, by { ext, refl }âŸ©,
  let q : tunit.{u} â‰ƒ+ â„¤ := tunit_add_equiv,
  let e : (Î¹ â†’â‚€ â„¤) â‰ƒ+ (Î¹ â†’ â„¤),
  { fconstructor,
    exact finsupp.equiv_fun_on_fintype,
    exact finsupp.equiv_fun_on_fintype.symm,
    exact finsupp.equiv_fun_on_fintype.left_inverse_symm,
    exact finsupp.equiv_fun_on_fintype.right_inverse_symm,
    intros x y, refl },
  refine add_equiv.trans _ e.symm,
  fconstructor,
  { intros f i, exact q (f i) },
  { intros f i, exact q.symm (f i) },
  { intros f, ext, dsimp, simp },
  { intros f, ext, dsimp, simp },
  { intros f g, ext i, simp, },
end
--as_iso (hom_of_basis ğ“‘)

@[derive partial_order]
def index_cat (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A] : Type u :=
{ H : add_subgroup A // H.fg } -- Is this the condition we want?

instance nonempty_index_cat (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A] :
  nonempty A.index_cat := âŸ¨âŸ¨âŠ¥, âˆ…, by simpâŸ©âŸ©

instance semilattice_sup_index_cat
  (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A] :
  semilattice_sup A.index_cat :=
{ sup := Î» I J, âŸ¨I.1 âŠ” J.1, begin
    obtain âŸ¨S,hSâŸ© := I.2,
    obtain âŸ¨T,hTâŸ© := J.2,
    rw [â† hS, â† hT],
    use S âˆª T,
    simp only [finset.coe_union, add_subgroup.closure_union],
  endâŸ©,
  le_sup_left := Î» I J, @le_sup_left (add_subgroup A) _ _ _,
  le_sup_right := Î» I J, @le_sup_right (add_subgroup A) _ _ _,
  sup_le := Î» I J K h1 h2, @sup_le (add_subgroup A) _ _ _ _ h1 h2,
  ..(infer_instance : partial_order _) }

def diagram (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A] :
  A.index_cat â¥¤ AddCommGroup.{u} :=
{ obj := Î» I, AddCommGroup.of I.1,
  map := Î» I J h, add_subgroup.inclusion h.le }

def cocone (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A] :
  limits.cocone A.diagram :=
{ X := A,
  Î¹ := { app := Î» I, I.1.subtype } }

def is_colimit_cocone (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A] :
  limits.is_colimit A.cocone :=
{ desc := Î» S,
  { to_fun := Î» a, S.Î¹.app âŸ¨add_subgroup.closure {a}, {a}, by simpâŸ©
      âŸ¨a, add_subgroup.subset_closure rflâŸ©,
    map_zero' := add_monoid_hom.map_zero _,
    map_add' := Î» x y, begin
      let I : A.index_cat := âŸ¨add_subgroup.closure {x}, {x}, by simpâŸ©,
      let J : A.index_cat := âŸ¨add_subgroup.closure {y}, {y}, by simpâŸ©,
      let K : A.index_cat := âŸ¨add_subgroup.closure {x + y}, {x + y}, by simpâŸ©,
      let IJ : A.index_cat := âŸ¨add_subgroup.closure {x,y}, {x,y}, by simpâŸ©,
      let iI : I âŸ¶ IJ := hom_of_le (add_subgroup.closure_mono $ by simp),
      let iJ : J âŸ¶ IJ := hom_of_le (add_subgroup.closure_mono $ by simp),
      let iK : K âŸ¶ IJ := hom_of_le ((add_subgroup.closure_le _).2 _),
      swap,
      { rintro a (rfl : a = _), change x + y âˆˆ IJ.1, apply IJ.1.add_mem,
        { apply add_subgroup.subset_closure, simp },
        { apply add_subgroup.subset_closure, simp } },
      rw [â† S.w iI, â† S.w iJ, â† S.w iK, comp_apply, comp_apply, comp_apply,
        â† add_monoid_hom.map_add],
      refl,
    end },
  fac' := begin
    rintros S J, ext âŸ¨x,hxâŸ©,
    let I : A.index_cat := âŸ¨add_subgroup.closure {x}, {x}, by simpâŸ©,
    let e : I âŸ¶ J := hom_of_le ((add_subgroup.closure_le _).2 _),
    { rw comp_apply, dsimp [cocone], rw â† S.w e, refl },
    rintros x (rfl : x = _), exact hx,
  end,
  uniq' := begin
    intros S m hm, ext x, dsimp [cocone] at x âŠ¢,
    let I : A.index_cat := âŸ¨add_subgroup.closure {x}, {x}, by simpâŸ©,
    specialize hm I,
    let y : I.1 := âŸ¨x, add_subgroup.subset_closure rflâŸ©,
    apply_fun (Î» e, e y) at hm, exact hm,
  end }

def colimit_comparison (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A] :
  limits.colimit A.diagram â‰… A :=
(limits.colimit.is_colimit A.diagram).cocone_point_unique_up_to_iso
  A.is_colimit_cocone

lemma exists_basis_of_index (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A]
  (I : A.index_cat) : âˆƒ (Î¹ : Type u) [fintype Î¹]
  (ğ“‘ : basis Î¹ â„¤ (AddCommGroup.of I.1)), true :=
begin
  obtain âŸ¨S,hSâŸ© := I.2,
  let e : S â†’ I.1 := Î» s, âŸ¨s,_âŸ©,
  swap, { rw â† hS, apply add_subgroup.subset_closure, exact s.2 },
  haveI : no_zero_smul_divisors â„¤ I.1,
  { constructor, rintros c âŸ¨x, hxâŸ© h, apply_fun (Î» e, e.val) at h,
    dsimp at h,
    cases no_zero_smul_divisors.eq_zero_or_eq_zero_of_smul_eq_zero h,
    left, assumption,
    right, ext, assumption },
  obtain âŸ¨n,BâŸ© := @module.free_of_finite_type_torsion_free S â„¤ _ _ _ I.1 _ _ _ e _ _,
  { use [ulift (fin n), infer_instance],
    refine âŸ¨_, trivialâŸ©,
    apply B.reindex,
    exact equiv.ulift.symm },
  { apply le_antisymm, { intros x hx, trivial },
    rintros âŸ¨x,hxâŸ© -,
    let J := (submodule.span â„¤ (set.range e)).to_add_subgroup,
    change _ âˆˆ J,
    have hJ : J = (J.map I.1.subtype).comap I.1.subtype, -- is this not a lemma?!
    { apply le_antisymm,
      { exact add_subgroup.le_comap_map I.val.subtype J },
      { intros t ht,
        simpa using ht } },
    rw [hJ, add_subgroup.mem_comap], clear hJ,
    dsimp [J],
    convert hx,
    rw [â† hS, submodule.span_int_eq_add_subgroup_closure, add_monoid_hom.map_closure], congr' 1,
    ext t,
    split,
    { rintros âŸ¨t,âŸ¨t,rflâŸ©,rflâŸ©, exact t.2 },
    { intros ht, refine âŸ¨âŸ¨t, _âŸ©, âŸ¨âŸ¨t, htâŸ©, rflâŸ©, rflâŸ© } },
end

lemma exists_sigma_iso_of_index (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A]
  (I : A.index_cat) : âˆƒ (Î¹ : Type u) [fintype Î¹]
  (e : (âˆ (Î» i : Î¹, tunit.{u})) â‰… AddCommGroup.of I.1), true :=
begin
  obtain âŸ¨Î¹,hÎ¹,ğ“‘,-âŸ© := exists_basis_of_index A I,
  resetI,
  use [Î¹, hÎ¹, iso_of_basis ğ“‘],
end

lemma exists_biprod_iso_of_index (A : AddCommGroup.{u}) [no_zero_smul_divisors â„¤ A]
  (I : A.index_cat) : âˆƒ (Î¹ : Type u) [fintype Î¹]
  (e : by exactI (â¨ (Î» i : Î¹, tunit.{u})) â‰… AddCommGroup.of I.1), true :=
begin
  obtain âŸ¨Î¹,hÎ¹,e,-âŸ© := exists_sigma_iso_of_index A I,
  resetI, use [Î¹, hÎ¹],
  use (limits.biproduct.is_bilimit _).is_colimit.cocone_point_unique_up_to_iso
      (limits.colimit.is_colimit _) â‰ªâ‰« e,
end

universes u'


lemma is_iso_of_preserves {ğ“ : Type u'} [category.{u} ğ“] [preadditive ğ“]
  (F G : AddCommGroup â¥¤ ğ“)
  [F.additive]
  [G.additive]
  [limits.preserves_filtered_colimits F]
  [limits.preserves_filtered_colimits G]
  (Î· : F âŸ¶ G)
  [hÎ· : is_iso (Î·.app tunit)]
  (A : AddCommGroup.{u})
  [no_zero_smul_divisors â„¤ A] :
  is_iso (Î·.app A) :=
begin
  let T := (limits.cocones.precompose (whisker_left A.diagram Î·)).obj
    (G.map_cocone A.cocone),
  let S := F.map_cocone A.cocone,
  let hS : limits.is_colimit S :=
    limits.is_colimit_of_preserves F A.is_colimit_cocone,
  have : Î·.app A = hS.desc T,
  { apply hS.hom_ext, intros j, rw hS.fac,
    dsimp, apply Î·.naturality },
  rw this, clear this,
  suffices : âˆ€ I : A.index_cat, is_iso (Î·.app (A.diagram.obj I)),
  { resetI,
    haveI : is_iso (whisker_left A.diagram Î·),
    { apply_with nat_iso.is_iso_of_is_iso_app { instances := ff },
      intros I, exact this I },
    let hT : limits.is_colimit T :=
      (limits.is_colimit.precompose_hom_equiv (as_iso (whisker_left A.diagram Î·))
      (G.map_cocone A.cocone)).symm (limits.is_colimit_of_preserves G A.is_colimit_cocone),
    use hT.desc S,
    split,
    { apply hS.hom_ext,
      intros j,
      erw [hS.fac_assoc, hT.fac, category.comp_id] },
    { apply hT.hom_ext,
      intros j,
      erw [hT.fac_assoc, hS.fac, category.comp_id] }
  }, --^ general colimit nonsense..., but I can't find applicable lemmas :-(
  intros I,
  obtain âŸ¨Î¹,hÎ¹,e,-âŸ© := A.exists_biprod_iso_of_index I,
  -- now use the fact that the functors are additive and that there exists some iso with a biproduct
  resetI,
  let eF : F.obj (â¨ Î» (i : Î¹), tunit.{u}) â‰… â¨ Î» (i : Î¹), F.obj tunit :=
    (limits.is_bilimit_of_preserves F
    (limits.biproduct.is_bilimit (Î» i : Î¹, tunit.{u}))).is_colimit.cocone_point_unique_up_to_iso
    (limits.biproduct.is_bilimit (Î» i : Î¹, F.obj tunit)).is_colimit,
  let eG : G.obj (â¨ Î» (i : Î¹), tunit.{u}) â‰… â¨ Î» (i : Î¹), G.obj tunit :=
    (limits.is_bilimit_of_preserves G
    (limits.biproduct.is_bilimit (Î» i : Î¹, tunit.{u}))).is_colimit.cocone_point_unique_up_to_iso
    (limits.biproduct.is_bilimit (Î» i : Î¹, G.obj tunit)).is_colimit,
  have : Î·.app (A.diagram.obj I) =
    F.map e.inv â‰« eF.hom â‰« limits.biproduct.desc
      (Î» i, Î·.app _ â‰« limits.biproduct.Î¹ _ i) â‰« eG.inv â‰« G.map e.hom,
  { rw [â† functor.map_iso_inv, iso.eq_inv_comp, â† iso.inv_comp_eq],
    apply limits.biproduct.hom_ext', intros i,
    simp only [functor.map_iso_hom, nat_trans.naturality,
      limits.biproduct.Î¹_desc_assoc, category.assoc],
    erw [limits.biproduct.Î¹_desc_assoc, limits.biproduct.Î¹_desc_assoc],
    dsimp, rw Î·.naturality_assoc },
  rw this,
  apply_with is_iso.comp_is_iso { instances := ff },
  apply_instance,
  apply_with is_iso.comp_is_iso { instances := ff },
  apply_instance,
  apply_with is_iso.comp_is_iso { instances := ff },
  swap,
  apply_instance,
  use limits.biproduct.desc
      (Î» i, inv (Î·.app _) â‰« limits.biproduct.Î¹ _ i),
  split,
  { ext, simp },
  { ext, simp },
end

def is_tensor_unit (A : AddCommGroup.{u}) : Prop :=
âˆƒ a : A, âˆ€ (B : AddCommGroup.{u}), function.bijective
  (Î» f : A âŸ¶ B, (f : A â†’ B) a)

def is_tensor_unit.gen {A : AddCommGroup.{u}} (h : A.is_tensor_unit) :
  A := h.some

def is_tensor_unit.as_hom {A B : AddCommGroup.{u}} (h : A.is_tensor_unit)
  (b : B) : A âŸ¶ B :=
((h.some_spec B).2 b).some

@[simp]
lemma is_tensor_unit.eval_as_hom {A B : AddCommGroup.{u}} (h : A.is_tensor_unit)
  (b : B) : h.as_hom b h.gen = b :=
((h.some_spec B).2 b).some_spec

lemma is_tensor_unit.ext {A B : AddCommGroup.{u}} (h : A.is_tensor_unit)
  (f g : A âŸ¶ B) (hh : f h.gen = g h.gen) : f = g :=
(h.some_spec B).1 hh

def is_tensor_unit.iso {A B : AddCommGroup.{u}} (hA : A.is_tensor_unit)
  (hB : B.is_tensor_unit) : A â‰… B :=
{ hom := hA.as_hom hB.gen,
  inv := hB.as_hom hA.gen,
  hom_inv_id' := begin
    apply hA.ext,
    simp,
  end,
  inv_hom_id' := begin
    apply hB.ext,
    simp,
  end }

lemma is_tensor_unit_tunit : tunit.{u}.is_tensor_unit :=
begin
  use tunit.gen,
  intros B,
  split,
  { intros f g h,
    dsimp [tunit.gen] at h,
    change (adj'.unit.app _ â‰« (forget _).map f) _ =
      (adj'.unit.app _ â‰« (forget _).map g) _ at h,
    apply_fun adj'.hom_equiv _ _,
    simp only [adjunction.hom_equiv_unit],
    ext t,
    let e := limits.terminal.from (punit : Type u),
    suffices : t = e punit.star,
    { rw this, exact h },
    apply_fun types.punit_iso.inv,
    { simp },
    intros i j h, apply_fun types.punit_iso.hom at h,
    simpa using h },
  { intros t,
    use tunit.lift (types.pt t),
    dsimp only [tunit.lift, types.pt, tunit.gen],
    simp only [adjunction.hom_equiv_counit],
    change (adj'.unit.app _ â‰«
      (forget _).map (free'.map (Î» (x : âŠ¤_ Type u), t) â‰« adj'.counit.app B)) _ = _,
    simp only [functor.map_comp, adjunction.unit_naturality_assoc],
    erw adj'.right_triangle_components, refl }
end

lemma is_iso_of_preserves_of_is_tensor_unit {ğ“ : Type u'} [category.{u} ğ“] [preadditive ğ“]
  (F G : AddCommGroup â¥¤ ğ“)
  [F.additive]
  [G.additive]
  [limits.preserves_filtered_colimits F]
  [limits.preserves_filtered_colimits G]
  (Î· : F âŸ¶ G)
  (U : AddCommGroup)
  (hU : U.is_tensor_unit)
  [hÎ· : is_iso (Î·.app U)]
  (A : AddCommGroup.{u})
  [no_zero_smul_divisors â„¤ A] :
  is_iso (Î·.app A) :=
begin
  suffices : is_iso (Î·.app tunit),
  { resetI, apply is_iso_of_preserves },
  let e : tunit â‰… U := is_tensor_unit_tunit.iso hU,
  suffices : Î·.app tunit = F.map e.hom â‰« Î·.app U â‰« G.map e.inv,
  { rw this, apply is_iso.comp_is_iso, },
  rw [â† Î·.naturality, â† F.map_comp_assoc, e.hom_inv_id, F.map_id, category.id_comp],
end

end AddCommGroup

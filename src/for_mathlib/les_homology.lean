import data.matrix.notation

import for_mathlib.snake_lemma2
import for_mathlib.short_exact_sequence

noncomputable theory

open category_theory
open category_theory.limits

universes v u

lemma preadditive.exact_of_iso_of_exact' {D : Type*} [category D] [abelian D]
  {A‚ÇÅ B‚ÇÅ C‚ÇÅ A‚ÇÇ B‚ÇÇ C‚ÇÇ : D}
  (f‚ÇÅ : A‚ÇÅ ‚ü∂ B‚ÇÅ) (g‚ÇÅ : B‚ÇÅ ‚ü∂ C‚ÇÅ) (f‚ÇÇ : A‚ÇÇ ‚ü∂ B‚ÇÇ) (g‚ÇÇ : B‚ÇÇ ‚ü∂ C‚ÇÇ)
  (Œ± : A‚ÇÅ ‚âÖ A‚ÇÇ) (Œ≤ : B‚ÇÅ ‚âÖ B‚ÇÇ) (Œ≥ : C‚ÇÅ ‚âÖ C‚ÇÇ) (hsq‚ÇÅ : Œ±.hom ‚â´ f‚ÇÇ = f‚ÇÅ ‚â´ Œ≤.hom)
  (hsq‚ÇÇ : Œ≤.hom ‚â´ g‚ÇÇ = g‚ÇÅ ‚â´ Œ≥.hom)
  (h : exact f‚ÇÅ g‚ÇÅ) :
  exact f‚ÇÇ g‚ÇÇ :=
preadditive.exact_of_iso_of_exact f‚ÇÅ g‚ÇÅ f‚ÇÇ g‚ÇÇ (arrow.iso_mk Œ± Œ≤ hsq‚ÇÅ) (arrow.iso_mk Œ≤ Œ≥ hsq‚ÇÇ) rfl h

namespace homological_complex

variables {C : Type u} [category.{v} C] [abelian C]
variables {Œπ : Type*} {c : complex_shape Œπ}

def mod_boundaries (A : homological_complex C c) (j : Œπ) : C :=
cokernel ((A.boundaries j).arrow)

def mod_boundaries_map {A B : homological_complex C c} (f : A ‚ü∂ B) (j : Œπ) :
  A.mod_boundaries j ‚ü∂ B.mod_boundaries j :=
cokernel.map _ _ (boundaries_map f j) (f.f j) $ by { rw image_subobject_map_arrow, refl }

@[simps]
def mod_boundaries_functor (j : Œπ) : homological_complex C c ‚•§ C :=
{ obj := Œª A, A.mod_boundaries j,
  map := Œª A B f, mod_boundaries_map f j,
  map_id' := Œª A,
  begin
    delta mod_boundaries mod_boundaries_map cokernel.map, ext,
    show cokernel.œÄ (A.boundaries j).arrow ‚â´ _ = cokernel.œÄ (A.boundaries j).arrow ‚â´ _,
    simp only [cokernel.œÄ_desc, category.id_comp, id_f, category.comp_id],
  end,
  map_comp' := Œª X Y Z f g,
  begin
    delta mod_boundaries mod_boundaries_map cokernel.map, ext,
    show cokernel.œÄ (X.boundaries j).arrow ‚â´ _ = cokernel.œÄ (X.boundaries j).arrow ‚â´ _,
    simp only [cokernel.œÄ_desc, cokernel.œÄ_desc_assoc, comp_f, category.assoc],
  end }
.

-- generalize to chain complexes over other shapes
@[simps]
def homology_to_mod_boundaries (n : ‚Ñï) :
  homology_functor C (complex_shape.down ‚Ñï) n ‚ü∂ mod_boundaries_functor n :=
{ app := Œª A, cokernel.map _ _ (ùüô _) ((A.cycles n).arrow)
    (by simp only [boundaries_to_cycles_arrow, category.id_comp]),
  naturality' := Œª A B f,
  begin
    ext,
    simp only [homology_functor_map, mod_boundaries_functor_map, homology.œÄ_map_assoc],
    delta mod_boundaries_map homology.œÄ cokernel.map cycles,
    simp only [cokernel.œÄ_desc, cokernel.œÄ_desc_assoc, comp_f, category.assoc,
      kernel_subobject_map_arrow_assoc, hom.sq_from_left],
  end }
.

section

variables (A : chain_complex C ‚Ñï) (n : ‚Ñï)

def delta_to_boundaries : A.X (n+1) ‚ü∂ (A.boundaries n) :=
(X_prev_iso A rfl).inv ‚â´ factor_thru_image_subobject _

instance delta_to_boundaries_epi : epi (delta_to_boundaries A n) :=
epi_comp _ _

@[ext] lemma boundaries.ext {X : C} (f g : (boundaries A n : C) ‚ü∂ X)
  (h : delta_to_boundaries A n ‚â´ f = delta_to_boundaries A n ‚â´ g) : f = g :=
by rwa ‚Üê cancel_epi (delta_to_boundaries A n)

@[simp, reassoc] lemma delta_to_boundaries_comp_arrow :
  (delta_to_boundaries A n) ‚â´ (boundaries A n).arrow = A.d (n + 1) n :=
by rw [delta_to_boundaries, category.assoc, image_subobject_arrow_comp, X_prev_iso_comp_d_to]

@[simp, reassoc] lemma boundaries_arrow_comp_delta_to_boundaries :
  (boundaries _ _).arrow ‚â´ delta_to_boundaries A n = 0 :=
begin
  ext,
  simp only [delta_to_boundaries_comp_arrow_assoc, category.assoc, delta_to_boundaries_comp_arrow,
    d_comp_d, comp_zero, zero_comp],
end

def delta_to_cycles : A.X (n+1) ‚ü∂ (A.cycles n) :=
delta_to_boundaries _ _ ‚â´ boundaries_to_cycles _ _

@[simp, reassoc] lemma delta_to_cycles_comp_arrow :
  (delta_to_cycles A n) ‚â´ (cycles A n).arrow = A.d (n + 1) n :=
by rw [delta_to_cycles, category.assoc, boundaries_to_cycles_arrow, delta_to_boundaries_comp_arrow]

@[simp, reassoc] lemma boundaries_arrow_comp_delta_to_cycles :
  (boundaries _ _).arrow ‚â´ delta_to_cycles A n = 0 :=
by rw [delta_to_cycles, ‚Üê category.assoc, boundaries_arrow_comp_delta_to_boundaries, zero_comp]

end

-- generalize to chain complexes over other shapes
@[simps]
def mod_boundaries_to_cycles (n : ‚Ñï) :
  mod_boundaries_functor (n+1) ‚ü∂ cycles_functor C (complex_shape.down ‚Ñï) n :=
{ app := Œª A, cokernel.desc _ (delta_to_cycles _ _) (boundaries_arrow_comp_delta_to_cycles _ _),
  naturality' := Œª A B f,
  begin
    ext, show cokernel.œÄ _ ‚â´ _ = cokernel.œÄ _ ‚â´ _,
    simp only [homology_functor_map, mod_boundaries_functor_map, homology.œÄ_map_assoc],
    delta mod_boundaries_map homology.œÄ cokernel.map,
    simp only [category.assoc, cycles_functor_map, cycles_map_arrow, hom.comm,
      cokernel.œÄ_desc_assoc, delta_to_cycles_comp_arrow_assoc, delta_to_cycles_comp_arrow]
  end }
.

-- generalize to chain complexes over other shapes
@[simps]
def cycles_to_homology (n : ‚Ñï) :
  cycles_functor C (complex_shape.down ‚Ñï) n ‚ü∂ homology_functor C (complex_shape.down ‚Ñï) n :=
{ app := Œª A, cokernel.œÄ _,
  naturality' := Œª A B f,
  begin
    simp only [cycles_functor_map, homology_functor_map],
    delta homology.map,
    rw cokernel.œÄ_desc, refl,
  end }

open_locale zero_object

instance uugh {A B : chain_complex C ‚Ñï} (f : A ‚ü∂ B) [‚àÄ n, epi (f.f n)] (n : ‚Ñï) :
  epi (f.prev n) :=
begin
  have : (complex_shape.down ‚Ñï).rel (n+1) n := rfl,
  rw hom.prev_eq f this,
  apply_with epi_comp { instances := ff },
  { apply_instance },
  { apply epi_comp }
end

instance {A B : chain_complex C ‚Ñï} (f : A ‚ü∂ B) [‚àÄ n, epi (f.f n)] (n : ‚Ñï) :
  epi (boundaries_map f n) :=
begin
  let sq := hom.sq_to f n,
  haveI : epi sq.left := by { dsimp, apply_instance, },
  apply_with (epi_of_epi (factor_thru_image_subobject _)) { instances := ff },
  suffices : factor_thru_image_subobject (A.d_to n) ‚â´
      boundaries_map f n =
    sq.left ‚â´ factor_thru_image_subobject (B.d_to n),
  { rw this, apply epi_comp, },
  ext,
  simp only [category.assoc, image_subobject_map_arrow, hom.sq_to_right,
    image_subobject_arrow_comp_assoc, hom.sq_to_left, image_subobject_arrow_comp, hom.comm_to],
end

instance uuugher (A B : C) (f : A ‚ü∂ B) : exact (kernel_subobject f).arrow f :=
by { rw [‚Üê kernel_subobject_arrow, exact_iso_comp], apply_instance }

instance uuugh (A : chain_complex C ‚Ñï) (n : ‚Ñï) : exact (cycles A n).arrow (d_from A n) :=
by delta cycles; apply_instance

lemma X_next_is_zero (A : chain_complex C ‚Ñï) : is_zero (A.X_next 0) :=
begin
  apply is_zero_of_iso_of_zero (is_zero_zero _),
  apply (X_next_iso_zero A _).symm,
  delta complex_shape.next option.choice,
  simp only [dif_neg, complex_shape.down_rel, nat.succ_ne_zero, nonempty_subtype,
    exists_false, not_false_iff],
end

lemma next_eq_zero {A‚ÇÅ A‚ÇÇ : chain_complex C ‚Ñï} (f : A‚ÇÅ ‚ü∂ A‚ÇÇ) :
  f.next 0 = 0 :=
(X_next_is_zero _).eq_zero_of_src _

instance jmc_is_weeping {A‚ÇÅ A‚ÇÇ : chain_complex C ‚Ñï} (f : A‚ÇÅ ‚ü∂ A‚ÇÇ) (n : ‚Ñï) [‚àÄ n, mono (f.f n)] :
  mono (f.next n) :=
begin
  cases n,
  { refine ‚ü®Œª Z a b H, _‚ü©, apply (X_next_is_zero _).eq_of_tgt },
  have : (complex_shape.down ‚Ñï).rel n.succ n := rfl,
  rw hom.next_eq _ this,
  apply_with mono_comp { instances := ff },
  { apply_instance },
  { apply mono_comp }
end

instance jmc_is_crying {A‚ÇÅ A‚ÇÇ A‚ÇÉ : chain_complex C ‚Ñï} (f : A‚ÇÅ ‚ü∂ A‚ÇÇ) (g : A‚ÇÇ ‚ü∂ A‚ÇÉ) (n : ‚Ñï)
  [‚àÄ n, exact (f.f n) (g.f n)] : exact (f.next n) (g.next n) :=
begin
  cases n,
  { rw [next_eq_zero],
    apply_with exact_zero_left_of_mono { instances := ff },
    { apply_instance },
    { refine ‚ü®Œª Z a b H, _‚ü©, apply (X_next_is_zero _).eq_of_tgt } },
  have : (complex_shape.down ‚Ñï).rel n.succ n := rfl,
  refine preadditive.exact_of_iso_of_exact' (f.f n) (g.f n) _ _
    (X_next_iso A‚ÇÅ this).symm (X_next_iso A‚ÇÇ this).symm (X_next_iso A‚ÇÉ this).symm
    _ _ infer_instance;
  simp only [hom.next_eq _ this, iso.symm_hom, iso.inv_hom_id_assoc],
end

lemma exact_cycles_map {A‚ÇÅ A‚ÇÇ A‚ÇÉ : chain_complex C ‚Ñï} (f : A‚ÇÅ ‚ü∂ A‚ÇÇ) (g : A‚ÇÇ ‚ü∂ A‚ÇÉ)
  (hfg : ‚àÄ n, short_exact (f.f n) (g.f n)) (n : ‚Ñï) :
  exact (cycles_map f n) (cycles_map g n) :=
begin
  have sq‚ÇÅ :  d_from A‚ÇÅ n ‚â´ f.next n = f.f n ‚â´ d_from A‚ÇÇ n := (hom.comm_from _ _).symm,
  have sq‚ÇÇ :  d_from A‚ÇÇ n ‚â´ g.next n = g.f n ‚â´ d_from A‚ÇÉ n := (hom.comm_from _ _).symm,
  suffices S : snake
    ‚Üë(cycles A‚ÇÅ n) ‚Üë(cycles A‚ÇÇ n) ‚Üë(cycles A‚ÇÉ n)
    (A‚ÇÅ.X n) (A‚ÇÇ.X n) (A‚ÇÉ.X n)
    _ _ _
    _ _ _
    (cycles_map f n) (cycles_map g n)
    (cycles _ n).arrow (cycles _ n).arrow (cycles _ n).arrow
    (f.f n) (g.f n)
    (A‚ÇÅ.d_from n) (A‚ÇÇ.d_from n) (A‚ÇÉ.d_from n)
    (f.next n) (g.next n)
    (cokernel.œÄ $ A‚ÇÅ.d_from n) (cokernel.œÄ $ A‚ÇÇ.d_from n) (cokernel.œÄ $ A‚ÇÉ.d_from n)
    (cokernel.map _ _ _ _ sq‚ÇÅ) (cokernel.map _ _ _ _ sq‚ÇÇ),
  { exact S.six_term_exact_seq.pair },
  have hfg_exact := Œª n, (hfg n).exact,
  have hfg_epi := Œª n, (hfg n).epi,
  have hfg_mono := Œª n, (hfg n).mono,
  resetI,
  fsplit,
  { refine exact_seq.cons _ _ infer_instance _ ((exact_iff_exact_seq _ _).mp infer_instance) },
  { refine exact_seq.cons _ _ infer_instance _ ((exact_iff_exact_seq _ _).mp infer_instance) },
  { refine exact_seq.cons _ _ infer_instance _ ((exact_iff_exact_seq _ _).mp infer_instance) },
  { rw cycles_map_arrow, },
  { rw cycles_map_arrow, },
  { exact sq‚ÇÅ },
  { exact sq‚ÇÇ },
  { apply cokernel.œÄ_desc, },
  { apply cokernel.œÄ_desc, },
end

variables {A‚ÇÅ A‚ÇÇ A‚ÇÉ : chain_complex C ‚Ñï} (f : A‚ÇÅ ‚ü∂ A‚ÇÇ) (g : A‚ÇÇ ‚ü∂ A‚ÇÉ)
variables (hfg : ‚àÄ n, short_exact (f.f n) (g.f n))

lemma mono_cycles_map (hfg : ‚àÄ n, short_exact (f.f n) (g.f n)) (n : ‚Ñï) :
  mono (cycles_map f n) :=
begin
  apply_with (mono_of_mono _ (subobject.arrow _)) { instances := ff },
  rw cycles_map_arrow,
  haveI : mono (f.f n) := (hfg n).mono,
  apply mono_comp,
end

@[simp] lemma image_subobject_arrow {X : C} (S : subobject X) :
  image_subobject (S.arrow) = S :=
begin
  delta image_subobject,
  ext,
  swap,
  { exact limits.image_mono_iso_source _ },
  { simp }
end

@[simp] lemma kernel_subobject_cokernel.œÄ {X : C} (S : subobject X) :
  kernel_subobject (cokernel.œÄ S.arrow) = S :=
begin
  delta kernel_subobject,
  ext,
  swap,
  { exact (abelian.image_iso_image _).trans (limits.image_mono_iso_source _) },
  { simp }
end

lemma exact.congr {X‚ÇÅ X‚ÇÇ Y Z‚ÇÅ Z‚ÇÇ : C} (f‚ÇÅ : X‚ÇÅ ‚ü∂ Y) (g‚ÇÅ : Y ‚ü∂ Z‚ÇÅ) (f‚ÇÇ : X‚ÇÇ ‚ü∂ Y) (g‚ÇÇ : Y ‚ü∂ Z‚ÇÇ)
  (h : exact f‚ÇÅ g‚ÇÅ) (him : image_subobject f‚ÇÅ = image_subobject f‚ÇÇ)
  (hker : kernel_subobject g‚ÇÅ = kernel_subobject g‚ÇÇ) :
  exact f‚ÇÇ g‚ÇÇ :=
by rwa [abelian.exact_iff_image_eq_kernel, ‚Üê him, ‚Üê hker, ‚Üê abelian.exact_iff_image_eq_kernel]

lemma exact_column (A : chain_complex C ‚Ñï) (n : ‚Ñï) :
exact_seq C [(kernel.Œπ (A.d (n + 1) n)), (A.d (n + 1) n), (cokernel.œÄ (A.boundaries n).arrow)] :=
begin
  refine exact_seq.cons _ _ exact_kernel_Œπ _ _,
  rw [‚Üê exact_iff_exact_seq],
  have : (complex_shape.down ‚Ñï).rel (n + 1) n := rfl,
  refine exact.congr (boundaries A n).arrow _ _ _ infer_instance _ rfl,
  rw [‚Üê boundaries_eq_image_subobject A this, image_subobject_arrow]
end

lemma exact_mod_boundaries_map (hfg : ‚àÄ n, short_exact (f.f n) (g.f n)) (n : ‚Ñï) :
  exact (mod_boundaries_map f n) (mod_boundaries_map g n) :=
begin
  have sq1 : A‚ÇÅ.d (n + 1) n ‚â´ f.f n = f.f (n+1) ‚â´ A‚ÇÇ.d (n + 1) n := (f.comm _ _).symm,
  have sq2 : A‚ÇÇ.d (n + 1) n ‚â´ g.f n = g.f (n+1) ‚â´ A‚ÇÉ.d (n + 1) n := (g.comm _ _).symm,
  suffices S : snake
    -- the objects
         (kernel _)           (kernel _)           (kernel _)
        (A‚ÇÅ.X (n+1))         (A‚ÇÇ.X (n+1))         (A‚ÇÉ.X (n+1))
          (A‚ÇÅ.X n)             (A‚ÇÇ.X n)             (A‚ÇÉ.X n)
    (mod_boundaries _ n) (mod_boundaries _ n) (mod_boundaries _ n)
    -- the morphisms
    (kernel.map _ _ _ _ sq1) (kernel.map _ _ _ _ sq2)
    (kernel.Œπ $ A‚ÇÅ.d (n+1) n) (kernel.Œπ $ A‚ÇÇ.d (n+1) n) (kernel.Œπ $ A‚ÇÉ.d (n+1) n)
    (f.f (n+1)) (g.f (n+1))
    (A‚ÇÅ.d (n+1) n) (A‚ÇÇ.d (n+1) n) (A‚ÇÉ.d (n+1) n)
    (f.f n) (g.f n)
    (cokernel.œÄ _) (cokernel.œÄ _) (cokernel.œÄ _)
    (mod_boundaries_map f n) (mod_boundaries_map g n),
  { exact (S.six_term_exact_seq.drop 3).pair },
  have hfg_exact := Œª n, (hfg n).exact,
  have hfg_epi := Œª n, (hfg n).epi,
  have hfg_mono := Œª n, (hfg n).mono,
  resetI,
  fsplit,
  { apply exact_column },
  { apply exact_column },
  { apply exact_column },
  { simp },
  { simp },
  { exact sq1 },
  { exact sq2 },
  { simp [mod_boundaries_map] },
  { simp [mod_boundaries_map] }
end

lemma epi_mod_boundaries_map (hfg : ‚àÄ n, short_exact (f.f n) (g.f n)) (n : ‚Ñï) :
  epi (mod_boundaries_map g n) :=
begin
  apply_with (epi_of_epi (cokernel.œÄ _)) { instances := ff },
  haveI : epi (g.f n) := (hfg n).epi,
  have : cokernel.œÄ _ ‚â´ mod_boundaries_map g n = g.f n ‚â´ cokernel.œÄ _ := cokernel.œÄ_desc _ _ _,
  rw this,
  apply epi_comp,
end

lemma mono_homology_to_mod_boundaries (A : chain_complex C ‚Ñï) (n : ‚Ñï) :
  mono ((homology_to_mod_boundaries n).app A) :=
cokernel.map_mono_of_epi_of_mono
  (boundaries A n) (cycles A n)
  (boundaries A n) (A.X n)
  _ _ _ _ _

variables {C}

@[simp] lemma image_subobject_comp_eq_of_epi {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [epi f] :
  image_subobject (f ‚â´ g) = image_subobject g :=
begin
  delta image_subobject,
  haveI : is_iso (image.pre_comp f g) := is_iso_of_mono_of_epi _,
  ext, swap,
  { exact as_iso (image.pre_comp f g) },
  { simp only [as_iso_hom, image.pre_comp_Œπ], },
end

@[simp] lemma kernel_subobject_comp_eq_of_mono {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [mono g] :
  kernel_subobject (f ‚â´ g) = kernel_subobject f :=
begin
  delta kernel_subobject,
  ext, swap,
  { exact kernel_comp_mono f g },
  { simp only [kernel_comp_mono_hom, kernel.lift_Œπ] },
end

lemma exact_cycles_arrow_delta_to_cycles (A : chain_complex C ‚Ñï) (n : ‚Ñï) :
  exact (A.cycles (n+1)).arrow (delta_to_cycles A n) :=
begin
  rw [category_theory.abelian.exact_iff_image_eq_kernel],
  dsimp [delta_to_cycles, delta_to_boundaries],
  simp only [image_subobject_arrow, kernel_subobject_comp_eq_of_mono],
  delta cycles,
  have : (complex_shape.down ‚Ñï).rel (n + 1) n := rfl,
  let g : ‚Üë(A.boundaries n) ‚ü∂ X_next A (n + 1) := (A.boundaries n).arrow ‚â´ (X_next_iso _ this).inv,
  haveI : mono g := mono_comp _ _,
  suffices aux : delta_to_boundaries _ _ ‚â´ g = d_from A (n + 1),
  { simp_rw [‚Üê aux, kernel_subobject_comp_eq_of_mono], refl, },
  simp only [delta_to_boundaries_comp_arrow_assoc, iso.comp_inv_eq, d_from_comp_X_next_iso],
end

lemma exact_homology_to_mod_boundaries_to_cycles (A : chain_complex C ‚Ñï) (n : ‚Ñï) :
  exact ((homology_to_mod_boundaries (n+1)).app A) ((mod_boundaries_to_cycles n).app A) :=
begin
  let œÜ : homology A (n + 1) ‚ü∂ mod_boundaries A (n + 1) :=
    limits.cokernel.desc _ ((kernel_subobject _).arrow ‚â´ (cokernel.œÄ _)) (by simp),
  suffices S : snake
    (0:C) 0 0
    (A.boundaries (n+1)) (boundaries A (n+1)) 0
    (A.cycles (n+1)) (A.X (n+1)) (A.cycles n)
    (homology A (n+1)) (mod_boundaries A (n+1)) (A.cycles n)
    0 0
    0 0 0
    (ùüô _) 0
    (boundaries_to_cycles _ _) (A.boundaries (n+1)).arrow 0
    (A.cycles (n+1)).arrow (delta_to_cycles _ _)
    (homology.œÄ _ _ _) (cokernel.œÄ _) (ùüô _)
    œÜ ((mod_boundaries_to_cycles n).app A),
    { exact (S.six_term_exact_seq.drop 3).pair },
  letI : exact (cycles A (n + 1)).arrow (delta_to_cycles A n) :=
    exact_cycles_arrow_delta_to_cycles _ _,
  letI : epi (homology.œÄ (d_to A (n + 1)) (d_from A (n + 1)) _) := coequalizer.œÄ_epi,
  fsplit,
  { refine exact_seq.cons _ _ (category_theory.exact_zero_mono _) _ _,
    rw [‚Üê exact_iff_exact_seq],
    exact abelian.exact_cokernel _ },
  { refine exact_seq.cons _ _ (category_theory.exact_zero_mono _) _ _,
    rw [‚Üê exact_iff_exact_seq],
    apply_instance },
  { refine exact_seq.cons _ _ (category_theory.exact_zero_mono _) _ _,
    rw [‚Üê exact_iff_exact_seq],
    apply_instance },
  { simp },
  { simp },
  { simp },
  { simp [boundaries_arrow_comp_delta_to_cycles] },
  { dsimp [homology.œÄ, cycles],
    simp },
  { simp },
end

lemma exact_mod_boundaries_to_cycles_to_homology (A : chain_complex C ‚Ñï) (n : ‚Ñï) :
  exact ((mod_boundaries_to_cycles n).app A) ((cycles_to_homology n).app A)  :=
begin
  refine exact.congr (boundaries_to_cycles _ _) _ _ _ _ _ rfl,
  { simp only [cycles_to_homology_app],
    delta boundaries_to_cycles,
    apply_instance },
  { simp only [mod_boundaries_to_cycles_app],
    delta delta_to_cycles,
    rw [‚Üê image_subobject_comp_eq_of_epi (cokernel.œÄ _)],
    simp only [cokernel.œÄ_desc, image_subobject_comp_eq_of_epi], }
end

lemma epi_cycles_to_homology (A : chain_complex C ‚Ñï) (n : ‚Ñï) :
  epi ((cycles_to_homology n).app A) :=
coequalizer.œÄ_epi

lemma exact_seq_column (A : chain_complex C ‚Ñï) (n : ‚Ñï) :
  exact_seq C
    [((homology_to_mod_boundaries (n + 1)).app A‚ÇÅ),
     ((mod_boundaries_to_cycles n).app A‚ÇÅ),
     ((cycles_to_homology n).app A‚ÇÅ)] :=
(exact_homology_to_mod_boundaries_to_cycles _ _).cons
  (exact_mod_boundaries_to_cycles_to_homology _ _).exact_seq

lemma snake (hfg : ‚àÄ n, short_exact (f.f n) (g.f n)) (n : ‚Ñï) :
  snake
  -- the objects
     (A‚ÇÅ.homology (n+1))       (A‚ÇÇ.homology (n+1))       (A‚ÇÉ.homology (n+1))
  (A‚ÇÅ.mod_boundaries (n+1)) (A‚ÇÇ.mod_boundaries (n+1)) (A‚ÇÉ.mod_boundaries (n+1))
        (A‚ÇÅ.cycles n)             (A‚ÇÇ.cycles n)             (A‚ÇÉ.cycles n)
       (A‚ÇÅ.homology n)           (A‚ÇÇ.homology n)           (A‚ÇÉ.homology n)
  -- the morphisms
  ((homology_functor _ _ (n+1)).map f) ((homology_functor _ _ (n+1)).map g)
  ((homology_to_mod_boundaries (n+1)).app A‚ÇÅ)
  ((homology_to_mod_boundaries (n+1)).app A‚ÇÇ)
  ((homology_to_mod_boundaries (n+1)).app A‚ÇÉ)
  (mod_boundaries_map f (n+1)) (mod_boundaries_map g (n+1))
  ((mod_boundaries_to_cycles n).app A‚ÇÅ)
  ((mod_boundaries_to_cycles n).app A‚ÇÇ)
  ((mod_boundaries_to_cycles n).app A‚ÇÉ)
  (cycles_map f n) (cycles_map g n)
  ((cycles_to_homology n).app A‚ÇÅ)
  ((cycles_to_homology n).app A‚ÇÇ)
  ((cycles_to_homology n).app A‚ÇÉ)
  ((homology_functor _ _ n).map f) ((homology_functor _ _ n).map g) :=
{ row_exact‚ÇÅ := exact_mod_boundaries_map f g hfg (n+1),
  row_exact‚ÇÇ := exact_cycles_map f g hfg n,
  row_epi := epi_mod_boundaries_map f g hfg _,
  row_mono := mono_cycles_map f g hfg _,
  col_exact_a := exact_seq_column A‚ÇÅ _,
  col_exact_b := exact_seq_column A‚ÇÇ _,
  col_exact_c := exact_seq_column A‚ÇÉ _,
  col_mono_a := mono_homology_to_mod_boundaries _ _,
  col_mono_b := mono_homology_to_mod_boundaries _ _,
  col_mono_c := mono_homology_to_mod_boundaries _ _,
  col_epi_a := epi_cycles_to_homology _ _,
  col_epi_b := epi_cycles_to_homology _ _,
  col_epi_c := epi_cycles_to_homology _ _,
  sq_a‚ÇÄ := ((homology_to_mod_boundaries _).naturality _).symm,
  sq_b‚ÇÄ := ((homology_to_mod_boundaries _).naturality _).symm,
  sq_a‚ÇÅ := ((mod_boundaries_to_cycles _).naturality _).symm,
  sq_b‚ÇÅ := ((mod_boundaries_to_cycles _).naturality _).symm,
  sq_a‚ÇÇ := ((cycles_to_homology _).naturality _).symm,
  sq_b‚ÇÇ := ((cycles_to_homology _).naturality _).symm }

def Œ¥ (hfg : ‚àÄ n, short_exact (f.f n) (g.f n)) (n : ‚Ñï) :
  homology A‚ÇÉ (n+1) ‚ü∂ homology A‚ÇÅ n :=
(snake f g hfg n).Œ¥

lemma six_term_exact_seq (hfg : ‚àÄ n, short_exact (f.f n) (g.f n)) (n : ‚Ñï) :
  exact_seq C [
    (homology_functor _ _ (n+1)).map f, -- H‚Åø‚Å∫¬π(A‚ÇÅ) ‚ü∂ H‚Åø‚Å∫¬π(A‚ÇÇ)
    (homology_functor _ _ (n+1)).map g, -- H‚Åø‚Å∫¬π(A‚ÇÇ) ‚ü∂ H‚Åø‚Å∫¬π(A‚ÇÉ)
    Œ¥ f g hfg n,                                          -- H‚Åø‚Å∫¬π(A‚ÇÉ) ‚ü∂  H‚Åø(A‚ÇÅ)
    (homology_functor _ _ n).map f,     --  H‚Åø(A‚ÇÅ)  ‚ü∂  H‚Åø(A‚ÇÇ)
    (homology_functor _ _ n).map g      --  H‚Åø(A‚ÇÅ)  ‚ü∂  H‚Åø(A‚ÇÉ)
  ] :=
(snake f g hfg n).six_term_exact_seq

end homological_complex

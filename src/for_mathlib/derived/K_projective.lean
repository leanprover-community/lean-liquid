import for_mathlib.homotopy_category_pretriangulated
import for_mathlib.abelian_category
import for_mathlib.derived.homological
import for_mathlib.derived.bounded_homotopy_category
import category_theory.abelian.projective
import for_mathlib.snake_lemma3
import for_mathlib.les_homology
import for_mathlib.exact_seq3
import for_mathlib.triangle_shift
import for_mathlib.homology_iso
import for_mathlib.projective_replacement
import for_mathlib.derived.lemmas
-- import for_mathlib.arrow_preadditive

import hacks_and_tricks.asyncI

noncomputable theory

open category_theory category_theory.limits category_theory.triangulated
open homological_complex

universes v u
variables {A : Type u} [category.{v} A] [abelian A]

namespace bounded_homotopy_category

local notation `ğ’¦` := bounded_homotopy_category A

section enough_projectives

abbreviation uniformly_bounded {Î± : Type*} (X : Î± â†’ ğ’¦) : Prop :=
homotopy_category.is_uniformly_bounded_above (val âˆ˜ X)

variable [enough_projectives A]

-- Main theorem about existence of K-projective replacements.
-- Perhaps all we need is this for bounded complexes, in which case we should
-- add an additional typeclass parameter here.
theorem exists_K_projective_replacement (X : ğ’¦) :
  âˆƒ (P : ğ’¦) [homotopy_category.is_K_projective P.val] (f : P âŸ¶ X),
  homotopy_category.is_quasi_iso f âˆ§ âˆ€ k, projective (P.val.as.X k) :=
begin
  obtain âŸ¨P,h1,h2,f,h3âŸ© :=
    homotopy_category.exists_K_projective_replacement_of_bounded X.val,
  resetI,

  exact âŸ¨âŸ¨PâŸ©, h1, f, h3âŸ©,
end

theorem exists_uniform_K_projective_replacement {Î± : Type*} (X : Î± â†’ ğ’¦)
  [uniformly_bounded X] :
  âˆƒ (P : Î± â†’ ğ’¦)
  [âˆ€ a, homotopy_category.is_K_projective (P a).val]
  [uniformly_bounded P]
  (f : Î  a, P a âŸ¶ X a),
  (âˆ€ a, homotopy_category.is_quasi_iso (f a)) âˆ§ âˆ€ a k, projective ((P a).val.as.X k) :=
begin
  obtain âŸ¨P,h1,h2,f,h3,h4âŸ© := homotopy_category.exists_K_projective_replacement_of_uniformly_bounded_above
    (val âˆ˜ X),
  resetI,
  exact âŸ¨Î» a, âŸ¨P aâŸ©, infer_instance, infer_instance, f, h3, h4âŸ©,
end

open homotopy_category

def replace (X : ğ’¦) : ğ’¦ := (exists_K_projective_replacement X).some

instance (X : ğ’¦) : is_K_projective X.replace.val :=
(exists_K_projective_replacement X).some_spec.some

def Ï€ (X : ğ’¦) : X.replace âŸ¶ X :=
(exists_K_projective_replacement X).some_spec.some_spec.some

instance (X : ğ’¦) : is_quasi_iso X.Ï€ :=
(exists_K_projective_replacement X).some_spec.some_spec.some_spec.1

instance (X : ğ’¦) (k : â„¤) : projective (X.replace.val.as.X k) :=
(exists_K_projective_replacement X).some_spec.some_spec.some_spec.2 k

def replace_uniformly {Î± : Type v}
  (X : Î± â†’ bounded_homotopy_category A)
  [uniformly_bounded X] : Î± â†’ bounded_homotopy_category A :=
(exists_uniform_K_projective_replacement X).some

instance is_K_projective_replace_uniformly_apply {Î± : Type v}
  (X : Î± â†’ bounded_homotopy_category A)
  [uniformly_bounded X] (a) : homotopy_category.is_K_projective (replace_uniformly X a).val :=
(exists_uniform_K_projective_replacement X).some_spec.some _

def Ï€_uniformly {Î± : Type v}
  (X : Î± â†’ bounded_homotopy_category A)
  [uniformly_bounded X] : Î  a, replace_uniformly X a âŸ¶ X a :=
(exists_uniform_K_projective_replacement X).some_spec.some_spec.some_spec.some

instance is_quasi_iso_Ï€_uniformly {Î± : Type v}
  (X : Î± â†’ bounded_homotopy_category A)
  [uniformly_bounded X] (a) : homotopy_category.is_quasi_iso (Ï€_uniformly X a) :=
(exists_uniform_K_projective_replacement X).some_spec.some_spec.some_spec.some_spec.1 _

instance uniform_bound_replace_uniformly {Î± : Type v}
  (X : Î± â†’ bounded_homotopy_category A)
  [uniformly_bounded X] : uniformly_bounded (replace_uniformly X) :=
(exists_uniform_K_projective_replacement X).some_spec.some_spec.some

def lift {P X Y : ğ’¦} [is_K_projective P.val] (f : P âŸ¶ Y) (g : X âŸ¶ Y) [is_quasi_iso g] :
  P âŸ¶ X :=
((hom_K_projective_bijective P.val g).2 f).some

@[simp, reassoc]
lemma lift_lifts {P X Y : ğ’¦} [is_K_projective P.val] (f : P âŸ¶ Y) (g : X âŸ¶ Y) [is_quasi_iso g] :
  lift f g â‰« g = f :=
((hom_K_projective_bijective P.val g).2 f).some_spec

lemma lift_unique {P X Y : ğ’¦} [is_K_projective P.val] (f : P âŸ¶ Y) (g : X âŸ¶ Y) [is_quasi_iso g]
  (e : P âŸ¶ X) (h : e â‰« g = f) : e = lift f g :=
begin
  apply (hom_K_projective_bijective P.val g).1,
  dsimp,
  erw lift_lifts,
  assumption
end

@[simp]
lemma lift_self {P X : ğ’¦} [is_K_projective P.val] (g : P âŸ¶ X) [is_quasi_iso g] :
  lift g g = ğŸ™ _ :=
(lift_unique _ _ _ (by simp)).symm

@[simp]
lemma lift_comp {P X Y : ğ’¦} [is_K_projective P.val] (f : P âŸ¶ X) (g : X âŸ¶ Y) [is_quasi_iso g] :
  lift (f â‰« g) g = f :=
(lift_unique _ _ _ (by simp)).symm

@[simp, reassoc]
lemma lift_comp_lift_self {P X Y Z : ğ’¦} [is_K_projective P.val] [is_K_projective X.val]
  (f : P âŸ¶ Y) (g : X âŸ¶ Y) [is_quasi_iso g] (k : Z âŸ¶ Y) [is_quasi_iso k] :
  lift f g â‰« lift g k = lift f k :=
lift_unique _ _ _ (by simp)

@[simp, reassoc]
lemma lift_comp_lift_comp {P W X Y Z : ğ’¦} [is_K_projective P.val] [is_K_projective X.val]
  (f : P âŸ¶ Y) (g : X âŸ¶ Y) [is_quasi_iso g] (h : Y âŸ¶ Z) (k : W âŸ¶ Z) [is_quasi_iso k] :
  lift f g â‰« lift (g â‰« h) k = lift (f â‰« h) k :=
lift_unique _ _ _ (by simp)

@[simp] lemma lift_neg {P X Y : ğ’¦} [is_K_projective P.val] (f : P âŸ¶ Y) (g : X âŸ¶ Y) [is_quasi_iso g] :
  lift (-f) g = -(lift f g) :=
(lift_unique _ _ _ (by simp)).symm

lemma lift_add {P X Y : ğ’¦} [is_K_projective P.val] (fâ‚ fâ‚‚ : P âŸ¶ Y) (g : X âŸ¶ Y) [is_quasi_iso g] :
  lift (fâ‚ + fâ‚‚) g = lift fâ‚ g + lift fâ‚‚ g :=
(lift_unique _ _ _ (by simp)).symm

instance is_K_projective_shift (X : ğ’¦) [is_K_projective X.val] (m : â„¤) :
  is_K_projective ((category_theory.shift_functor ğ’¦ m).obj X).val :=
by exact homotopy_category.is_K_projective_shift X.val m -- strange?

instance {X Y : ğ’¦} (g : X âŸ¶ Y) [is_quasi_iso g] (m : â„¤) :
  is_quasi_iso ((category_theory.shift_functor ğ’¦ m).map g) :=
homotopy_category.is_quasi_iso_shift _ _ _ _

lemma shift_functor_map_lift
  {P X Y : ğ’¦} [is_K_projective P.val] (f : P âŸ¶ Y) (g : X âŸ¶ Y) [is_quasi_iso g] (m : â„¤) :
  (category_theory.shift_functor ğ’¦ m).map (lift f g) =
    lift ((category_theory.shift_functor ğ’¦ m).map f) ((category_theory.shift_functor ğ’¦ m).map g) :=
begin
  apply lift_unique,
  simp only [â†category_theory.functor.map_comp, lift_lifts],
end

lemma lift_ext {P X Y : ğ’¦} [is_K_projective P.val] (g : X âŸ¶ Y) [is_quasi_iso g]
  (a b : P âŸ¶ X) (h : a â‰« g = b â‰« g) : a = b :=
(hom_K_projective_bijective P.val g).1 h

@[simps]
def replace_triangle (T : triangle ğ’¦) : triangle ğ’¦ :=
{ objâ‚ := T.objâ‚.replace,
  objâ‚‚ := T.objâ‚‚.replace,
  objâ‚ƒ := T.objâ‚ƒ.replace,
  morâ‚ := lift (T.objâ‚.Ï€ â‰« T.morâ‚) T.objâ‚‚.Ï€,
  morâ‚‚ := lift (T.objâ‚‚.Ï€ â‰« T.morâ‚‚) T.objâ‚ƒ.Ï€,
  morâ‚ƒ := begin
    have h : is_quasi_iso (T.objâ‚.Ï€âŸ¦(1 : â„¤)âŸ§') := infer_instance,
    exact @lift _ _ _ _ _ _ _ _ (T.objâ‚ƒ.Ï€ â‰« T.morâ‚ƒ) (T.objâ‚.Ï€âŸ¦(1 : â„¤)âŸ§') h, -- What?
  end }

lemma distinguished_replace_triangle (T : triangle ğ’¦) (hT : T âˆˆ dist_triang ğ’¦) :
  replace_triangle T âˆˆ dist_triang ğ’¦ :=
begin
  let S := replace_triangle T,
  change S âˆˆ _,
  obtain âŸ¨Z,g,h,hWâŸ© := pretriangulated.distinguished_cocone_triangle _ _ S.morâ‚,
  let W := triangle.mk (bounded_homotopy_category A) S.morâ‚ g h,
  change W âˆˆ _ at hW,
  have hWT : W.morâ‚ â‰« T.objâ‚‚.Ï€ = T.objâ‚.Ï€ â‰« T.morâ‚ := _,
  obtain âŸ¨q,sq2,sq3âŸ© := pretriangulated.complete_distinguished_triangle_morphism _ _ hW hT
    T.objâ‚.Ï€ T.objâ‚‚.Ï€ hWT,
  let r : W âŸ¶ T := âŸ¨T.objâ‚.Ï€, T.objâ‚‚.Ï€, q, hWT, sq2, sq3âŸ©,
  let W' := (triangle.mk (homotopy_category _ _) W.morâ‚ W.morâ‚‚ W.morâ‚ƒ),
  let T' := (triangle.mk (homotopy_category _ _) T.morâ‚ T.morâ‚‚ T.morâ‚ƒ),
  let r' : W' âŸ¶ T' := âŸ¨T.objâ‚.Ï€, T.objâ‚‚.Ï€, q, hWT, sq2, sq3âŸ©,
  haveI : is_quasi_iso r.homâ‚ƒ, { exact is_quasi_iso_of_triangle W' T' hW hT r' },
  haveI : is_K_projective W.objâ‚ƒ.val,
  by asyncI
  { haveI : is_K_projective W'.objâ‚ := show is_K_projective T.objâ‚.replace.val, by apply_instance,
    haveI : is_K_projective W'.objâ‚‚ := show is_K_projective T.objâ‚‚.replace.val, by apply_instance,
    exact homotopy_category.is_K_projective_of_triangle W' hW },
  haveI : is_K_projective S.objâ‚.val := show is_K_projective T.objâ‚.replace.val, by apply_instance,
  haveI : is_K_projective S.objâ‚‚.val := show is_K_projective T.objâ‚‚.replace.val, by apply_instance,
  haveI : is_K_projective S.objâ‚ƒ.val := show is_K_projective T.objâ‚ƒ.replace.val, by apply_instance,
  apply mem_distinguished_of_iso _ hW,
  refine âŸ¨âŸ¨ğŸ™ _,ğŸ™ _, lift q T.objâ‚ƒ.Ï€, _, _, _âŸ©,âŸ¨ğŸ™ _,ğŸ™ _, lift T.objâ‚ƒ.Ï€ q, _,_,_âŸ©,_,_âŸ©,
  asyncI
  { dsimp, rw [category.comp_id, category.id_comp], },
  asyncI
  { dsimp [S, replace_triangle],
    rw category.id_comp,
    apply lift_unique,
    erw [category.assoc, lift_lifts], exact sq2, },
  asyncI
  { dsimp [S, replace_triangle],
    rw [category_theory.functor.map_id, category.comp_id],
    haveI : is_quasi_iso
      ((category_theory.shift_functor (bounded_homotopy_category A) (1 : â„¤)).map T.objâ‚.Ï€),
    { show is_quasi_iso (T.objâ‚.Ï€âŸ¦(1 : â„¤)âŸ§'), apply_instance }, -- strange.
    apply lift_ext (T.objâ‚.Ï€âŸ¦(1 : â„¤)âŸ§'),
    erw [category.assoc, lift_lifts, lift_lifts_assoc],
    exact sq3,
    assumption },
  asyncI
  { dsimp, rw [category.id_comp, category.comp_id] },
  asyncI
  { dsimp [S, replace_triangle],
    rw category.id_comp,
    apply lift_ext q,
    erw [category.assoc, lift_lifts, lift_lifts, sq2],
    assumption },
  asyncI
  { dsimp [S, replace_triangle],
    rw [category_theory.functor.map_id, category.comp_id],
    haveI : is_quasi_iso
      ((category_theory.shift_functor (bounded_homotopy_category A) (1 : â„¤)).map T.objâ‚.Ï€),
    { show is_quasi_iso (T.objâ‚.Ï€âŸ¦(1 : â„¤)âŸ§'), apply_instance }, -- strange.
    apply lift_ext (T.objâ‚.Ï€âŸ¦(1 : â„¤)âŸ§'),
    erw [category.assoc, lift_lifts, sq3, lift_lifts_assoc],
    assumption },
  asyncI
  { ext; dsimp, rw category.id_comp, rw category.id_comp,
    apply lift_ext q, erw [category.assoc, lift_lifts, lift_lifts, category.id_comp],
    assumption },
  asyncI
  { ext; dsimp, rw category.id_comp, rw category.id_comp,
    apply lift_ext T.objâ‚ƒ.Ï€, erw [category.assoc, lift_lifts, lift_lifts, category.id_comp],
    assumption },
  asyncI
  { dsimp [W, S, replace_triangle],
    rw lift_lifts },
end

@[simps]
def Ext0 : ğ’¦áµ’áµ– â¥¤ ğ’¦ â¥¤ Ab :=
{ obj := Î» X, preadditive_yoneda.flip.obj (opposite.op $ X.unop.replace),
  map := Î» Xâ‚ Xâ‚‚ f, preadditive_yoneda.flip.map (lift (Xâ‚‚.unop.Ï€ â‰« f.unop) Xâ‚.unop.Ï€).op,
  map_id' := by asyncI {
    intros X,
    ext Y e,
    dsimp [preadditive_yoneda, preadditive_yoneda_obj],
    change _ â‰« e = e,
    simp only [category.comp_id, id_apply],
    convert category.id_comp _,
    symmetry,
    apply lift_unique,
    simp, },
  map_comp' := by asyncI {
    intros Xâ‚ Xâ‚‚ Xâ‚ƒ f g,
    ext Y e,
    dsimp,
    simp only [comp_apply, linear_map.to_add_monoid_hom_coe,
      preadditive_yoneda_obj_map_apply, quiver.hom.unop_op],
    change _ â‰« e = _ â‰« _ â‰« e,
    conv_rhs { rw â† category.assoc },
    congr' 1,
    symmetry,
    apply lift_unique,
    simp } }
.

def Ext (i : â„¤) : ğ’¦áµ’áµ– â¥¤ ğ’¦ â¥¤ Ab :=
Ext0 â‹™ (whiskering_left _ _ _).obj (shift_functor _ i)

-- why is this so slow?
-- DT: squeezing the simps made it very fast!
@[simps]
def replacement_iso (Pâ‚ Pâ‚‚ X : ğ’¦) [is_K_projective Pâ‚.val] [is_K_projective Pâ‚‚.val]
  (fâ‚ : Pâ‚ âŸ¶ X) (fâ‚‚ : Pâ‚‚ âŸ¶ X) [is_quasi_iso fâ‚] [is_quasi_iso fâ‚‚] : Pâ‚ â‰… Pâ‚‚ :=
{ hom         := lift fâ‚ fâ‚‚,
  inv         := lift fâ‚‚ fâ‚,
  hom_inv_id' := by asyncI {
    have : ğŸ™ Pâ‚ = lift fâ‚ fâ‚,
    { apply lift_unique, simp only [category.id_comp] },
    rw this,
    apply lift_unique,
    simp only [category.assoc, lift_lifts], },
  inv_hom_id' := by asyncI {
    have : ğŸ™ Pâ‚‚ = lift fâ‚‚ fâ‚‚,
    { apply lift_unique, simp only [category.id_comp] },
      rw this,
    apply lift_unique,
    simp only [category.assoc, lift_lifts], } }
.

@[simps]
def Ext_iso
  (i : â„¤) (P X Y : ğ’¦) [is_K_projective P.val]
  (f : P âŸ¶ X) [is_quasi_iso f] :
  ((Ext i).obj (opposite.op X)).obj Y â‰… AddCommGroup.of (P âŸ¶ YâŸ¦iâŸ§) :=
(preadditive_yoneda.obj (YâŸ¦iâŸ§)).map_iso (replacement_iso P X.replace X f X.Ï€).op

instance ext_additive (i : â„¤) (X : ğ’¦) : functor.additive ((Ext i).obj (opposite.op X)) :=
begin
  refine âŸ¨_âŸ©,
  intros X Y f g,
  ext h,
  dsimp [Ext, preadditive_yoneda],
  rw [(category_theory.shift_functor ğ’¦ i).map_add, preadditive.comp_add],
end

instance ext_additive' (i : â„¤) (X : ğ’¦) : functor.additive ((Ext i).flip.obj X).right_op :=
begin
  refine âŸ¨_âŸ©,
  intros X Y f g,
  dsimp [Ext, preadditive_yoneda],
  rw â† op_add,
  congr' 1,
  ext h,
  dsimp,
  rw â† preadditive.add_comp,
  congr' 1,
  symmetry,
  apply lift_unique,
  simp only [preadditive.add_comp, lift_lifts, preadditive.comp_add],
end .

def _root_.category_theory.adjunction.yoneda_whiskering_left
  {C D : Type*} [category C] [category D] {F : C â¥¤ D}
  {G : D â¥¤ C} (adj : F âŠ£ G) :
  yoneda â‹™ ((whiskering_left _ _ _).obj F.op) â‰… G â‹™ yoneda :=
begin
  fapply nat_iso.of_components,
  { intro Y,
    fapply nat_iso.of_components,
    { intro X, exact (adj.hom_equiv (opposite.unop X) Y).to_iso },
    { intros Xâ‚ Xâ‚‚ f, ext g, exact adj.hom_equiv_naturality_left f.unop g } },
  { intros Yâ‚ Yâ‚‚ f, ext X g, exact adj.hom_equiv_naturality_right g f }
end

def _root_.category_theory.adjunction.preadditive_yoneda_whiskering_left
  {C D : Type*} [category C] [category D] [preadditive C] [preadditive D] {F : C â¥¤ D}
  {G : D â¥¤ C} (adj : F âŠ£ G) [functor.additive G] :
  preadditive_yoneda â‹™ ((whiskering_left _ _ _).obj F.op) â‰… G â‹™ preadditive_yoneda :=
begin
  fapply nat_iso.of_components,
  { intro Y,
    fapply nat_iso.of_components,
    { intro X,
      refine add_equiv_iso_AddCommGroup_iso.hom
        { map_add' := _, ..(adj.hom_equiv (opposite.unop X) Y) },
      intros f g, simp },
    { intros Xâ‚ Xâ‚‚ f, ext g, exact adj.hom_equiv_naturality_left f.unop g } },
  { intros Yâ‚ Yâ‚‚ f, ext X g, exact adj.hom_equiv_naturality_right g f }
end
.

end enough_projectives

instance shift_equiv_symm_inverse_additive (i : â„¤) :
  (shift_equiv (bounded_homotopy_category A) i).symm.inverse.additive :=
show (category_theory.shift_functor (bounded_homotopy_category A) (i)).additive, by apply_instance

instance shift_equiv_inverse_additive (i : â„¤) :
  (shift_equiv (bounded_homotopy_category A) i).inverse.additive :=
show (category_theory.shift_functor (bounded_homotopy_category A) (-i)).additive, by apply_instance

def hom_shift_right_iso (X : ğ’¦) (i : â„¤) :
  category_theory.shift_functor ğ’¦ i â‹™ preadditive_yoneda.flip.obj (opposite.op X) â‰…
  preadditive_yoneda.flip.obj (opposite.op (XâŸ¦-iâŸ§)) :=
begin
  have := (iso_whisker_right ((shift_equiv (bounded_homotopy_category A) i).symm
  .to_adjunction).preadditive_yoneda_whiskering_left.symm
    ((evaluation _ _).obj $ opposite.op X) : _),
  exact this,
end

def hom_shift_left_iso (X : ğ’¦) (i : â„¤) :
  (category_theory.shift_functor ğ’¦ i).op â‹™ preadditive_yoneda.obj X â‰…
  preadditive_yoneda.obj (XâŸ¦-iâŸ§) :=
begin
  have := (shift_equiv (bounded_homotopy_category A) i)
  .to_adjunction.preadditive_yoneda_whiskering_left.app X,
  exact this,
end

-- The LES for Ext in the second variable.
instance (i : â„¤) (X : ğ’¦) [enough_projectives A] : homological_functor ((Ext i).obj (opposite.op X)) :=
begin
  show homological_functor (category_theory.shift_functor ğ’¦ i â‹™ preadditive_yoneda.flip.obj _),
  let E := hom_shift_right_iso X.replace i,
  exact homological_of_nat_iso _ _ E.symm,
end

-- The LES for Ext in the first variable.
-- We need K-projective replacements of triangles for this.
instance (i : â„¤) (X : ğ’¦) [enough_projectives A] : homological_functor ((Ext i).flip.obj X).right_op :=
begin
  constructor,
  intros T hT,
  have := homological_functor.cond
    (preadditive_yoneda.obj (XâŸ¦iâŸ§)).right_op
    (replace_triangle T)
    (distinguished_replace_triangle _ hT),
  exact this,
end

instance lift_is_iso
  [enough_projectives A] (X Y X' Y' : ğ’¦)
  (f : X âŸ¶ Y) (Ï€X : X' âŸ¶ X) (Ï€Y : Y' âŸ¶ Y)
  [homotopy_category.is_quasi_iso f]
  [homotopy_category.is_quasi_iso Ï€X]
  [homotopy_category.is_quasi_iso Ï€Y]
  [homotopy_category.is_K_projective X'.val]
  [homotopy_category.is_K_projective Y'.val] :
  is_iso (lift (Ï€X â‰« f) Ï€Y) :=
begin
  use lift Ï€Y (Ï€X â‰« f),
  split,
  { apply lift_ext (Ï€X â‰« f), simp, apply_instance },
  { apply lift_ext Ï€Y, simp, apply_instance }
end

@[simp]
lemma inv_lift
  [enough_projectives A] (X Y X' Y' : ğ’¦)
  (f : X âŸ¶ Y) (Ï€X : X' âŸ¶ X) (Ï€Y : Y' âŸ¶ Y)
  [homotopy_category.is_quasi_iso f]
  [homotopy_category.is_quasi_iso Ï€X]
  [homotopy_category.is_quasi_iso Ï€Y]
  [homotopy_category.is_K_projective X'.val]
  [homotopy_category.is_K_projective Y'.val] :
  inv (lift (Ï€X â‰« f) Ï€Y) = lift Ï€Y (Ï€X â‰« f) :=
begin
  apply lift_unique, rw is_iso.inv_comp_eq, simp,
end

instance is_iso_Ext_flip_obj_map_of_is_quasi_iso [enough_projectives A] (i : â„¤)
  (X X' Y : ğ’¦)
  (f : X âŸ¶ X') [homotopy_category.is_quasi_iso f] :
  is_iso (((Ext i).flip.obj Y).map f.op) :=
begin
  let e := (preadditive_yoneda.obj (YâŸ¦iâŸ§)).map (lift (X.Ï€ â‰« f) X'.Ï€).op,
  change is_iso e,
  apply functor.map_is_iso,
end

end bounded_homotopy_category

variable [enough_projectives A]

def Ext' (i : â„¤) : Aáµ’áµ– â¥¤ A â¥¤ Ab :=
(bounded_homotopy_category.single A 0).op â‹™
  (bounded_homotopy_category.single A 0 â‹™ (bounded_homotopy_category.Ext i).flip).flip

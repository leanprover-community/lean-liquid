import for_mathlib.derived.K_projective
import for_mathlib.complex_extend
import for_mathlib.projectives
import for_mathlib.two_step_resolution
import data.zmod.basic

.

universes v u

open category_theory homotopy_category

variables {C : Type u} {Œπ : Type*} [category.{v} C] [abelian C] {c : complex_shape Œπ}

open_locale zero_object

instance projective_zero : projective (0 : C) :=
{ factors := Œª E X f e he, ‚ü®0, by ext‚ü© }

lemma is_zero_homology_of_exact {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (hfg : exact f g) :
  is_zero (homology f g hfg.w) :=
begin
  rw preadditive.exact_iff_homology_zero at hfg,
  rcases hfg with ‚ü®w, ‚ü®e‚ü©‚ü©,
  exact is_zero_of_iso_of_zero (is_zero_zero _) e.symm,
end

lemma category_theory.is_zero.exact {X Y Z : C} (hY : is_zero Y)
  (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : exact f g :=
by simp only [abelian.exact_iff, hY.eq_zero_of_tgt f, hY.eq_zero_of_tgt (limits.kernel.Œπ g),
    limits.zero_comp, eq_self_iff_true, and_true]

lemma category_theory.is_zero.homology_is_zero {X Y Z : C} (hY : is_zero Y)
  (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (w : f ‚â´ g = 0) :
  is_zero (homology f g w) :=
is_zero_homology_of_exact f g $ hY.exact f g

lemma category_theory.is_zero.is_iso {X Y : C} (hX : is_zero X) (hY : is_zero Y) (f : X ‚ü∂ Y) :
  is_iso f :=
{ out := ‚ü®0, hX.eq_of_src _ _, hY.eq_of_tgt _ _‚ü© }

lemma category_theory.is_iso_of_nat_iso {C D : Type*} [category C] [category D]
  {F G : C ‚•§ D} (Œ± : F ‚âÖ G)
  {X Y : C} (f : X ‚ü∂ Y) (h : is_iso (F.map f)) :
  is_iso (G.map f) :=
begin
  rw ‚Üê nat_iso.naturality_1 Œ± f,
  apply_instance,
end

lemma category_theory.nat_iso.is_iso_iff {C D : Type*} [category C] [category D]
  {F G : C ‚•§ D} (Œ± : F ‚âÖ G)
  {X Y : C} (f : X ‚ü∂ Y) :
  is_iso (F.map f) ‚Üî is_iso (G.map f) :=
‚ü®category_theory.is_iso_of_nat_iso Œ± _, category_theory.is_iso_of_nat_iso Œ±.symm _‚ü©

open category_theory

@[simps obj obj_val map]
noncomputable def chain_complex.to_bounded_homotopy_category :
  chain_complex C ‚Ñï ‚•§ bounded_homotopy_category C :=
{ obj := Œª P,
  { val := (homological_complex.embed (complex_shape.embedding.nat_down_int_up) ‚ãô
      homotopy_category.quotient C _).obj P,
    bdd := begin
      refine ‚ü®‚ü®1, _‚ü©‚ü©,
      rintro ((_|i)|i) h,
      { exfalso, revert h, dec_trivial },
      { exact is_zero_zero _ },
      { exfalso, revert h, dec_trivial }
    end },
  map := Œª P Q f, (homological_complex.embed (complex_shape.embedding.nat_down_int_up) ‚ãô
      homotopy_category.quotient C _).map f,
  map_id' := Œª P, (homological_complex.embed (complex_shape.embedding.nat_down_int_up) ‚ãô
      homotopy_category.quotient C _).map_id P,
  map_comp' := Œª P Q R f g, (homological_complex.embed (complex_shape.embedding.nat_down_int_up) ‚ãô
      homotopy_category.quotient C _).map_comp f g }

lemma chain_complex.to_bounded_homotopy_category.is_K_projective [enough_projectives C]
  (P : chain_complex C ‚Ñï) (A : C) (œÄ : P ‚ü∂ (chain_complex.single‚ÇÄ C).obj A)
  (hP : P.is_projective_resolution A œÄ) :
  is_K_projective (chain_complex.to_bounded_homotopy_category.obj P).val :=
begin
  constructor,
  introsI Y hY f,
  rw [‚Üê quotient_map_out f],
  apply eq_of_homotopy,
  refine projective.null_homotopic_of_projective_to_acyclic _ 1 _ _ hY.cond,
  { rintro ((_|i)|i),
    { exact hP.projective 0 },
    { exact projective_zero, },
    { exact hP.projective _ }, },
  { rintro ((_|i)|i),
    { intro h, exfalso, revert h, dec_trivial },
    { intro h, exact is_zero_zero _ },
    { intro h, exfalso, revert h, dec_trivial } },
end
.

noncomputable
def homological_complex.homology_functor_single [decidable_eq Œπ] (i : Œπ) :
  homological_complex.single C c i ‚ãô homology_functor C c i ‚âÖ ùü≠ C :=
begin
  refine nat_iso.of_components _ _,
  { intro X,
    refine homology.congr _ _ _ _ ‚â™‚â´ homology_zero_zero ‚â™‚â´ _,
    { delta homological_complex.d_to,
      rcases c.prev i with (_|‚ü®_, _‚ü©),
      { dsimp, rw if_pos rfl },
      { dsimp, rw limits.comp_zero } },
    { delta homological_complex.d_from,
      rcases c.next i with (_|‚ü®_, _‚ü©),
      { dsimp, rw if_pos rfl },
      { dsimp, rw limits.zero_comp } },
    { exact eq_to_iso (if_pos rfl) } },
  { intros X Y f,
    apply homology.ext,
    dsimp,
    simpa only [category.comp_id, homological_complex.hom.sq_from_left, eq_to_hom_refl,
      homological_complex.single_obj_X_self_hom, homology.map_desc_assoc, eq_to_hom_trans,
      homological_complex.single_obj_X_self_inv, homological_complex.single_map_f_self,
      category.assoc, limits.kernel_subobject_map_arrow, homology.œÄ_desc_assoc] }
end

instance {X Y Z : C} (f: X ‚ü∂ Y) : is_iso (homology.Œπ f (0 : Y ‚ü∂ Z) limits.comp_zero) :=
begin
  suffices : limits.cokernel.desc f 0 limits.comp_zero = 0,
  { exact @@is_iso.comp_is_iso _ _ (show _, by convert limits.kernel.Œπ_zero_is_iso) },
  ext,
  simp,
end

lemma homology.desc_zero_is_iso_of_exact_of_epi {X Y Z W : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
  (h : exact f g) [epi g] : is_iso (homology.desc' f (0 : Y ‚ü∂ W) limits.comp_zero
    (limits.kernel_zero_iso_source.hom ‚â´ g) (by simp [h.w])) :=
begin
  convert_to is_iso (homology.Œπ _ _ _ ‚â´
    (limits.colimit.iso_colimit_cocone ‚ü®_, abelian.is_colimit_of_exact_of_epi f g h‚ü©).hom),
  { ext, simp },
  { apply_instance }
end

lemma chain_complex.is_projective_resolution.is_quasi_iso_embed {P : chain_complex C ‚Ñï}
  {A : C} {œÄ : P ‚ü∂ (chain_complex.single‚ÇÄ C).obj A}
  (hP : P.is_projective_resolution A œÄ) :
  is_quasi_iso ((homological_complex.embed (complex_shape.embedding.nat_down_int_up) ‚ãô
       homotopy_category.quotient C _).map œÄ) :=
begin
  constructor,
  intro i,
  rw [‚Üê functor.comp_map, category_theory.nat_iso.is_iso_iff
    (functor.associator _ _ _ ‚â™‚â´ iso_whisker_left _ (homology_factors _ _ _))],
  rcases i with ((_|i)|i),
  { apply_with is_iso.of_is_iso_comp_right { instances := ff },
    show is_iso (functor.map_iso _ (chain_complex.single‚ÇÄ_comp_embed_iso_single.app A) ‚â™‚â´
        ((homological_complex.homology_functor_single (0 : ‚Ñ§)).app A : _)).hom, by apply_instance,
    apply_with is_iso.of_is_iso_comp_left { instances := ff },
    swap 2, convert @@homology.desc_zero_is_iso_of_exact_of_epi _ _ _ _ hP.exact‚ÇÄ hP.epi using 1,
    swap 4,
    { refine (homology.map_iso _ _ (arrow.iso_mk _ _ _)
        (arrow.iso_mk _ (by exact iso.refl _) _) rfl).hom,
      { exact ((homological_complex.embed.obj complex_shape.embedding.nat_down_int_up P).X_prev_iso
          (show (complex_shape.up ‚Ñ§).rel (-[1+0]) 0, from neg_add_self 1)).symm },
      { exact iso.refl _ },
      { dsimp,
        rw [eq_comm, iso.eq_inv_comp, category.comp_id, eq_comm],
        apply homological_complex.d_to_eq },
      { dsimp,
        rw [category.comp_id, category.id_comp],
        rw homological_complex.d_from_eq _ (show (complex_shape.up ‚Ñ§).rel 0 1, from rfl),
        exact limits.zero_comp } },
    { ext,
      dsimp [homology.map_iso, homological_complex.homology_functor_single],
      rw [‚Üê cancel_epi (limits.kernel_subobject_iso _).hom, homology.œÄ'_eq_œÄ_assoc],
      simp only [homology.œÄ'_desc', category.comp_id, homological_complex.hom.sq_from_left,
        limits.kernel_subobject_arrow_assoc, homology.œÄ_desc, homology.map_desc,
        limits.kernel_subobject_map_arrow_assoc, arrow.iso_mk_hom_left,
        limits.kernel_subobject_map_arrow],
      dsimp [chain_complex.single‚ÇÄ_comp_embed_iso_single,
        chain_complex.single‚ÇÄ_comp_embed_iso_single_component],
      simpa,
      apply_instance },
    { apply_instance } },
  { refine is_zero.is_iso _ _ _; refine is_zero_homology_of_exact _ _ (exact_of_zero _ _), },
  { refine is_zero.is_iso _ _ _,
    { refine is_zero_of_iso_of_zero _ (homology_iso _ (-[1+i.succ] : ‚Ñ§) _ (-i : ‚Ñ§) _ _).symm,
      rotate,
      { dsimp, refl, },
      { dsimp, simp only [int.neg_succ_of_nat_eq', sub_add_cancel], },
      refine is_zero_homology_of_exact _ _ _,
      cases i; exact (hP.exact _), },
    { refine is_zero_of_iso_of_zero _ (homology_iso _ (-[1+i.succ] : ‚Ñ§) _ (-i : ‚Ñ§) _ _).symm,
      rotate,
      { dsimp, refl, },
      { dsimp, simp only [int.neg_succ_of_nat_eq', sub_add_cancel], },
      refine is_zero_homology_of_exact _ _ (exact_of_zero _ _), } }
end
.

open opposite

namespace homological_complex

@[simps]
def op (X : homological_complex C c) : homological_complex C·µí·µñ c.symm :=
{ X := Œª i, op (X.X i),
  d := Œª i j, (X.d j i).op,
  shape' := Œª i j hij, by { rw [X.shape j i hij, op_zero], },
  d_comp_d' := by { intros, rw [‚Üê op_comp, X.d_comp_d, op_zero], } }

@[simps]
def unop (X : homological_complex C·µí·µñ c) : homological_complex C c.symm :=
{ X := Œª i, unop (X.X i),
  d := Œª i j, (X.d j i).unop,
  shape' := Œª i j hij, by { rw [X.shape j i hij, unop_zero], },
  d_comp_d' := by { intros, rw [‚Üê unop_comp, X.d_comp_d, unop_zero], } }

end homological_complex

@[to_additive]
lemma subgroup.comap_eq_iff {A B : Type*} [comm_group A] [comm_group B] (f : A ‚âÉ* B)
  (C : subgroup A) (D : subgroup B) :
  D.comap f.to_monoid_hom = C ‚Üî C.comap f.symm.to_monoid_hom = D :=
begin
  split; rintro rfl; ext,
  { simp only [subgroup.mem_comap, mul_equiv.coe_to_monoid_hom, mul_equiv.apply_symm_apply], },
  { simp only [subgroup.mem_comap, mul_equiv.coe_to_monoid_hom, mul_equiv.symm_apply_apply], }
end

@[to_additive] noncomputable
def mul_equiv.surjective_congr {A B C D : Type*}
  [comm_group A] [comm_group B] [comm_group C] [comm_group D]
  (e : A ‚âÉ* B) (f : A ‚Üí* C) (g : B ‚Üí* D)
  (hf : function.surjective f) (hg : function.surjective g)
  (he : g.ker.comap e.to_monoid_hom = f.ker) :
C ‚âÉ* D :=
{ to_fun := f.lift_of_surjective hf ‚ü®g.comp e.to_monoid_hom, Œª x hx,
  by { rw ‚Üê he at hx, exact hx }‚ü©,
  inv_fun := g.lift_of_surjective hg ‚ü®f.comp e.symm.to_monoid_hom, Œª x hx,
  by { rw subgroup.comap_eq_iff at he, rw ‚Üê he at hx, exact hx, }‚ü©,
  left_inv := Œª x, begin
    obtain ‚ü®x, rfl‚ü© := hf x,
    delta monoid_hom.lift_of_surjective,
    simp only [monoid_hom.lift_of_right_inverse_comp_apply, subtype.coe_mk, monoid_hom.comp_apply],
    convert monoid_hom.lift_of_right_inverse_comp_apply _ _ _ _ (e.to_monoid_hom x) using 1,
    exact f.congr_arg (e.symm_apply_apply x).symm,
  end,
  right_inv := Œª x, begin
    obtain ‚ü®x, rfl‚ü© := hg x,
    delta monoid_hom.lift_of_surjective,
    simp only [monoid_hom.lift_of_right_inverse_comp_apply, subtype.coe_mk, monoid_hom.comp_apply],
    convert monoid_hom.lift_of_right_inverse_comp_apply _ _ _ _ (e.symm.to_monoid_hom x) using 1,
    exact g.congr_arg (e.apply_symm_apply x).symm,
  end,
  map_mul' := Œª x y, monoid_hom.map_mul _ _ _ }

@[to_additive]
def mul_equiv.quotient_congr {A B : Type*} [comm_group A] [comm_group B] (f : A ‚âÉ* B)
  (C : subgroup A) (D : subgroup B) (h : D.comap f.to_monoid_hom = C) :
A ‚ß∏ C ‚âÉ* B ‚ß∏ D :=
{ to_fun := quotient_group.map _ _ f.to_monoid_hom h.ge,
  inv_fun := quotient_group.map _ _ f.symm.to_monoid_hom
  begin
    refine le_of_eq _, subst h, ext,
    simp only [subgroup.mem_comap, mul_equiv.coe_to_monoid_hom, mul_equiv.apply_symm_apply],
  end,
  left_inv := Œª x, begin
    induction x using quotient_group.induction_on,
    erw [quotient_group.map_coe, f.symm_apply_apply],
    refl,
  end,
  right_inv := Œª x, begin
    induction x using quotient_group.induction_on,
    erw [quotient_group.map_coe, f.apply_symm_apply],
    refl,
  end,
  map_mul' := Œª x y, monoid_hom.map_mul _ _ _ }
.

attribute [elementwise] iso.hom_inv_id

protected noncomputable
def AddCommGroup.homology_iso {A B C : AddCommGroup.{u}} (f : A ‚ü∂ B) (g : B ‚ü∂ C) (w : f ‚â´ g = 0) :
  homology f g w ‚âÖ AddCommGroup.of (g.ker ‚ß∏ (f.range.comap g.ker.subtype)) :=
begin
  refine homology_iso_cokernel_lift f g w ‚â™‚â´
    AddCommGroup.cokernel_iso_quotient _ ‚â™‚â´
    add_equiv_iso_AddCommGroup_iso.hom _,
  refine add_equiv.quotient_congr _ _ _ _,
  { exact add_equiv_iso_AddCommGroup_iso.inv (AddCommGroup.kernel_iso_ker _) },
  rw add_subgroup.comap_eq_iff,
  show add_subgroup.comap (AddCommGroup.kernel_iso_ker g).inv
    (add_monoid_hom.range (limits.kernel.lift g f w)) =
    add_subgroup.comap (add_monoid_hom.ker g).subtype (add_monoid_hom.range f),
  dsimp only [AddCommGroup.kernel_iso_ker],
  have : function.injective (Œª x, limits.kernel.Œπ g x),
  { rw [‚Üê AddCommGroup.kernel_iso_ker_hom_comp_subtype, coe_comp],
    have : function.injective (g.ker.subtype) := subtype.val_injective,
    refine this.comp _,
    refine function.has_left_inverse.injective _,
    refine ‚ü®(AddCommGroup.kernel_iso_ker _).inv, _‚ü©,
    intro y, refine category_theory.iso.hom_inv_id_apply (AddCommGroup.kernel_iso_ker g) y, },
  ext ‚ü®x, hx‚ü©,
  simp only [add_subgroup.mem_comap, add_monoid_hom.mem_range, add_subgroup.coe_subtype,
    subtype.coe_mk, ‚Üê this.eq_iff, category_theory.limits.kernel.lift_Œπ_apply],
end
.

def homotopy_category.quotient_map_hom (A B : homological_complex C c) :
  (A ‚ü∂ B) ‚Üí+ ((homotopy_category.quotient C c).obj A ‚ü∂ (homotopy_category.quotient C c).obj B) :=
add_monoid_hom.mk' (Œª f, (homotopy_category.quotient C c).map f) $ Œª f g, rfl

lemma quot.mk_surjective {X : Type*} (r : X ‚Üí X ‚Üí Prop) :
  function.surjective (quot.mk r) :=
Œª x, quot.induction_on x $ Œª x, ‚ü®x, rfl‚ü©

open category_theory.limits

@[simp]
lemma _root_.category_theory.equivalence.symm_to_adjunction_counit {C D : Type*} [category C]
  [category D] (e : C ‚âå D) : e.symm.to_adjunction.counit = e.unit_inv := rfl

@[simp]
lemma _root_.homological_complex.shift_equiv_unit_app (i j : ‚Ñ§) (X : cochain_complex C ‚Ñ§) :
  homological_complex.hom.f ((shift_equiv _ i).unit.app X) j = (X.X_eq_to_iso $ by simp).hom :=
begin
  dsimp [shift_equiv, unit_of_tensor_iso_unit],
  simp [homological_complex.X_eq_to_iso],
end

@[simp]
lemma _root_.homological_complex.shift_equiv_unit_inv_app (i j : ‚Ñ§) (X : cochain_complex C ‚Ñ§) :
  homological_complex.hom.f ((shift_equiv _ i).unit_inv.app X) j = (X.X_eq_to_iso $ by simp).hom :=
begin
  dsimp [shift_equiv, unit_of_tensor_iso_unit],
  simp [homological_complex.X_eq_to_iso],
end
@[simp]
lemma _root_.category_theory.equivalence.symm_to_adjunction_unit {C D : Type*} [category C]
  [category D] (e : C ‚âå D) : e.symm.to_adjunction.unit = e.counit_inv := rfl

@[simp]
lemma _root_.homological_complex.shift_equiv_counit_app (i j : ‚Ñ§) (X : cochain_complex C ‚Ñ§) :
  homological_complex.hom.f ((shift_equiv _ i).counit.app X) j = (X.X_eq_to_iso $ by simp).hom :=
begin
  dsimp [shift_equiv, unit_of_tensor_iso_unit],
  simpa [homological_complex.X_eq_to_iso],
end

@[simp]
lemma _root_.homological_complex.shift_equiv_counit_inv_app (i j : ‚Ñ§) (X : cochain_complex C ‚Ñ§) :
  homological_complex.hom.f ((shift_equiv _ i).counit_inv.app X) j = (X.X_eq_to_iso $ by simp).hom :=
begin
  dsimp [shift_equiv, unit_of_tensor_iso_unit],
  simpa [homological_complex.X_eq_to_iso],
end

noncomputable
def homotopy.to_single [decidable_eq Œπ] [decidable_rel c.rel] {X : homological_complex C c} {B : C}
  {i j : Œπ} (r : c.rel i j)
  (f g : X ‚ü∂ (homological_complex.single C c i).obj B) (h : X.X j ‚ü∂ B)
  (H : f.f i = X.d i j ‚â´ h ‚â´ eq_to_hom (if_pos rfl).symm + g.f i) :
  homotopy f g :=
{ hom := Œª i‚ÇÅ i‚ÇÇ, if r' : c.rel i‚ÇÇ i‚ÇÅ then if e : i‚ÇÇ = i then
    (X.X_eq_to_iso (c.next_eq (e ‚ñ∏ r' : c.rel i i‚ÇÅ) r)).hom ‚â´ h ‚â´ eq_to_hom (if_pos e).symm
    else 0 else 0,
  zero' := Œª _ _ e, dif_neg e,
  comm := Œª k, begin
    dsimp,
    by_cases k = i,
    swap, { apply is_zero.eq_of_tgt, dsimp, rw if_neg h, exact is_zero_zero _ },
    subst h,
    rw [d_next_eq _ r, dif_pos r, dif_pos rfl, H, X.X_eq_to_iso_refl, category.id_comp],
    nth_rewrite_lhs 0 ‚Üê add_monoid.add_zero (X.d k j ‚â´ h ‚â´ eq_to_hom _),
    congr,
    delta prev_d,
    rcases c.prev k with (_|‚ü®i, _‚ü©); dsimp,
    { refl },
    { rw comp_zero, refl }
  end }

lemma homotopic_to_single_iff [decidable_eq Œπ] {X : homological_complex C c}
  {B : C} {i j : Œπ} (r : c.rel i j)
  (f g : X ‚ü∂ (homological_complex.single C c i).obj B) :
  homotopic _ _ f g ‚Üî
    ‚àÉ (h : X.X j ‚ü∂ B), f.f i = X.d i j ‚â´ h ‚â´ eq_to_hom (if_pos rfl).symm + g.f i :=
begin
  haveI : decidable_rel c.rel := Œª _ _, classical.dec _,
  refine ‚ü®_, Œª ‚ü®h, H‚ü©, ‚ü®homotopy.to_single r f g h H‚ü©‚ü©,
  rintro ‚ü®h‚ü©,
  use h.hom j i ‚â´ eq_to_hom (if_pos rfl),
  rw [category.assoc, eq_to_hom_trans, eq_to_hom_refl, category.comp_id, ‚Üê add_zero (_ ‚â´ _)],
  have := h.comm i,
  rw [d_next_eq _ r] at this,
  convert this,
  delta prev_d,
  rcases c.prev i with (_|‚ü®j, _‚ü©); dsimp; simp
end

instance : decidable_rel (complex_shape.up ‚Ñ§).rel :=
Œª i j, show decidable (i + 1 = j), by apply_instance

@[simps] noncomputable
def homological_complex.hom_single_iso
  (P : cochain_complex C ‚Ñ§) (B : C) (i : ‚Ñ§) :
  (P ‚ü∂ (homological_complex.single C (complex_shape.up ‚Ñ§) i).obj B) ‚âÉ+
    (add_monoid_hom.ker ((((preadditive_yoneda.obj B).map_homological_complex
      (complex_shape.up ‚Ñ§).symm).obj P.op).d i (i - 1))) :=
{ to_fun := Œª f, begin
    refine ‚ü®f.f i ‚â´ eq_to_hom (if_pos rfl), _‚ü©,
    change P.d (i - 1) i ‚â´ f.f i ‚â´ eq_to_hom _ = 0,
    rw ‚Üê f.comm_assoc,
    dsimp,
    rw [zero_comp, comp_zero],
  end,
  inv_fun := Œª f, begin
    refine ‚ü®Œª j, if e : j = i then
      (P.X_eq_to_iso $ e).hom ‚â´ f.1 ‚â´ eq_to_hom (if_pos e).symm else 0, _‚ü©,
    rintros j k (rfl : j + 1 = k),
    dsimp,
    rw comp_zero,
    split_ifs,
    { have := eq_sub_iff_add_eq.mpr h, subst this,
      rw [P.X_d_eq_to_iso_assoc, ‚Üê category.assoc, ‚Üê subtype.val_eq_coe,
        show P.d (i - 1) i ‚â´ f.1 = 0, from f.2, zero_comp] },
    { exact comp_zero.symm }
  end,
  left_inv := begin
    intro f,
    ext j,
    dsimp,
    split_ifs,
    { subst h, simp },
    { apply is_zero.eq_of_tgt, rw if_neg h, exact is_zero_zero _ }
  end,
  right_inv := Œª f, by { ext, dsimp, simp },
  map_add' := Œª f g, subtype.ext (preadditive.add_comp _ _ _ _ _ _) }
.
noncomputable
def bounded_homotopy_category.hom_single_iso
  (P : bounded_homotopy_category C) (B : C) (i : ‚Ñ§) :
  AddCommGroup.of (P ‚ü∂ (bounded_homotopy_category.single C i).obj B) ‚âÖ
  (((preadditive_yoneda.obj B).map_homological_complex _).obj P.val.as.op).homology i :=
begin
  refine _ ‚â™‚â´ (homology_iso _ (i+1) i (i-1) _ _).symm,
  rotate, { dsimp, refl }, { dsimp, exact sub_add_cancel _ _ },
  refine add_equiv_iso_AddCommGroup_iso.hom _ ‚â™‚â´ (AddCommGroup.homology_iso _ _ _).symm,
  refine add_equiv.surjective_congr (homological_complex.hom_single_iso P.val.as B i)
    (homotopy_category.quotient_map_hom _ _)
    (quotient_add_group.mk' _) (quot.mk_surjective _) (quot.mk_surjective _) _,
  ext f,
  dsimp,
  simp only [homotopy_category.quotient_map_hom, quotient_add_group.ker_mk,
    add_equiv.coe_to_add_monoid_hom, add_monoid_hom.mem_ker, add_subgroup.mem_comap,
    add_subgroup.coe_subtype, add_monoid_hom.mk'_apply, add_subgroup.coe_mk,
    add_equiv.coe_mk, add_monoid_hom.mem_range],
  rw ‚Üê (homotopy_category.quotient _ _).map_zero,
  any_goals { apply_instance },
  erw quotient.functor_map_eq_iff,
  rw homotopic_to_single_iff (show (complex_shape.up ‚Ñ§).rel i (i+1), from rfl),
  apply exists_congr,
  intro g,
  simp only [add_zero, quiver.hom.unop_op, linear_map.to_add_monoid_hom_coe,
    preadditive_yoneda_obj_map_apply, homological_complex.zero_f_apply,
    homological_complex.hom_single_iso_apply_coe],
  rw [‚Üê is_iso.comp_inv_eq, inv_eq_to_hom, eq_comm, category.assoc],
end

variable [enough_projectives C]

noncomputable
def Ext'_iso (A : C·µí·µñ) (B : C) (i : ‚Ñ§) (P : chain_complex C ‚Ñï)
  (œÄ : P ‚ü∂ (chain_complex.single‚ÇÄ C).obj A.unop)
  (hP : P.is_projective_resolution A.unop œÄ) :
  ((Ext' i).obj A).obj B ‚âÖ
  (((preadditive_yoneda.obj B).map_homological_complex _).obj
    ((homological_complex.embed complex_shape.embedding.nat_down_int_up).obj P).op).homology (-i) :=
begin
  dsimp only [Ext', functor.comp_obj, functor.op_obj, functor.flip_obj_obj],
  haveI := chain_complex.to_bounded_homotopy_category.is_K_projective P A.unop œÄ hP,
  haveI : is_quasi_iso (chain_complex.to_bounded_homotopy_category.map œÄ) := hP.is_quasi_iso_embed,
  refine (bounded_homotopy_category.Ext_iso i
    (chain_complex.to_bounded_homotopy_category.obj P)
    _ _ (chain_complex.to_bounded_homotopy_category.map œÄ ‚â´ _)) ‚â™‚â´
    (preadditive_yoneda.map_iso _).app (op (chain_complex.to_bounded_homotopy_category.obj P)) ‚â™‚â´
      bounded_homotopy_category.hom_single_iso _ B (-i),
  { exact ((homotopy_category.quotient _ _).map_iso $
      (chain_complex.single‚ÇÄ_comp_embed_iso_single).app A.unop).hom, },
  { apply_instance },
  { exact (bounded_homotopy_category.shift_single_iso 0 i).app B ‚â™‚â´ eq_to_iso (by rw zero_sub) }
end

lemma AddCommGroup.is_zero_of_eq (A : AddCommGroup) (h : ‚àÄ x y : A, x = y) :
  is_zero A :=
{ eq_zero_of_src := Œª B f, by { ext, cases h x 0, exact f.map_zero },
  eq_zero_of_tgt := Œª B f, by { ext, exact h _ _ } }

lemma category_theory.ProjectiveResolution.is_projective_resolution
  {A : C} (P : ProjectiveResolution A) :
  P.complex.is_projective_resolution _ P.œÄ :=
{ projective := P.projective,
  exact‚ÇÄ := P.exact‚ÇÄ,
  exact := P.exact,
  epi := ProjectiveResolution.f.category_theory.epi P 0 }

lemma Ext_is_zero_of_neg (A : C·µí·µñ) (B : C) (i : ‚Ñ§) (hi : i < 0) :
  is_zero (((Ext' i).obj A).obj B) :=
begin
  let P := ProjectiveResolution.of A.unop,
  refine is_zero_of_iso_of_zero _ (Ext'_iso _ _ i P.complex P.œÄ P.is_projective_resolution).symm,
  rcases i with (i|i),
  { exfalso, revert hi, dec_trivial },
  refine is_zero.homology_is_zero _ _ _ _,
  refine AddCommGroup.is_zero_of_eq _ _,
  intros f g,
  ext,
end

namespace AddCommGroup

-- We only need `G` to preserve epimorphisms, but we don't have such a class.
lemma preserves_projectives {C D : Type*} [category C] [category.{v} D] {F : C ‚•§ D} {G : D ‚•§ C}
  (adj : F ‚ä£ G) [preserves_colimits_of_shape walking_span.{v} G] (P : C) [projective P] :
    projective (F.obj P) :=
begin
  constructor,
  intros,
  resetI,
  use (adj.hom_equiv _ _).symm (projective.factor_thru (adj.hom_equiv _ _ f) (G.map e)),
  rw [‚Üê adj.hom_equiv_naturality_right_symm, projective.factor_thru_comp, equiv.symm_apply_apply],
end

instance {C D : Type*} [category C] [category D] {F : C ‚•§ D}
  {G : D ‚•§ C} (adj : F ‚ä£ G) [faithful G] (X : D) : epi (adj.counit.app X) :=
begin
  haveI : split_epi (G.map (adj.counit.app X)) := ‚ü®_, adj.right_triangle_components‚ü©,
  exact faithful_reflects_epi G infer_instance
end

lemma enough_projectives_of_adjoint {C D : Type*} [category C] [category.{v} D] {F : C ‚•§ D}
  {G : D ‚•§ C} (adj : F ‚ä£ G) [preserves_colimits_of_shape walking_span.{v} G] [faithful G]
  [enough_projectives C] : enough_projectives D :=
begin
  haveI : is_left_adjoint F := ‚ü®_, adj‚ü©,
  constructor,
  intro X,
  refine ‚ü®‚ü®_, preserves_projectives adj _, (adj.hom_equiv _ _).symm (projective.œÄ (G.obj X)), _‚ü©‚ü©,
  dsimp,
  rw adjunction.hom_equiv_counit,
  exact epi_comp _ _,
end

instance : enough_projectives AddCommGroup.{u} :=
enough_projectives_of_adjoint
  (functor.as_equivalence (forget‚ÇÇ (Module ‚Ñ§) AddCommGroup)).to_adjunction

lemma Ext_is_zero_of_one_lt
  (A : AddCommGroup.{u}·µí·µñ) (B : AddCommGroup.{u}) (i : ‚Ñ§) (hi : i > 1) :
  is_zero (((Ext' i).obj A).obj B) :=
begin
  induction A,
  rcases A with ‚ü®A, _Ainst‚ü©, resetI,
  let := Ext'_iso (op $AddCommGroup.of A) B i,
  dsimp at this,
  refine is_zero_of_iso_of_zero _ (this _ _ (two_step_resolution_ab_projective A)).symm,
  rcases i with ((_|_|i)|i),
  { exfalso, revert hi, dec_trivial },
  { exfalso, revert hi, dec_trivial },
  swap,
  { exfalso, revert hi, dec_trivial },
  refine is_zero.homology_is_zero _ _ _ _,
  refine AddCommGroup.is_zero_of_eq _ _,
  intros f g,
  apply category_theory.limits.has_zero_object.from_zero_ext,
end

noncomputable theory
variable (n : ‚Ñï)

def zmod_resolution : chain_complex AddCommGroup ‚Ñï :=
chain_complex.mk' (of ‚Ñ§) (of ‚Ñ§) (n ‚Ä¢ ùüô _) (Œª _, ‚ü®0, 0, zero_comp‚ü©)

example : (zmod_resolution n).X 0 = of ‚Ñ§ := rfl

def zmod_resolution_pi_f :
  Œ† (i : ‚Ñï), (zmod_resolution n).X i ‚ü∂ ((chain_complex.single‚ÇÄ AddCommGroup).obj (of $ zmod n)).X i
| 0     := show of ‚Ñ§ ‚ü∂ of (zmod n), from @int.cast_add_hom _ _ ‚ü®(1 : zmod n)‚ü©
| (i+1) := 0

def zmod_resolution_pi :
  zmod_resolution n ‚ü∂ (chain_complex.single‚ÇÄ AddCommGroup).obj (of $ zmod n) :=
{ f := zmod_resolution_pi_f n,
  comm' := begin
    rintros i ‚ü®_|j‚ü© (rfl : _ = _),
    { ext k, dsimp [zmod_resolution_pi_f, zmod_resolution],
      simp only [zero_apply, fin.coe_zero, comp_apply, int.coe_cast_add_hom],
      simp only [chain_complex.mk'_d_1_0, add_monoid_hom.coe_smul, pi.smul_apply, id_apply,
        nsmul_one, int.nat_cast_eq_coe_nat, int.coe_nat_bit0, int.coe_nat_succ, int.coe_nat_zero,
        zero_add, int.cast_bit0, int.cast_one],
      exact (zmod.nat_cast_self n).symm },
    { exact comp_zero.trans comp_zero.symm }
  end }

instance : projective (AddCommGroup.of ‚Ñ§) :=
preserves_projectives (functor.as_equivalence (forget‚ÇÇ (Module ‚Ñ§) AddCommGroup)).to_adjunction
  (Module.of ‚Ñ§ ‚Ñ§)

lemma exact_zmod_nsmul_cast :
  exact (n ‚Ä¢ ùüô (of ‚Ñ§)) (AddCommGroup.of_hom $ int.cast_add_hom (zmod n)) :=
begin
  rw AddCommGroup.exact_iff,
  erw zmod.ker_int_cast_add_hom,
  ext,
  apply exists_congr,
  rintro (a : ‚Ñ§),
  change n ‚Ä¢ a = x ‚Üî a * (n : ‚Ñ§) = x,
  rw mul_comm,
  norm_num,
end

lemma zmod_resolution_is_resolution (hn : n ‚â† 0) :
  (zmod_resolution n).is_projective_resolution (of (zmod n)) (zmod_resolution_pi n) :=
begin
  constructor,
  { rintro (_|_|_|_),
    { show projective (AddCommGroup.of ‚Ñ§), by apply_instance },
    { show projective (AddCommGroup.of ‚Ñ§), by apply_instance },
    { show projective (0 : AddCommGroup), by apply_instance },
    { show projective (0 : AddCommGroup), by apply_instance } },
  { dsimp [zmod_resolution_pi, zmod_resolution_pi_f, zmod_resolution],
    rw chain_complex.mk'_d_1_0,
    exact AddCommGroup.exact_zmod_nsmul_cast n },
  { intro i,
    dsimp [zmod_resolution, chain_complex.mk', chain_complex.mk, chain_complex.of],
    rw [if_pos rfl, if_pos rfl, category.id_comp, category.id_comp],
    rcases i with (_|_|_),
    { show exact 0 (n ‚Ä¢ ùüô (of ‚Ñ§)),
      rw [(abelian.tfae_mono 0 (n ‚Ä¢ ùüô (of ‚Ñ§))).out 2 0, AddCommGroup.mono_iff_injective],
      rintros (x : ‚Ñ§) (y : ‚Ñ§) (e : n ‚Ä¢ x = n ‚Ä¢ y),
      norm_num at e,
      exact e.resolve_right hn },
    { show exact 0 0, apply exact_of_zero },
    { show exact 0 0, apply exact_of_zero } },
  { dsimp [zmod_resolution_pi, zmod_resolution_pi_f],
    rw AddCommGroup.epi_iff_surjective,
    rintro (x : zmod n),
    exact ‚ü®(x : ‚Ñ§), by norm_num‚ü© }
end

lemma zmod.nsmul_eq_zero (k : zmod n) : n ‚Ä¢ k = 0 := by norm_num

@[simps] def add_subgroup.equiv_top (A : Type*) [add_comm_group A] :
  A ‚âÉ+ (‚ä§ : add_subgroup A) :=
{ to_fun := Œª x, ‚ü®x, add_subgroup.mem_top _‚ü©,
  inv_fun := Œª x, x,
  left_inv := Œª x, rfl,
  right_inv := by { rintro ‚ü®x, hx‚ü©, refl },
  map_add' := Œª x y, rfl }

def Ext_zmod (hn : n ‚â† 0) :
  ((Ext' 1).obj (op $ of $ zmod n)).obj (of $ zmod n) ‚âÖ of (zmod n) :=
begin
  refine Ext'_iso (op $ of $ zmod n) (of $ zmod n) 1 (zmod_resolution n) (zmod_resolution_pi n)
    (zmod_resolution_is_resolution n hn) ‚â™‚â´
      (homology_iso _ 0 (-1) (-2) rfl rfl) ‚â™‚â´ _,
  refine (AddCommGroup.homology_iso _ _ _) ‚â™‚â´ _,
  refine add_equiv_iso_AddCommGroup_iso.hom _,
  refine add_equiv.surjective_congr _ (quotient_add_group.mk' _) (add_monoid_hom.id _)
    (quot.mk_surjective _) function.surjective_id _,
  refine (add_equiv.add_subgroup_congr _).trans _,
  { exact ‚ä§ },
  { convert add_monoid_hom.ker_zero using 2,
    refine is_zero.eq_of_tgt _ _ _,
    refine AddCommGroup.is_zero_of_eq _ _,
    intros f g,
    apply category_theory.limits.has_zero_object.from_zero_ext, },
  { refine (add_subgroup.equiv_top _).symm.trans (zmultiples_add_hom _).symm, },
  { simp only [add_monoid_hom.ker_zero, quotient_add_group.ker_mk,
     functor.map_homological_complex_obj_d, homological_complex.op_d],
    ext ‚ü®f, hf‚ü©,
    simp only [add_subgroup.mem_comap, add_equiv.coe_to_add_monoid_hom, add_equiv.coe_trans,
      function.comp_app, zmultiples_add_hom_symm_apply, add_subgroup.coe_subtype,
      add_subgroup.coe_mk, add_monoid_hom.mem_range],
    simp only [add_subgroup.equiv_top_symm_apply, add_monoid_hom.mem_ker],
    dsimp [add_equiv.add_subgroup_congr, zmod_resolution],
    split,
    { intro hf1, refine ‚ü®0, comp_zero.trans _‚ü©, ext1, exact hf1.symm },
    { intro H, cases H with g hg, rw [‚Üê hg, coe_comp],
      convert g.map_nsmul _ _ using 1,
      simp only [eq_to_hom_refl, id_apply, zmod.nsmul_eq_zero] } }
end


end AddCommGroup

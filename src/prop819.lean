import algebra.homology.homological_complex
import topology.category.Profinite.cofiltered_limit

import for_mathlib.Cech.split
import for_mathlib.Profinite.arrow_limit
import for_mathlib.Profinite.clopen_limit
import for_mathlib.simplicial.complex

import locally_constant.Vhat
import prop819.completion
--import prop819.locally_constant

open_locale nnreal

noncomputable theory

open category_theory opposite
open SemiNormedGroup

universes u

-- We have a surjective morphism of profinite sets.
variables (F : arrow Profinite.{u}) (surj : function.surjective F.hom)
variables (M : SemiNormedGroup.{u})

/-- The cochain complex built out of the cosimplicial object obtained by applying
  `LocallyConstant.obj M` to the augmented Cech nerve of `F`. -/
abbreviation FL : cochain_complex SemiNormedGroup ‚Ñï :=
  (((cosimplicial_object.augmented.whiskering _ _).obj (LocallyConstant.{u u}.obj M)).obj
  F.augmented_cech_nerve.right_op).to_cocomplex

/-- The cochain complex built out of the cosimplicial object obtained by applying
  `LCC.obj M` to the augmented Cech nerve of `F`. -/
abbreviation FLC : cochain_complex SemiNormedGroup ‚Ñï :=
  (((cosimplicial_object.augmented.whiskering _ _).obj (LCC.{u u}.obj M)).obj
  F.augmented_cech_nerve.right_op).to_cocomplex

--def Rop : (simplicial_object.augmented Profinite)·µí·µñ ‚•§ cosimplicial_object.augmented Profinite·µí·µñ :=
--{ obj := Œª X, X.unop.right_op,
--  map := Œª X Y f,
--  { left := quiver.hom.op (comma_morphism.right f.unop),
--    right := nat_trans.right_op (comma_morphism.left f.unop),
--    w' := by { ext, exact congr_arg (Œª Œ∑, (nat_trans.app Œ∑ (op x)).op) f.unop.w.symm, } } }

/-- A functorial version of `FL`. -/
def FL_functor : (arrow Profinite.{u})·µí·µñ ‚•§ cochain_complex SemiNormedGroup ‚Ñï :=
simplicial_object.augmented_cech_nerve.op ‚ãô
simplicial_to_cosimplicial_augmented _ ‚ãô
(cosimplicial_object.augmented.whiskering _ _).obj (LocallyConstant.obj M) ‚ãô
cosimplicial_object.augmented.cocomplex

/-- The functor sending an augmented cosimplicial object `X` to
  the cochain complex associated to the composition of `X` with `LCC.obj M`. -/
@[simps obj map]
def FLC_functor' : (simplicial_object.augmented Profinite.{u})·µí·µñ ‚•§ cochain_complex SemiNormedGroup ‚Ñï :=
simplicial_to_cosimplicial_augmented _ ‚ãô
  (cosimplicial_object.augmented.whiskering _ _).obj (SemiNormedGroup.LCC.{u u}.obj M) ‚ãô
  cosimplicial_object.augmented.cocomplex

/-- A functorial version of `FLC`. -/
def FLC_functor : (arrow Profinite.{u})·µí·µñ ‚•§ cochain_complex SemiNormedGroup ‚Ñï :=
simplicial_object.augmented_cech_nerve.op ‚ãô FLC_functor' M

-- Sanity checks
example : FL F M = (FL_functor M).obj (op F) := rfl
example : FLC F M = (FLC_functor M).obj (op F) := rfl

lemma _root_.cosimplicial_object.augmented.cocomplex_map_norm_noninc
  {C‚ÇÅ C‚ÇÇ : cosimplicial_object.augmented SemiNormedGroup} (f : C‚ÇÅ ‚ü∂ C‚ÇÇ)
  (hf1 : f.left.norm_noninc) (hf2 : ‚àÄ n, (f.right.app n).norm_noninc) (i : ‚Ñï) :
  ((cosimplicial_object.augmented.cocomplex.map f).f i).norm_noninc :=
begin
  cases i,
  { exact hf1 },
  { exact hf2 _ },
end

lemma FLC_functor_map_norm_noninc {f g : (arrow Profinite.{u})·µí·µñ} (Œ± : f ‚ü∂ g) (i : ‚Ñï) :
  (((FLC_functor M).map Œ±).f i).norm_noninc :=
begin
  refine cosimplicial_object.augmented.cocomplex_map_norm_noninc _ _ _ _,
  { exact SemiNormedGroup.LCC_obj_map_norm_noninc _ _ },
  { intro n,
    exact SemiNormedGroup.LCC_obj_map_norm_noninc _ _ },
end

--‚ä¢ cosimplicial_object.Œ¥
--      (functor.right_op F.cech_nerve ‚ãô (curry.obj (uncurry.obj LocallyConstant ‚ãô Completion)).obj M)
--      k =
--    Completion.map (cosimplicial_object.Œ¥ (functor.right_op F.cech_nerve ‚ãô LocallyConstant.obj M) k)

lemma FLC_iso_helper {x y : simplex_category} (f : x ‚ü∂ y) :
  (F.cech_nerve.right_op ‚ãô LCC.obj M).map f =
  Completion.map ((F.cech_nerve.right_op ‚ãô LocallyConstant.obj M).map f) :=
begin
  change Completion.map _ = _,
  congr' 1,
  dsimp [uncurry],
  erw locally_constant.map_hom_id,
  change ùüô _ ‚â´ _ = _,
  rw category.id_comp,
end

/--
This is a strict (i.e. norm-preserving) isomorphism between `FLC F M` and
the cochain complex obtained by mapping `FL F M` along the `Completion` functor.
-/
def FLC_iso : strict_iso ((Completion.map_homological_complex _).obj (FL F M)) (FLC F M) :=
{ iso := homological_complex.hom.iso_of_components
    (Œª i, nat.rec_on i (eq_to_iso rfl) (Œª _ _, eq_to_iso rfl))
    begin
      rintro (_|i) (_|j) (_|‚ü®i,w‚ü©); ext,
      { dsimp only [],
        delta FLC FL,
        dsimp only [
          cosimplicial_object.augmented.whiskering,
          cosimplicial_object.augmented.whiskering_obj,
          cosimplicial_object.augmented.to_cocomplex,
          cosimplicial_object.augmented.to_cocomplex_obj,
          cochain_complex.of,
          functor.map_homological_complex ],
        rw dif_pos rfl,
        rw dif_pos rfl,
        erw [category.id_comp, category.comp_id, category.comp_id, category.comp_id],
        dsimp only [cosimplicial_object.augmented.to_cocomplex_d,
          cosimplicial_object.augmented.drop, comma.snd, cosimplicial_object.whiskering,
          whiskering_right, cosimplicial_object.coboundary, functor.const_comp, LCC],
        simp only [quiver.hom.unop_op, arrow.augmented_cech_nerve_hom_app,
          whisker_right_app, nat_trans.comp_app, curry.obj_obj_map, category.id_comp,
          nat_trans.right_op_app, uncurry.obj_map, nat_trans.id_app,
          simplicial_object.augmented.right_op_hom,
          category_theory.functor.map_id, category_theory.functor.comp_map,
          SemiNormedGroup.LocallyConstant_obj_map, SemiNormedGroup.Completion_map],
        erw category.id_comp },
      { dsimp only [],
        delta FLC FL,
        dsimp only [
          cosimplicial_object.augmented.whiskering,
          cosimplicial_object.augmented.whiskering_obj,
          cosimplicial_object.augmented.to_cocomplex,
          cosimplicial_object.augmented.to_cocomplex_obj,
          cochain_complex.of,
          functor.map_homological_complex ],
        rw dif_pos rfl,
        rw dif_pos rfl,
        erw [category.id_comp, category.comp_id, category.comp_id, category.comp_id],
        dsimp only [
          cosimplicial_object.augmented.to_cocomplex_d,
          cosimplicial_object.augmented.drop,
          comma.snd,
          cosimplicial_object.whiskering,
          whiskering_right,
          cosimplicial_object.coboundary,
          LCC ],
        rw [Completion.map_sum],
        congr,
        funext k,
        rw [Completion.map_gsmul],
        congr' 1,
        apply FLC_iso_helper }
    end,
  is_strict := Œª i, { strict_hom' := Œª a, by { cases i; refl } } }.

open_locale simplicial

-- TODO: Move this to mathlib (also relax the has_limits condition).
/-- the isomorphism between the 0-th term of the Cech nerve and F.left-/
@[simps]
def cech_iso_zero {C : Type*} [category C] (F : arrow C) [limits.has_limits C]
  : F.cech_nerve _[0] ‚âÖ F.left :=
{ hom := limits.wide_pullback.œÄ _ ‚ü®0‚ü©,
  inv := limits.wide_pullback.lift F.hom (Œª _, ùüô _) (by simp),
  hom_inv_id' := begin
    apply limits.wide_pullback.hom_ext,
    { intro i,
      simp only [limits.wide_pullback.lift_œÄ, category.id_comp, category.comp_id, category.assoc],
      congr,
      tidy },
    { simp }
  end }

lemma augmentation_zero {C : Type*} [category C] (F : arrow C) [limits.has_limits C] :
  (cech_iso_zero F).inv ‚â´ F.augmented_cech_nerve.hom.app _ = F.hom := by tidy

lemma locally_constant_norm_empty (X : Profinite) (hX : ¬¨ nonempty X)
  (g : (LocallyConstant.obj M).obj (op X)) : ‚à• g ‚à• = 0 :=
begin
  rw locally_constant.norm_def,
  dsimp [supr],
  suffices : set.range (Œª x : ‚Ü•X, ‚à• g.to_fun x ‚à•) = ‚àÖ,
  { erw [this, real.Sup_empty],  },
  simp only [set.range_eq_empty, not_nonempty_iff],
  exact not_nonempty_iff.mp hX
end

lemma Profinite.coe_comp_apply {X Y Z : Profinite} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
  (f ‚â´ g) x = g (f x) := rfl

lemma locally_constant_to_fun_eq {X : Profinite} (f : locally_constant X M) :
  f.to_fun = f := rfl

lemma locally_constant_eq {X : Profinite} (f g : locally_constant X M) :
  f.to_fun = g.to_fun ‚Üî f = g :=
begin
  split,
  { intro h, ext, change f.to_fun _ = _, rw h, refl, },
  { intro h, rw h }
end

lemma locally_constant_eq_zero {X : Profinite} (f : locally_constant X M) :
  f = 0 ‚Üî set.range f.to_fun ‚äÜ {0} :=
begin
  split,
  { intro h, rw h, simp, },
  { intro h, ext x,
    dsimp,
    apply h,
    use x,
    refl }
end

include surj

lemma prop819_degree_zero_helper :
  function.surjective (limits.wide_pullback.base (Œª i : ulift (fin 1), F.hom)) :=
begin
  intro x,
  obtain ‚ü®x,rfl‚ü© := surj x,
  dsimp at *,
  refine ‚ü®(cech_iso_zero F).inv x, _‚ü©,
  dsimp,
  change (limits.wide_pullback.lift F.hom _ _ ‚â´ limits.wide_pullback.base _) _ = _,
  simp,
end

lemma prop819_zero_norm_le (g : (LocallyConstant.obj M).obj (op F.right)) : ‚à• g ‚à• ‚â§
  ‚à•(LocallyConstant.obj M).map (limits.wide_pullback.base (Œª i : ulift (fin 1), F.hom)).op g‚à• :=
begin
  by_cases hh : nonempty F.right,
  { apply cSup_le,
    { rcases hh with ‚ü®x‚ü©,
      refine ‚ü®‚à•g.to_fun x‚à•, x, rfl‚ü© },
    { rintros z ‚ü®z,rfl‚ü©,
      obtain ‚ü®z,rfl‚ü© := (prop819_degree_zero_helper _ surj) z,
      change ‚à•g.to_fun _‚à• ‚â§ _,
      erw ‚Üê LocallyConstant_map_apply M _ F.right (limits.wide_pullback.base (Œª i, F.hom)) g z,
      apply locally_constant.norm_apply_le } },
  { rw locally_constant_norm_empty _ _ hh g,
    simp }
end

theorem prop819_degree_zero (f : (FLC F M).X 0) (hf : (FLC F M).d 0 1 f = 0) :
  f = 0 :=
begin
  apply injective_of_strict_iso _ _ (FLC_iso F M) _ _ hf,
  intros f hf,
  have := @controlled_exactness ((FL F M).X 0) (0 : SemiNormedGroup) ((FL F M).X 1) _ _ _ 0 1
    zero_lt_one 1 ((FL F M).d _ _) _ _ 1 zero_lt_one f _,
  { rcases this with ‚ü®g,h1,h2‚ü©,
    rw ‚Üê h1,
    simp },
  { intros g hg,
    refine ‚ü®0,_, by simp‚ü©,
    change (FL F M).d 0 1 g = 0 at hg,
    dsimp,
    symmetry,
    delta FL at hg,
    dsimp only [cosimplicial_object.augmented.whiskering,
      cosimplicial_object.augmented.whiskering_obj,
      cosimplicial_object.augmented.to_cocomplex,
      cochain_complex.of] at hg,
    rw dif_pos at hg,
    swap, {simp},
    dsimp [cosimplicial_object.augmented.to_cocomplex_d] at hg,
    ext x,
    obtain ‚ü®x,rfl‚ü© := (prop819_degree_zero_helper F surj) x,
    apply_fun (Œª e, e x) at hg,
    rw locally_constant.coe_comap at hg,
    swap, { continuity },
    exact hg },
  { rintro g ‚ü®g,rfl‚ü©,
    refine ‚ü®g,rfl,_‚ü©,
    dsimp [cosimplicial_object.augmented.to_cocomplex_d],
    simp only [locally_constant.comap_hom_apply, one_mul,
      if_true, eq_self_iff_true, category.id_comp, category.comp_id],
    apply prop819_zero_norm_le _ surj },
  { exact hf }
end
.

/-- Any discrete quotient `S` of `F.left` yields a cochain complex, as follows.
First, let `T` be the maximal quotient of `F.right` such that `F.hom : F.left ‚ü∂ F.right`
descends to `S ‚Üí T`. Next construct the augmented Cech nerve of `S ‚Üí T`, and finally
apply `FL_functor M` to this augmented Cech nerve.
-/
def FLF : (discrete_quotient F.left)·µí·µñ ‚•§ cochain_complex SemiNormedGroup ‚Ñï :=
(Profinite.arrow_diagram F surj).op ‚ãô FL_functor M

/--
The diagram of cochain complexes given by `FLF F surj` fits together into a cocone
whose cocone point is defeq to `FL F M`. This is precisely this cocone.
-/
def FLF_cocone : limits.cocone (FLF F surj M) :=
(FL_functor M).map_cocone $ (Profinite.arrow_cone F surj).op

open Profinite

lemma exists_locally_constant_FLF (n : ‚Ñï) (f : (FL F M).X (n+1)) :
  ‚àÉ (S : discrete_quotient F.left) (g : ((FLF F surj M).obj (op S)).X (n+1)),
    ((FLF_cocone F surj M).Œπ.app (op S)).f _ g = f :=
begin
  have hC := Cech_cone_is_limit F surj n,
  obtain ‚ü®i,g,hg‚ü© := Profinite.exists_locally_constant _ hC f,
  use [i, g],
  exact hg.symm,
end

lemma FLF_cocone_app_coe_eq (n : ‚Ñï) (S : discrete_quotient F.left)
  (g : ((FLF F surj M).obj (op S)).X (n+1)) :
  (((FLF_cocone F surj M).Œπ.app (op S)).f _ g).to_fun =
    g.to_fun ‚àò ((Cech_cone F surj n).œÄ.app _) :=
begin
  ext x,
  change locally_constant.comap _ _ _ = _,
  rw locally_constant.coe_comap,
  swap, { continuity },
  refl,
end

lemma FLF_map_coe_eq (n : ‚Ñï) (S T : discrete_quotient F.left) (hh : T ‚â§ S)
  (g : ((FLF F surj M).obj (op S)).X (n+1)) :
  (((FLF F surj M).map (hom_of_le hh).op).f _ g).to_fun  =
  g.to_fun ‚àò ((Cech_cone_diagram F surj n).map (hom_of_le hh)) :=
begin
  ext x,
  change locally_constant.comap _ _ _ = _,
  rw locally_constant.coe_comap,
  swap, { continuity },
  refl,
end

lemma eq_zero_FLF (n : ‚Ñï) (S : discrete_quotient F.left)
  (g : ((FLF F surj M).obj (op S)).X (n+1))
  (hg : ((FLF_cocone F surj M).Œπ.app (op S)).f _ g = 0) :
  ‚àÉ (T : discrete_quotient F.left) (hT : T ‚â§ S),
    ((FLF F surj M).map (hom_of_le hT).op).f _ g = 0 :=
begin
  have := exists_image (Cech_cone_diagram F surj n)
    (Cech_cone F surj n) (Cech_cone_is_limit F surj n) S,
  obtain ‚ü®T,hT,hh‚ü© := this,
  use T, use hT,
  rw [locally_constant_eq_zero, FLF_map_coe_eq],
  rw [locally_constant_eq_zero, FLF_cocone_app_coe_eq]  at hg,
  rintro x ‚ü®x,rfl‚ü©,
  apply hg,
  let P : (Cech_cone F surj n).X ‚ü∂ (Cech_cone_diagram F surj n).obj S :=
    (Cech_cone F surj n).œÄ.app _,
  let p : (Cech_cone_diagram F surj n).obj T ‚ü∂ (Cech_cone_diagram F surj n).obj S :=
    (Cech_cone_diagram F surj n).map (hom_of_le hT),
  change ‚Ü•((Cech_cone_diagram F surj n).obj T) at x,
  have : p x ‚àà set.range p, use x,
  erw ‚Üê hh at this,
  obtain ‚ü®y,hy‚ü© := this,
  dsimp only [p] at hy,
  use y,
  dsimp only [function.comp_apply],
  erw hy,
end
.

lemma d_eq_zero_FLF (n : ‚Ñï) (S : discrete_quotient F.left)
  (g : ((FLF F surj M).obj (op S)).X (n+1))
  (hg : (FL F M).d (n+1) (n+2)
    (((FLF_cocone F surj M).Œπ.app (op S)).f _ g) = 0) :
  ‚àÉ (T : discrete_quotient F.left) (hT : T ‚â§ S),
  ((FLF F surj M).obj (op T)).d (n+1) (n+2)
    (((FLF F surj M).map $ (hom_of_le hT).op).f _ g) = 0 :=
begin
  have := ((FLF_cocone F surj M).Œπ.app (op S)).comm (n+1) (n+2),
  apply_fun (Œª e, e g) at this,
  erw this at hg,
  dsimp only [SemiNormedGroup.coe_comp] at hg,
  have := eq_zero_FLF F surj M (n+1) S _ hg,
  obtain ‚ü®T,hT,h‚ü© := this,
  use T, use hT,
  have hh := ((FLF F surj M).map (hom_of_le hT).op).comm (n+1) (n+2),
  apply_fun (Œª e, e g) at hh,
  erw ‚Üê hh at h,
  exact h,
end

lemma norm_eq_FLF (n : ‚Ñï) (S : discrete_quotient F.left)
  (g : ((FLF F surj M).obj (op S)).X (n+1)) :
  ‚àÉ (T : discrete_quotient F.left) (hT : T ‚â§ S),
    ‚à•((FLF_cocone F surj M).Œπ.app (op S)).f _ g‚à•‚Çä =
    ‚à•(((FLF F surj M)).map (hom_of_le hT).op).f _ g‚à•‚Çä :=
begin
  have := exists_image (Cech_cone_diagram F surj n)
    (Cech_cone F surj n) (Cech_cone_is_limit F surj n) S,
  obtain ‚ü®T,hT,hh‚ü© := this,
  use T, use hT,
  ext,
  dsimp,
  change Sup _ = Sup _,
  congr' 1,
  ext r,
  split,
  { rintros ‚ü®x,rfl‚ü©,
    dsimp only,
    change ‚Ü•(Cech_cone F surj n).X at x,
    use (Cech_cone F surj n).œÄ.app T x,
    dsimp only,
    rw [‚Üê locally_constant_to_fun_eq, FLF_map_coe_eq,
      function.comp_apply, ‚Üê Profinite.coe_comp_apply,
      (Cech_cone F surj n).w, ‚Üê locally_constant_to_fun_eq,
      FLF_cocone_app_coe_eq],
    refl },
  { rintros ‚ü®x,rfl‚ü©,
    dsimp only,
    change ‚Ü•((Cech_cone_diagram F surj n).obj T) at x,
    have : (Cech_cone_diagram F surj n).map (hom_of_le hT) x ‚àà
      set.range ((Cech_cone_diagram F surj n).map (hom_of_le hT)), use x,
    rw ‚Üê hh at this,
    obtain ‚ü®y,hy‚ü© := this,
    change ‚Ü•(Cech_cone F surj n).X at y,
    use y,
    dsimp only,
    simp_rw ‚Üê locally_constant_to_fun_eq,
    rw [FLF_map_coe_eq, FLF_cocone_app_coe_eq],
    dsimp only [function.comp_apply],
    erw hy }
end

lemma exists_locally_constant (n : ‚Ñï) (f : (FL F M).X (n+1))
  (hf : (FL F M).d _ (n+2) f = 0) :
  -- TODO: ‚àÉ ..., true looks a bit fuuny
  ‚àÉ (S : discrete_quotient F.left)
    (g : ((FLF F surj M).obj (op S)).X (n+1))
    (hgf : ((FLF_cocone F surj M).Œπ.app (op S)).f _ g = f)
    (hgd : (((FLF F surj M).obj (op S)).d _ (n+2) g = 0))
    (hgnorm : ‚à•f‚à•‚Çä = ‚à•g‚à•‚Çä), true :=
begin
  obtain ‚ü®S,f,rfl‚ü© := exists_locally_constant_FLF F surj M n f,
  obtain ‚ü®T1,hT1,h1‚ü© := d_eq_zero_FLF F surj M n S f hf,
  obtain ‚ü®T2,hT2,h2‚ü© := norm_eq_FLF F surj M n S f,
  let T := T1 ‚äì T2,
  have hT : T ‚â§ S := le_trans inf_le_left hT1,
  have hhT1 : T ‚â§ T1 := inf_le_left,
  have hhT2 : T ‚â§ T2 := inf_le_right,
  let g := ((FLF F surj M).map (hom_of_le hT).op).f _ f,
  let g1 := ((FLF F surj M).map (hom_of_le hT1).op).f _ f,
  let g2 := ((FLF F surj M).map (hom_of_le hT2).op).f _ f,
  have hg1 : ((FLF F surj M).map (hom_of_le hhT1).op).f _ g1 = g,
  { dsimp only [g, g1],
    have : (hom_of_le hT).op = (hom_of_le hT1).op ‚â´ (hom_of_le hhT1).op, refl,
    rw [this, functor.map_comp],
    refl },
  have hg2 : ((FLF F surj M).map (hom_of_le hhT2).op).f _ g2 = g,
  { dsimp only [g, g2],
    have : (hom_of_le hT).op = (hom_of_le hT2).op ‚â´ (hom_of_le hhT2).op, refl,
    rw [this, functor.map_comp],
    refl },
  refine ‚ü®T, g, _, _, _, trivial‚ü©,
  { rw ‚Üê (FLF_cocone F surj M).w (hom_of_le hT).op,
    refl },
  { rw ‚Üê hg1,
    have := ((FLF F surj M).map (hom_of_le hhT1).op).comm (n+1) (n+2),
    apply_fun (Œª e, e g1) at this,
    erw this, clear this,
    dsimp only [g1, SemiNormedGroup.coe_comp, function.comp_app],
    rw [h1, normed_group_hom.map_zero] },
  { apply le_antisymm,
    { dsimp only [g],
      have := (FLF_cocone F surj M).w (hom_of_le hT).op,
      rw ‚Üê this, clear this,
      apply LocallyConstant_obj_map_norm_noninc },
    { rw [‚Üê hg2, h2],
      apply LocallyConstant_obj_map_norm_noninc } }
end

lemma FLF_norm_noninc (n : ‚Ñï) (S : discrete_quotient F.left)
  (f : ((FLF F surj M).obj (op S)).X n) :
  ‚à•((FLF_cocone F surj M).Œπ.app (op S)).f _ f‚à•‚Çä ‚â§ ‚à•f‚à•‚Çä :=
begin
  cases n,
  apply LocallyConstant_obj_map_norm_noninc,
  apply LocallyConstant_obj_map_norm_noninc,
end

theorem prop819 {m : ‚Ñï} (Œµ : ‚Ñù‚â•0) (hŒµ : 0 < Œµ)
  (f : (FLC F M).X (m+1)) (hf : (FLC F M).d (m+1) (m+2) f = 0) :
  ‚àÉ g : (FLC F M).X m, (FLC F M).d m (m+1) g = f ‚àß ‚à•g‚à•‚Çä ‚â§ (1 + Œµ) * ‚à•f‚à•‚Çä :=
begin
  apply exact_of_strict_iso _ _ (FLC_iso F M) Œµ hŒµ _ _ _ hf,
  apply cmpl_exact_of_exact _ _ hŒµ,
  clear hf f m hŒµ Œµ,
  intros n f hf,
  -- We've reduced to the non-completed case.
  have := exists_locally_constant F surj M _ f hf,
  rcases this with ‚ü®S,g,rfl,h2,h3,-‚ü©,
  --let gg := ((FLF_cocone F surj M).Œπ.app (op S)).f _ g,
  let CC : Œ† (n : ‚Ñï), ((FLF F surj M).obj (op S)).X (n+1) ‚ü∂
      ((FLF F surj M).obj (op S)).X n :=
      ((Profinite.arrow_diagram F surj).obj S).contracting_homotopy
      (LocallyConstant.{u u}.obj M),
  let gc := CC _ g,
  let GG := ((FLF_cocone F surj M).Œπ.app (op S)).f _ gc,
  refine ‚ü®GG,_,_‚ü©,
  { dsimp only [GG],
    have := ((FLF_cocone F surj M).Œπ.app (op S)).comm n (n+1),
    apply_fun (Œª e, e gc) at this,
    erw this, clear this,
    change ((FLF_cocone F surj M).Œπ.app (op S)).f (n + 1) _ = _,
    congr' 1,
    change (CC n ‚â´ _) g = g,
    cases n,
    { have hh := arrow.is_contracting_homotopy_one (LocallyConstant.{u u}.obj M)
        ((Profinite.arrow_diagram F surj).obj S),
      apply_fun (Œª e, e g) at hh,
      change CC 1 (_) + _ = g at hh,
      conv at hh {
        congr,
        congr,
        erw h2 },
      rw [normed_group_hom.map_zero, zero_add] at hh,
      exact hh },
    { have hh := arrow.is_contracting_homotopy (LocallyConstant.{u u}.obj M)
        ((Profinite.arrow_diagram F surj).obj S) _,
      apply_fun (Œª e, e g) at hh,
      change CC _ (_) + _ = g at hh,
      conv at hh {
        congr,
        congr,
        erw h2 },
      rw [normed_group_hom.map_zero, zero_add] at hh,
      exact hh } },
  { rw h3,
    suffices : ‚à•GG‚à•‚Çä ‚â§ ‚à•gc‚à•‚Çä,
    { apply le_trans this _,
      cases n,
      apply LocallyConstant_obj_map_norm_noninc,
      apply LocallyConstant_obj_map_norm_noninc },
    apply FLF_norm_noninc }
end

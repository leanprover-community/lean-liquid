import algebra.homology.chain_complex
import hacks_and_tricks.by_exactI_hack
import system_of_complexes.basic
import normed_group.NormedGroup
import facts

universe variables v u
noncomputable theory
open opposite category_theory
open_locale nnreal

/-!
# Systems of double complexes of normed abelian groups

In this file we define systems of double complexes of normed abelian groups,
as needed for Definition 9.6 of [Analytic].

## Main declarations

* `system_of_double_complexes`: a system of complexes of normed abelian groups.
* `admissible`: such a system is *admissible* if all maps that occur in the system
    are norm-nonincreasing.
-/

/-- A system of double complexes of normed abelian groups, indexed by `‚Ñù‚â•0`.
See also Definition 9.3 of [Analytic].

Implementation detail: `cochain_complex` assumes that the complex is indexed by `‚Ñ§`,
whereas we are interested in complexes indexed by `‚Ñï`.
We therefore set all objects indexed by negative integers to `0`, in our use case. -/
@[derive category_theory.category]
def system_of_double_complexes : Type (u+1) :=
‚Ñù‚â•0·µí·µñ ‚•§ (cochain_complex (cochain_complex NormedGroup.{u}))

namespace system_of_double_complexes

variables (C : system_of_double_complexes)

/-- `C.X c p q` is the object $C_c^{p,q}$ in a system of double complexes `C`. -/
def X (c : ‚Ñù‚â•0) (p q : ‚Ñ§) : NormedGroup :=
((C.obj $ op c).X p).X q

/-- `C.res` is the restriction map `C.X c' p q ‚ü∂ C.X c p q` for a system of complexes `C`,
and nonnegative reals `c ‚â§ c'`. -/
def res {c' c : ‚Ñù‚â•0} {p q : ‚Ñ§} [h : fact (c ‚â§ c')] :
  C.X c' p q ‚ü∂ C.X c p q :=
((C.map (hom_of_le h).op).f p).f q

variables (c : ‚Ñù‚â•0) {c‚ÇÅ c‚ÇÇ c‚ÇÉ : ‚Ñù‚â•0} (p q : ‚Ñ§)

@[simp] lemma res_refl : @res C c c p q _ = ùüô _ :=
begin
  have := (category_theory.functor.map_id C (op $ c)),
  delta res, erw this, refl
end

@[simp] lemma res_comp_res (h‚ÇÅ : fact (c‚ÇÇ ‚â§ c‚ÇÅ)) (h‚ÇÇ : fact (c‚ÇÉ ‚â§ c‚ÇÇ)) :
  @res C _ _ p q h‚ÇÅ ‚â´ @res C _ _ p q h‚ÇÇ  = @res C _ _ p q (le_trans h‚ÇÇ h‚ÇÅ) :=
begin
  have := (category_theory.functor.map_comp C (hom_of_le h‚ÇÅ).op (hom_of_le h‚ÇÇ).op),
  rw [‚Üê op_comp] at this,
  delta res, erw this, refl,
end

@[simp] lemma res_res (h‚ÇÅ : fact (c‚ÇÇ ‚â§ c‚ÇÅ)) (h‚ÇÇ : fact (c‚ÇÉ ‚â§ c‚ÇÇ)) (x : C.X c‚ÇÅ p q) :
  @res C _ _ p q h‚ÇÇ (@res C _ _ p q h‚ÇÅ x) = @res C _ _ p q (le_trans h‚ÇÇ h‚ÇÅ) x :=
by { rw ‚Üê (C.res_comp_res p q h‚ÇÅ h‚ÇÇ), refl }

/-- `C.d` is the differential `C.X c p q ‚ü∂ C.X c (p+1) q` for a system of double complexes `C`. -/
def d {c : ‚Ñù‚â•0} {p q : ‚Ñ§} :
  C.X c p q ‚ü∂ C.X c (p+1) q :=
((C.obj $ op c).d p).f q

lemma d_comp_res (h : fact (c‚ÇÇ ‚â§ c‚ÇÅ)) :
  @d C c‚ÇÅ p q ‚â´ @res C _ _ _ _ h = @res C _ _ p q _ ‚â´ @d C c‚ÇÇ p q :=
begin
  have step1 := (homological_complex.comm_at (C.map (hom_of_le h).op) p),
  have step2 := congr_arg differential_object.hom.f step1,
  exact congr_fun step2 q
end

lemma d_res (h : fact (c‚ÇÇ ‚â§ c‚ÇÅ)) (x) :
  @d C c‚ÇÇ p q (@res C _ _ p q _ x) = @res C _ _ _ _ h (@d C c‚ÇÅ p q x) :=
show (@res C _ _ p q _ ‚â´ @d C c‚ÇÇ p q) x = (@d C c‚ÇÅ p q ‚â´ @res C _ _ _ _ h) x,
by rw d_comp_res

@[simp] lemma d_comp_d {c : ‚Ñù‚â•0} {p q : ‚Ñ§} :
  @d C c p q ‚â´ C.d = 0 :=
begin
  have step1 := (homological_complex.d_squared (C.obj $ op c)) p,
  have step2 := congr_arg differential_object.hom.f step1,
  exact congr_fun step2 q
end

@[simp] lemma d_d {c : ‚Ñù‚â•0} {p q : ‚Ñ§} (x : C.X c p q) :
  C.d (C.d x) = 0 :=
show (@d C c _ _ ‚â´ C.d) x = 0, by { rw d_comp_d, refl }

/-- `C.d'` is the differential `C.X c p q ‚ü∂ C.X c p (q+1)` for a system of double complexes `C`. -/
def d' {c : ‚Ñù‚â•0} {p q : ‚Ñ§} :
  C.X c p q ‚ü∂ C.X c p (q+1) :=
((C.obj $ op c).X p).d q

lemma d'_comp_res (h : fact (c‚ÇÇ ‚â§ c‚ÇÅ)) :
  @d' C c‚ÇÅ p q ‚â´ @res C _ _ _ _ h = @res C _ _ p q _ ‚â´ @d' C c‚ÇÇ p q :=
homological_complex.comm_at ((C.map (hom_of_le h).op).f p) q

lemma d'_res (h : fact (c‚ÇÇ ‚â§ c‚ÇÅ)) (x) :
  @d' C c‚ÇÇ p q (@res C _ _ p q _ x) = @res C _ _ _ _ h (@d' C c‚ÇÅ p q x) :=
show (@res C _ _ p q _ ‚â´ @d' C c‚ÇÇ p q) x = (@d' C c‚ÇÅ p q ‚â´ @res C _ _ _ _ h) x,
by rw d'_comp_res

@[simp] lemma d'_comp_d' {c : ‚Ñù‚â•0} {p q : ‚Ñ§} :
  @d' C c p q ‚â´ C.d' = 0 :=
((C.obj $ op c).X p).d_squared q

@[simp] lemma d'_d' {c : ‚Ñù‚â•0} {p q : ‚Ñ§} (x : C.X c p q) :
  C.d' (C.d' x) = 0 :=
show (@d' C c _ _ ‚â´ C.d') x = 0, by { rw d'_comp_d', refl }

/-- Convenience definition:
The identity morphism of an object in the system of double complexes
when it is given by different indices that are not
definitionally equal. -/
def congr {c c' : ‚Ñù‚â•0} {p p' q q' : ‚Ñ§} (hc : c = c') (hp : p = p') (hq : q = q') :
  C.X c p q ‚ü∂ C.X c' p' q' :=
eq_to_hom $ by { subst hc, subst hp, subst hq, }

/-- A system of double complexes is *admissible*
if all the differentials and restriction maps are norm-nonincreasing.

See Definition 9.3 of [Analytic]. -/
structure admissible (C : system_of_double_complexes) : Prop :=
(d_norm_noninc : ‚àÄ c p q (x : C.X c p q), ‚à•C.d x‚à• ‚â§ ‚à•x‚à•)
(d'_norm_noninc : ‚àÄ c p q (x : C.X c p q), ‚à•C.d' x‚à• ‚â§ ‚à•x‚à•)
(res_norm_noninc : ‚àÄ c' c p q h (x : C.X c' p q), ‚à•@res C c' c p q h x‚à• ‚â§ ‚à•x‚à•)

attribute [simps] differential_object.forget

/-- The `p`-th row in a system of double complexes, as system of complexes.
  It has object `(C.obj c).X p`over `c`. -/
def row (C : system_of_double_complexes.{u}) (p : ‚Ñ§) : system_of_complexes.{u} :=
C.comp ((homological_complex.forget _).comp $ pi.eval _ p)

@[simp] lemma row_X (C : system_of_double_complexes) (p q : ‚Ñ§) (c : ‚Ñù‚â•0) :
  C.row p c q = C.X c p q :=
by refl

@[simp] lemma row_res (C : system_of_double_complexes) (p q : ‚Ñ§) {c' c : ‚Ñù‚â•0} [h : fact (c ‚â§ c')] :
  @system_of_complexes.res (C.row p) _ _ q h  = @res C _ _ p q h :=
by refl

@[simp] lemma row_d (C : system_of_double_complexes) (p q : ‚Ñ§) (c : ‚Ñù‚â•0) :
  @system_of_complexes.d (C.row p) _ _ = @d' C c p q :=
by refl

/-- The `q`-th column in a system of double complexes, as system of complexes. -/
def col (C : system_of_double_complexes.{u}) (q : ‚Ñ§) : system_of_complexes.{u} :=
C.comp
  (functor.map_differential_object _
    (functor.pi $ Œª n, (homological_complex.forget _).comp $ pi.eval _ q)
    { app := Œª X, ùüô _, naturality' := by { intros, ext, simp } }
    (by { intros, ext, simp }))

@[simp] lemma col_X (C : system_of_double_complexes) (p q : ‚Ñ§) (c : ‚Ñù‚â•0) :
  C.col q c p = C.X c p q :=
by refl

@[simp] lemma col_res (C : system_of_double_complexes) (p q : ‚Ñ§) {c' c : ‚Ñù‚â•0} [h : fact (c ‚â§ c')] :
  @system_of_complexes.res (C.col q) _ _ _ _ = @res C _ _ p q h :=
by refl

@[simp] lemma col_d (C : system_of_double_complexes) (p q : ‚Ñ§) (c : ‚Ñù‚â•0) :
  @system_of_complexes.d (C.col q) _ _ = @d C c p q :=
by { dsimp [system_of_complexes.d, col, d], simp }

end system_of_double_complexes

import free_pfpng.setup

.

noncomputable theory

open_locale classical

open category_theory
open opposite

universe u

lemma Profinite.mono_free'_to_condensed_free_pfpng_aux
  (S B : Profinite.{u}) (b : B) (T : discrete_quotient S)
  (t : S.to_Condensed.val.obj (op B) ‚Üí‚ÇÄ ‚Ñ§) :
let e : S.to_Condensed.val.obj (op B) ‚Üí
    S.condensed_free_pfpng.val.obj (op B) :=
    Œª f, (S.to_condensed_free_pfpng.val.app (op B) f),
    Œπ : S.to_Condensed.val.obj (op B) ‚Üí S :=
      Œª f, (ulift.down f).1 b in
    ((limits.limit.œÄ (S.fintype_diagram ‚ãô forget Fintype ‚ãô
      AddCommGroup.free' ‚ãô Ab.ulift) T)
      (S.condensed_free_pfpng_specialize B b (free'_lift e t))).down
  = t.map_domain (T.proj ‚àò Œπ) :=
begin
  dsimp,
  revert t,
  rw ‚Üê function.funext_iff,
  dsimp,
  change ulift.down ‚àò _ = _,
  apply finsupp.fun_ext,
  { intros, simp only [function.comp_app, map_add, ulift.add_down,
      eq_self_iff_true, forall_const] },
  { intros, simp only [function.comp_app, map_zero, ulift.zero_down,
      finsupp.map_domain_add] },
  { intros,
    simp only [function.comp_app, finsupp.map_domain_single,
      free'_lift_eq_finsupp_lift],
    dsimp [Profinite.condensed_free_pfpng_specialize],
    simp only [‚Üê comp_apply],
    erw limits.limit.lift_œÄ,
    simp only [finsupp.sum_single_index, zero_smul, one_zsmul],
    ext i,
    dsimp [Profinite.condensed_free_pfpng_specialize_cone,
      finsupp.single, Profinite.free_pfpng_œÄ, Profinite.to_free_pfpng],
    erw ProFiltPseuNormGrp‚ÇÅ.limit_œÄ_coe_eq,
    simp only [‚Üê comp_apply, category.assoc],
    dsimp [Profinite.free_pfpng_level_iso,
      limits.is_limit.cone_point_unique_up_to_iso],
    simp only [‚Üê comp_apply, category.assoc],
    erw (limits.is_limit_of_preserves (ProFiltPseuNormGrp‚ÇÅ.level.obj 1)
      (limits.limit.is_limit (S.fintype_diagram ‚ãô free_pfpng_functor))).fac,
    erw limits.limit.lift_œÄ,
    refl },
end

lemma Profinite.specialization_eq_zero_of_eq_zero (S B : Profinite.{u}) (b : B)
  (t : S.to_Condensed.val.obj (op B) ‚Üí‚ÇÄ ‚Ñ§)
  (ht : free'_lift (S.to_condensed_free_pfpng.val.app (op B)) t = 0) :
  t.map_domain (Œª f, (ulift.down f).1 b) = 0 :=
begin
  apply free_pfpng.discrete_quotient_separates_points' S,
  intros T,
  apply_fun (Œª e, S.condensed_free_pfpng_specialize B b e) at ht,
  rw add_monoid_hom.map_zero at ht,
  apply_fun (Œª e, limits.limit.œÄ (S.fintype_diagram ‚ãô forget Fintype ‚ãô
    AddCommGroup.free' ‚ãô Ab.ulift) T e) at ht,
  rw add_monoid_hom.map_zero at ht,
  apply_fun ulift.down at ht,
  dsimp [AddCommGroup.free'],
  rw ‚Üê finsupp.map_domain_comp,
  have := S.mono_free'_to_condensed_free_pfpng_aux B b T t,
  dsimp at this, erw ‚Üê this, exact ht
end

lemma Profinite.adj'_hom_equiv_symm_eq_free'_lift (S B : Profinite.{u}) :
    (((AddCommGroup.adj'.whisker_right Profinite.{u}·µí·µñ).hom_equiv
      S.to_Condensed.val S.condensed_free_pfpng.val).symm
      S.to_condensed_free_pfpng.val).app (op B) =
    free'_lift (S.to_condensed_free_pfpng.val.app (op B)) :=
begin
  ext u v, dsimp [free'_lift],
  simp only [adjunction.hom_equiv_counit, whiskering_right_obj_map,
    nat_trans.comp_app, whisker_right_app,
    adjunction.whisker_right_counit_app_app],
end

open_locale big_operators
lemma finsupp.map_domain_ne_zero_of_ne_zero_of_inj_on {Œ± Œ≤ Œ≥ : Type*} [add_comm_group Œ≤]
  (t : Œ± ‚Üí‚ÇÄ Œ≤) (ht : t ‚â† 0) (f : Œ± ‚Üí Œ≥)
  (hinj : set.inj_on f t.support) :
  t.map_domain f ‚â† 0 :=
begin
  contrapose! ht,
  have : ‚àÄ (e : Œ≥) (he : e ‚àà (t.map_domain f).support), ‚àÉ (q : Œ±) (hq : q ‚àà t.support), f q = e,
  { intros e he, by_contra c, push_neg at c,
    simp only [finsupp.mem_support_iff, ne.def] at he,
    apply he,
    erw finset.sum_apply',
    apply finset.sum_eq_zero,
    intros a ha,
    dsimp [finsupp.single], rw if_neg, apply c, exact ha },
  choose q hq hh using this,
  let Œπ : (t.map_domain f).support ‚Üí t.support :=
    Œª e, ‚ü®q e.1 e.2, hq e.1 e.2‚ü©,
  have hŒπ : function.surjective Œπ,
  { rintros ‚ü®e,he‚ü©, use f e,
    { simp only [finsupp.mem_support_iff, ne.def],
      rw finsupp.map_domain_apply' _ _ (set.subset.refl _) hinj he,
      simpa using he },
    { ext, dsimp,
      apply hinj, apply hq, apply he, apply hh } },
  have : (t.map_domain f).support = ‚àÖ, by simpa using ht,
  suffices : t.support = ‚àÖ, by simpa using this,
  by_contra c, change _ ‚â† _ at c,
  erw ‚Üê finset.nonempty_iff_ne_empty at c,
  obtain ‚ü®c,hc‚ü© := c, obtain ‚ü®‚ü®c,hc‚ü©,ee‚ü© := hŒπ ‚ü®c,hc‚ü©,
  rw this at hc, simpa using hc,
end

lemma finsupp.lift_map_domain {Œ≥ Œ± Œ≤ : Type*} [add_comm_group Œ≤]
  (f : Œ± ‚Üí Œ≤) (Œπ : Œ≥ ‚Üí Œ±) :
  (finsupp.lift _ ‚Ñ§ _ f) ‚àò finsupp.map_domain Œπ = finsupp.lift _ ‚Ñ§ _ (f ‚àò Œπ) :=
begin
  apply finsupp.fun_ext,
  { intros x y,
    dsimp only [function.comp_apply],
    simp only [finsupp.map_domain_add],
    erw ((finsupp.lift Œ≤ ‚Ñ§ Œ±) f).to_add_monoid_hom.map_add, refl },
  { intros x y,
    erw ((finsupp.lift Œ≤ ‚Ñ§ Œ≥) (f ‚àò Œπ)).to_add_monoid_hom.map_add, refl },
  { intros x, simp },
end

lemma finsupp.lift_map_domain_apply {Œ≥ Œ± Œ≤ : Type*} [add_comm_group Œ≤]
  (f : Œ± ‚Üí Œ≤) (Œπ : Œ≥ ‚Üí Œ±) (e : Œ≥ ‚Üí‚ÇÄ ‚Ñ§) :
  (finsupp.lift _ ‚Ñ§ _ f).to_add_monoid_hom (e.map_domain Œπ) =
  finsupp.lift _ ‚Ñ§ _ (f ‚àò Œπ) e :=
begin
  rw ‚Üê finsupp.lift_map_domain, refl,
end

lemma finsupp.card_supp_map_domain_lt {Œ± Œ≤ Œ≥ : Type*} [add_comm_group Œ≥]
  (f : Œ± ‚Üí Œ≤) (t : Œ± ‚Üí‚ÇÄ Œ≥) (u v : Œ±)
  (huv : u ‚â† v) (hu : u ‚àà t.support) (hv : v ‚àà t.support)
  (hf : f u = f v) : (t.map_domain f).support.card < t.support.card :=
begin
  classical,
  have key : (finsupp.map_domain f t).support ‚äÜ _ := finsupp.map_domain_support,
  have : (finsupp.map_domain f t).support.card ‚â§ (t.support.image f).card :=
    finset.card_le_of_subset key,
  refine lt_of_le_of_lt this _,
  have key' : (t.support.image f).card ‚â§ t.support.card := finset.card_image_le,
  apply lt_of_le_of_ne key',
  change ¬¨ _,
  rw finset.card_image_iff,
  dsimp [set.inj_on],
  push_neg, use [u, hu, v, hv, hf],
end

lemma Profinite.mono_free'_to_condensed_free_pfpng_induction_aux (n : ‚Ñï) :
  ‚àÄ (S B : Profinite.{u}) (t : S.to_Condensed.val.obj (op B) ‚Üí‚ÇÄ ‚Ñ§),
    t.support.card ‚â§ n ‚Üí
    (free'_lift (S.to_condensed_free_pfpng.val.app (op B))) t = 0 ‚Üí
  (‚àÄ (b : ‚Ü•B), finsupp.map_domain (Œª f : S.to_Condensed.val.obj (op B),
    (ulift.down f).1 b) t = 0) ‚Üí
  (‚àÉ (Œ± : Type u) [_inst_1 : fintype Œ±] (X : Œ± ‚Üí Profinite) (œÄ : Œ† (a : Œ±), X a ‚ü∂ B)
    (surj : ‚àÄ (b : ‚Ü•B), ‚àÉ (a : Œ±) (x : ‚Ü•(X a)), (œÄ a) x = b),
    ‚àÄ (a : Œ±), finsupp.map_domain (S.to_Condensed.val.map (œÄ a).op) t = 0) :=
begin
  /-
  TODO: This proof is very slow. It would be better to pull out a few
  of the `have` statements into separate lemmas to (hopefully)
  speed this up.
  -/
  induction n,
  case nat.zero
  { intros S B t ht, simp at ht, rw ht, intros h1 h2,
    use [punit, infer_instance, Œª _, B, Œª _, ùüô _],
    split, { intros b, use [punit.star, b], refl },
    { intros _, rw finsupp.map_domain_zero, } },
  case nat.succ : n hn
  { intros S B t ht1 ht2 H,
    by_cases ht1' : t.support.card = n+1, swap,
    { apply hn, exact nat.le_of_lt_succ (nat.lt_of_le_and_ne ht1 ht1'),
      assumption' },
    clear ht1,
    let F := t.support,
    let e : F ‚Üí (B ‚ü∂ S) := Œª f, f.1.1,
    obtain ‚ü®Q,h1,h2,ee,-‚ü© : ‚àÉ (Œ± : Type u) (hŒ±1 : fintype Œ±)
      (hŒ±2 : linear_order Œ±) (ee : Œ± ‚âÉ F), true,
    { refine ‚ü®ulift (fin (fintype.card F)), infer_instance,
        is_well_order.linear_order well_ordering_rel,
        equiv.ulift.trans (fintype.equiv_fin _).symm, trivial‚ü©, },
    resetI,
    let E‚ÇÄ := { a : Q √ó Q | a.1 < a.2 },
    let X‚ÇÄ : E‚ÇÄ ‚Üí Profinite.{u} := Œª i, Profinite.equalizer (e (ee i.1.1)) (e (ee i.1.2)),
    let œÄ‚ÇÄ : Œ† (i : E‚ÇÄ), X‚ÇÄ i ‚ü∂ B := Œª i, Profinite.equalizer.Œπ _ _,

    have surj‚ÇÄ : ‚àÄ (b : B), ‚àÉ (e‚ÇÄ : E‚ÇÄ) (x : X‚ÇÄ e‚ÇÄ), œÄ‚ÇÄ _ x = b,
    { intro b, specialize H b,
      contrapose! H,
      have key : ‚àÄ (i j : Q) (h : i < j), e (ee i) b ‚â† e (ee j) b,
      { intros i j h, specialize H ‚ü®‚ü®i,j‚ü©, h‚ü©, intro c,
        specialize H, dsimp [X‚ÇÄ] at H, specialize H ‚ü®b, c‚ü©,
        apply H, refl },
      apply finsupp.map_domain_ne_zero_of_ne_zero_of_inj_on,
      { intro c, rw c at ht1', simpa using ht1' },
      { intros x hx y hy hxy, dsimp at hxy,
        let i : Q := ee.symm ‚ü®x,hx‚ü©,
        let j : Q := ee.symm ‚ü®y,hy‚ü©,
        rcases lt_trichotomy i j with (hhh|hhh|hhh),
        { specialize key i j hhh, contrapose hxy, convert key,
          { dsimp [i], rw ee.apply_symm_apply, refl },
          { dsimp [j], rw ee.apply_symm_apply, refl } },
        { apply_fun (Œª q, (ee q).1) at hhh, dsimp [i,j] at hhh,
          simp_rw ee.apply_symm_apply at hhh, exact hhh },
        { specialize key j i hhh, contrapose hxy, convert key.symm,
          { dsimp [j], rw ee.apply_symm_apply, refl },
          { dsimp [i], rw ee.apply_symm_apply, refl } } } },

    let f‚ÇÄ : Œ† (i : E‚ÇÄ), S.to_Condensed.val.obj (op B) ‚Üí S.to_Condensed.val.obj (op (X‚ÇÄ i)) :=
      Œª i, S.to_Condensed.val.map (œÄ‚ÇÄ i).op,

    let t‚ÇÄ : Œ† (i : E‚ÇÄ), S.to_Condensed.val.obj (op (X‚ÇÄ i)) ‚Üí‚ÇÄ ‚Ñ§ :=
      Œª i, t.map_domain (f‚ÇÄ i),

    have card‚ÇÄ : ‚àÄ (i : E‚ÇÄ), (t‚ÇÄ i).support.card ‚â§ n,
    { intros i, suffices : (t‚ÇÄ i).support.card < n + 1,
        by exact nat.lt_succ_iff.mp this,
      rw ‚Üê ht1',
      fapply finsupp.card_supp_map_domain_lt,
      refine (ee i.1.1).1,
      refine (ee i.1.2).1,
      { change ¬¨ _,
        erw ‚Üê subtype.ext_iff,
        apply ee.injective.ne,
        apply ne_of_lt,
        exact i.2 },
      refine (ee i.1.1).2,
      refine (ee i.1.2).2,
      { dsimp [f‚ÇÄ, œÄ‚ÇÄ, Profinite.to_Condensed], ext1, dsimp,
        -- missing Profinite.equalizer.condition
        ext t, exact t.2 } },

    have lift‚ÇÄ : ‚àÄ (i : E‚ÇÄ), free'_lift (S.to_condensed_free_pfpng.val.app (op (X‚ÇÄ i))) (t‚ÇÄ i) = 0,
    { intros i, rw free'_lift_eq_finsupp_lift, dsimp only [t‚ÇÄ, f‚ÇÄ],
      apply_fun (Œª q, S.condensed_free_pfpng.val.map (œÄ‚ÇÄ i).op q) at ht2,
      rw [add_monoid_hom.map_zero, free'_lift_eq_finsupp_lift] at ht2,
      convert ht2,
      rw finsupp.lift_map_domain_apply,
      dsimp [finsupp.lift],
      rw (S.condensed_free_pfpng.val.map (œÄ‚ÇÄ i).op).map_finsupp_sum,
      refl },

    have map‚ÇÄ : ‚àÄ (i : E‚ÇÄ) (b : ‚Ü•(X‚ÇÄ i)),
        finsupp.map_domain
          (Œª (f : S.to_Condensed.val.obj (op (X‚ÇÄ i))), f.down.to_fun b) (t‚ÇÄ i) = 0,
    { intros i b, dsimp [t‚ÇÄ], rw ‚Üê finsupp.map_domain_comp,
      exact H (œÄ‚ÇÄ i b) },

    have key := Œª i, hn S (X‚ÇÄ i) (t‚ÇÄ i) (card‚ÇÄ i) (lift‚ÇÄ i) (map‚ÇÄ i),

    choose A hA X‚ÇÅ œÄ‚ÇÅ surj‚ÇÅ key using key, resetI,

    let E := Œ£ (e : E‚ÇÄ), A e,
    let X : E ‚Üí Profinite.{u} := Œª i, X‚ÇÅ i.1 i.2,
    let œÄ : Œ† (e : E), X e ‚ü∂ B := Œª e, œÄ‚ÇÅ e.1 e.2 ‚â´ œÄ‚ÇÄ e.1,

    use [E, infer_instance, X, œÄ], split,

    { intros b,
      obtain ‚ü®e‚ÇÄ,x,hx‚ü© := surj‚ÇÄ b,
      obtain ‚ü®i,q,hq‚ü© := surj‚ÇÅ e‚ÇÄ x,
      use [‚ü®e‚ÇÄ,i‚ü©,q], dsimp [œÄ], rw [hq, hx] },
    { intros a,
      dsimp [œÄ], rw functor.map_comp,
      erw finsupp.map_domain_comp,
      apply key } },
end

instance Profinite.mono_free'_to_condensed_free_pfpng
  (S : Profinite.{u}) : mono S.free'_to_condensed_free_pfpng :=
begin
  apply presheaf_to_Condensed_Ab_map_mono_of_exists, intros B t ht,
  let e : S.to_Condensed.val.obj (op B) ‚Üí
    S.condensed_free_pfpng.val.obj (op B) :=
    Œª f, (S.to_condensed_free_pfpng.val.app (op B) f),
  dsimp at t ht,
  replace ht : free'_lift e t = 0, by rwa ‚Üê S.adj'_hom_equiv_symm_eq_free'_lift,
  let Œπ : Œ† b : B, S.to_Condensed.val.obj (op B) ‚Üí S :=
    Œª b f, (ulift.down f).1 b,
  have aux : ‚àÄ b : B, t.map_domain (Œπ b) = 0 :=
    Œª b, S.specialization_eq_zero_of_eq_zero B b t ht,
  dsimp,
  apply Profinite.mono_free'_to_condensed_free_pfpng_induction_aux,
  refl,
  assumption',
end

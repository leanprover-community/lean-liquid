import data.fintype.intervals
import analysis.specific_limits
import category_theory.Fintype
import analysis.normed_space.basic

import pseudo_normed_group.basic
import pseudo_normed_group.category

universe u

noncomputable theory
open_locale big_operators nnreal classical
open set

instance (kâ‚ kâ‚‚ : â„¤) : fintype (Icc kâ‚ kâ‚‚) := (Icc_â„¤_finite _ _).some

structure laurent_measures_bdd (r : â„â‰¥0) (S : Fintype) (T : finset â„¤) (c : â„â‰¥0) :=
(to_fun : S â†’ T â†’ â„¤)
(bound' : âˆ‘ s i, âˆ¥ to_fun s i âˆ¥ * (r : â„) ^ (i : â„¤) â‰¤ c)

namespace laurent_measures_bdd

variables {r : â„â‰¥0} {S S' S'' : Fintype.{u}} {T : finset â„¤} {c : â„â‰¥0}

instance : has_coe_to_fun (laurent_measures_bdd r S T c) :=
âŸ¨Î» _, S â†’ T â†’ â„¤, Î» F, F.1âŸ©

instance : has_norm (laurent_measures_bdd r S T c) :=
âŸ¨Î» F, âˆ‘ s i, âˆ¥ F s i âˆ¥ * (r : â„)^(i : â„¤)âŸ©

@[ext]
lemma ext (F G : laurent_measures_bdd r S T c) :
  (F : S â†’ T â†’ â„¤) = G  â†’ F = G := by {intros h, cases F, cases G, simpa }

@[simp]
lemma norm_def (F : laurent_measures_bdd r S T c) : âˆ¥ F âˆ¥ =
  âˆ‘ s i, âˆ¥ F s i âˆ¥ * (r : â„)^(i : â„¤) := rfl

lemma bound (F : laurent_measures_bdd r S T c) :
  âˆ¥ F âˆ¥ â‰¤ c := F.2

def map (f : S âŸ¶ S') : laurent_measures_bdd r S T c â†’ laurent_measures_bdd r S' T c := Î» F,
{ to_fun := Î» s' k, âˆ‘ s in finset.univ.filter (Î» t, f t = s'), F s k,
  bound' := calc
  âˆ‘ (s : S') (i : T),
    âˆ¥âˆ‘ (s : S.Î±) in finset.univ.filter (Î» (t : S), f t = s), F s iâˆ¥ * (r : â„)^(i : â„¤) â‰¤
  âˆ‘ (s' : S') (i : T), âˆ‘ s in finset.univ.filter (Î» t, f t = s'), âˆ¥ F s i âˆ¥ * (r : â„)^(i : â„¤) :
  begin
    apply finset.sum_le_sum,
    intros s' hs',
    apply finset.sum_le_sum,
    intros i hi,
    rw â† finset.sum_mul,
    refine mul_le_mul _ (le_refl _) (fpow_nonneg (nnreal.coe_nonneg _) _)
      (finset.sum_nonneg $ Î» _ _, norm_nonneg _),
    apply norm_sum_le,
  end
  ... =
    âˆ‘ (s' : S'), âˆ‘ s in finset.univ.filter (Î» t, f t = s'), âˆ‘ i, âˆ¥ F s i âˆ¥ * (r : â„)^(i : â„¤) :
  begin
    apply finset.sum_congr rfl,
    intros s' hs',
    rw finset.sum_comm,
  end
  ... = âˆ‘ s, âˆ‘ i, âˆ¥ F s i âˆ¥ * (r : â„)^(i : â„¤) :
  begin
    rw â† finset.sum_bUnion,
    { apply finset.sum_congr,
      { ext e,
        split,
        { simp },
        { intro h,
          simp only [true_and, finset.mem_univ,
            finset.mem_bUnion, exists_true_left, finset.mem_filter],
          use f e,
          simp } },
      { tauto } },
    { intros x hx y hy h i hi,
      apply h,
      simp at hi,
      rw [â† hi.1, â† hi.2] }
  end
  ... â‰¤ c : F.bound }

@[simp]
lemma map_apply (f : S âŸ¶ S') (F : laurent_measures_bdd r S T c) (s' : S') (t : T) :
  map f F s' t = âˆ‘ s in finset.univ.filter (Î» i, f i = s'), F s t := rfl

@[simp]
lemma map_id : (map (ğŸ™ S) : laurent_measures_bdd r S T c â†’ laurent_measures_bdd r S T c) = id :=
begin
  ext F s t,
  dsimp,
  change âˆ‘ s in finset.univ.filter (Î» i, i = s), F s t = _,
  simp [finset.sum_filter],
end

@[simp]
lemma map_comp (f : S âŸ¶ S') (g : S' âŸ¶ S'') :
  (map (f â‰« g) : laurent_measures_bdd r S T c â†’ laurent_measures_bdd r S'' T c) = map g âˆ˜ map f :=
begin
  ext F s t,
  simp,
  rw â† finset.sum_bUnion,
  { apply finset.sum_congr,
    { ext x,
      split,
      { intro h, simpa using h },
      { intro h, simpa using h } },
    { tauto } },
  { intros i hi j hj h e he,
    simp at he,
    apply h,
    rw [â† he.1, â† he.2] }
end

lemma coeff_bound (F : laurent_measures_bdd r S T c) [hr : fact (0 < r)]
  (s : S) (i : T) : âˆ¥ F s i âˆ¥ â‰¤ c * ((r : â„)^(i : â„¤))â»Â¹ :=
begin
  suffices : âˆ¥ F s i âˆ¥ * (r : â„)^(i : â„¤) â‰¤ c,
  { have hh : 0 < ((r : â„)^(i : â„¤))â»Â¹,
    { rw [inv_pos],
      refine fpow_pos_of_pos _ _,
      exact_mod_cast hr.out },
    have hh' : (r : â„)^(i : â„¤) â‰  0,
    { apply fpow_ne_zero,
      apply ne_of_gt,
      exact_mod_cast hr.out },
    convert mul_le_mul this (le_refl _) (le_of_lt hh) _,
    { field_simp [this] },
    exact nnreal.coe_nonneg c },
  refine le_trans _ F.bound,
  have : âˆ‘ i, âˆ¥ F s i âˆ¥ * (r : â„)^(i : â„¤) â‰¤ âˆ¥ F âˆ¥,
  { apply @finset.single_le_sum S â„ _ (Î» s, âˆ‘ i, âˆ¥ F s i âˆ¥ * (r : â„)^(i : â„¤)),
    { rintros s -,
      apply finset.sum_nonneg,
      rintros i -,
      refine mul_nonneg (norm_nonneg _) (fpow_nonneg _ _),
      exact nnreal.coe_nonneg r },
    { simp } },
  refine le_trans _ this,
  apply @finset.single_le_sum T â„ _ (Î» i, âˆ¥ F s iâˆ¥ * (r : â„)^(i : â„¤)),
  { rintros i -,
    refine mul_nonneg (norm_nonneg _) (fpow_nonneg _ _),
    exact nnreal.coe_nonneg r },
  { simp }
end

open_locale classical

instance (r : â„â‰¥0) [fact (0 < r)] (S : Fintype) (T : finset â„¤) :
  fintype (laurent_measures_bdd r S T c) :=
begin
  let lb : T â†’ â„¤ := Î» i, floor (-((c : â„) * ((r : â„)^(i : â„¤))â»Â¹)),
  let ub : T â†’ â„¤ := Î» i, ceil ((c : â„) * ((r : â„)^(i : â„¤))â»Â¹),
  let Î¹ : laurent_measures_bdd r S T c â†’
    (Î  (s : S) (i : T), Icc (lb i) (ub i)) :=
    Î» F s i, âŸ¨F s i, _âŸ©,
  apply fintype.of_injective Î¹ _,
  { intros F G h,
    ext s i,
    apply_fun (Î» e, (e s i : â„¤)) at h,
    exact h },
  { have := F.coeff_bound s i,
    change (abs (F s i) : â„) â‰¤ _ at this,
    rw abs_le at this,
    split,
    { exact_mod_cast le_trans (floor_le _) this.1 },
    { exact_mod_cast le_trans this.2 (le_ceil _) } }
end

instance : topological_space (laurent_measures_bdd r S T c) := âŠ¥

example [fact (0 < r)] : compact_space (laurent_measures_bdd r S T c) :=
  by apply_instance

example : t2_space (laurent_measures_bdd r S T c) := by apply_instance

example : totally_disconnected_space (laurent_measures_bdd r S T c) :=
  by apply_instance

end laurent_measures_bdd

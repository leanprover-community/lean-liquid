import analysis.normed_space.banach
import analysis.mean_inequalities

/-!
# p-Banach spaces

A `p`-Banach space is just like an ordinary Banach space,
except that the axiom `âˆ¥c â€¢ vâˆ¥ = âˆ¥câˆ¥ * âˆ¥vâˆ¥` is replaced by `âˆ¥c â€¢ vâˆ¥ = âˆ¥câˆ¥^p * âˆ¥vâˆ¥`

In this file, we define `p`-normed spaces, called `normed_space'`,
and we prove that every `p`-normed space is also `p'`-normed, for `0 < p' â‰¤ p`.

-/

noncomputable theory

open_locale nnreal

-- move this
lemma real.add_rpow_le {x y r : â„}
  (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) (h0r : 0 â‰¤ r) (hr1 : r â‰¤ 1) :
  (x + y)^r â‰¤ x^r + y^r :=
begin
  by_cases hr : 0 = r,
  { subst r, simp only [zero_le_one, real.rpow_zero, le_add_iff_nonneg_left], },
  let x' : â„â‰¥0 := âŸ¨x, hxâŸ©,
  let y' : â„â‰¥0 := âŸ¨y, hyâŸ©,
  exact_mod_cast ennreal.rpow_add_le_add_rpow x' y' (lt_of_le_of_ne h0r hr) hr1,
end

set_option extends_priority 920
-- Here, we set a rather high priority for the instance `[normed_space Î± Î²] : module Î± Î²`
-- to take precedence over `semiring.to_module` as this leads to instance paths with better
-- unification properties.
/-- A normed space over a normed field is a vector space endowed with a norm which satisfies the
equality `âˆ¥c â€¢ xâˆ¥ = âˆ¥câˆ¥ âˆ¥xâˆ¥`. We require only `âˆ¥c â€¢ xâˆ¥ â‰¤ âˆ¥câˆ¥ âˆ¥xâˆ¥` in the definition, then prove
`âˆ¥c â€¢ xâˆ¥ = âˆ¥câˆ¥ âˆ¥xâˆ¥` in `norm_smul`. -/
class normed_space' (ð•œ : Type*) (p : out_param â„) (V : Type*)
  [normed_field ð•œ] [normed_group V] [module ð•œ V] :=
(norm_smul : âˆ€ (c:ð•œ) (v:V), âˆ¥c â€¢ vâˆ¥ = âˆ¥câˆ¥^p * âˆ¥vâˆ¥)

@[priority 100]
instance normed_space.normed_space'
  (ð•œ : Type*) (V : Type*) [normed_field ð•œ] [normed_group V] [normed_space ð•œ V] :
  normed_space' ð•œ 1 V :=
{ norm_smul := Î» c k, by simp only [real.rpow_one, norm_smul] }

/-- A type alias: `as_normed_space' p' V` is a `p'`-normed space over `ð•œ`,
when `V` is a `p`-normed space over `ð•œ` and `0 < p' â‰¤ p`. -/
@[nolint unused_arguments]
def as_normed_space' (p' : â„) (V : Type*) := V

namespace as_normed_space'

instance (p' : â„) (V : Type*) [i : inhabited V] : inhabited (as_normed_space' p' V) := i

/-- The identity map `V â†’ as_normed_space' p' V`. -/
def up (p' : â„) {V : Type*} (v : V) : as_normed_space' p' V := v

/-- The identity map `as_normed_space' p' V â†’ V`. -/
def down {p' : â„} {V : Type*} (v : as_normed_space' p' V) : V := v

instance (p' : â„) (V : Type*) [i : add_comm_group V] : add_comm_group (as_normed_space' p' V) := i

instance (p' : â„) (ð•œ V : Type*) [ring ð•œ] [add_comm_group V] [i : module ð•œ V] :
  module ð•œ (as_normed_space' p' V) := i

@[simp] lemma down_add {p' : â„} {V : Type*} [add_comm_group V] (v w : as_normed_space' p' V) :
  (v+w).down = v.down + w.down := rfl

@[simp] lemma down_neg {p' : â„} {V : Type*} [add_comm_group V] (v : as_normed_space' p' V) :
  (-v).down = - v.down := rfl

@[simp] lemma down_smul {p' : â„} {ð•œ V : Type*} [ring ð•œ] [add_comm_group V] [module ð•œ V]
  (c : ð•œ) (v : as_normed_space' p' V) :
  (c â€¢ v).down = c â€¢ v.down := rfl

/-- The natural `p'`-norm on `as_normed_space' p' V` induced by a `p`-norm on `V`. -/
protected def has_norm (p' p : â„) (V : Type*) [has_norm V] :
  has_norm (as_normed_space' p' V) :=
âŸ¨Î» v, âˆ¥v.downâˆ¥^(p'/p)âŸ©

lemma norm_def {V : Type*} [has_norm V] (p' p : â„) (v : as_normed_space' p' V) :
  @has_norm.norm _ (as_normed_space'.has_norm p' p V) v = âˆ¥v.downâˆ¥^(p'/p) := rfl

/-- The natural `p'`-normed group structure on `as_normed_space' p' V`
induced by a `p`-normed group structure on `V` -/
protected def normed_group (V : Type*) [normed_group V] (p' p : â„) [fact (0 < p')] [fact (p' â‰¤ p)] :
  normed_group (as_normed_space' p' V) :=
@normed_group.of_core _ _ (as_normed_space'.has_norm p' p V) $
have hp' : 0 < p'   := fact.out _,
have hp  : 0 < p    := lt_of_lt_of_le hp' (fact.out _),
have H   : 0 < p'/p := div_pos hp' hp,
{ norm_eq_zero_iff := Î» v, show âˆ¥v.downâˆ¥^(p'/p) = 0 â†” v = 0,
  by simpa only [real.rpow_eq_zero_iff_of_nonneg (norm_nonneg v.down), norm_eq_zero,
        H.ne', and_true, ne.def, not_false_iff],
  triangle := Î» v w, show âˆ¥(v+w).downâˆ¥^(p'/p) â‰¤ âˆ¥v.downâˆ¥^(p'/p) + âˆ¥w.downâˆ¥^(p'/p),
  begin
    rw [down_add],
    calc âˆ¥v.down + w.downâˆ¥ ^ (p' / p)
        â‰¤ (âˆ¥v.downâˆ¥ + âˆ¥w.downâˆ¥) ^ (p' / p) : real.rpow_le_rpow (norm_nonneg _) (norm_add_le _ _) H.le
    ... â‰¤ âˆ¥v.downâˆ¥ ^ (p' / p) + âˆ¥w.downâˆ¥ ^ (p' / p) :
      real.add_rpow_le (norm_nonneg _) (norm_nonneg _) H.le _,
    rw [div_le_iff hp, one_mul],
    exact fact.out _
  end,
  norm_neg := Î» v, show âˆ¥(-v).downâˆ¥^(p'/p) = âˆ¥v.downâˆ¥^(p'/p), by rw [down_neg, norm_neg] }

local attribute [instance] as_normed_space'.normed_group

instance (ð•œ : Type*) (V : Type*) [normed_field ð•œ] [normed_group V] [module ð•œ V]
  (p' p : â„) [fact (0 < p')] [fact (p' â‰¤ p)] [normed_space' ð•œ p V] :
  normed_space' ð•œ p' (as_normed_space' p' V) :=
{ norm_smul := Î» c v,
  begin
    have hp' : 0 < p'   := fact.out _,
    have hp  : 0 < p    := lt_of_lt_of_le hp' (fact.out _),
    rw [norm_def, norm_def, down_smul, normed_space'.norm_smul, real.mul_rpow, â† real.rpow_mul,
      mul_div_cancel' _ hp.ne'],
    { exact norm_nonneg _ },
    { exact real.rpow_nonneg_of_nonneg (norm_nonneg _) _ },
    { exact norm_nonneg _ },
  end }

end as_normed_space'

variables (p : â„â‰¥0)

structure pBanach :=
(V : Type*)
(is_normed_group : normed_group V)
(is_module : module â„ V)
(is_normed_space' : normed_space' â„ p V)

namespace pBanach

instance : has_coe_to_sort (pBanach p) :=
{ S := Type*,
  coe := Î» X, X.V }

instance (X : pBanach p) : normed_group X := X.is_normed_group
instance (X : pBanach p) : module â„ X := X.is_module
instance (X : pBanach p) : normed_space' â„ p X := X.is_normed_space'

end pBanach
